MODULE library

CONTAINS

SUBROUTINE mesh_ensi(argv,nlen,g_coord,g_num,element,etype,nf,loads,          &
                     nstep,npri,dtim,solid)
!
! This subroutine outputs a set of files in the Ensight gold format.
! Models in this format can be viewed in the visualisation tool ParaView.
! 
! Element types supported:                Tested with:
!
! 2-node bar
! 3-node triangle                         p51  (4th edition p51_1.dat)
! 6-node triangle
! 4-node quadrilateral                    p115 (4th edition)
! 8-node quadrilateral                    p116 (4th edition)
! 4-node tetrahedron                      p54  (4th edition p54_2.dat)
! 8-node hexahedron                       p86  (4th edition)       
! 20-node hexahedron                      p55  (4th edition)
 
  IMPLICIT none

  INTEGER,PARAMETER             :: iwp=SELECTED_REAL_KIND(15)
  INTEGER,   INTENT(IN)         :: nlen,nstep,npri
  INTEGER,   INTENT(IN)         :: g_num(:,:),etype(:),nf(:,:)
  INTEGER                       :: i,j,k,l,m,n,nfe,nod,nels,ndim,nn
  INTEGER                       :: prnwidth,remainder
  REAL(iwp), INTENT(IN)         :: g_coord(:,:),loads(:),dtim
  CHARACTER(LEN=15), INTENT(IN) :: argv,element  
  LOGICAL, INTENT(IN)           :: solid
!------------------------------------------------------------------------------
! 1. Initialisation
!------------------------------------------------------------------------------

  nn   = UBOUND(g_coord,2) ; ndim = UBOUND(g_coord,1)
  nels = UBOUND(g_num,2)   ; nod  = UBOUND(g_num,1)

!------------------------------------------------------------------------------
! 2. Write case file
!------------------------------------------------------------------------------

  OPEN(12,FILE=argv(1:nlen)//'.ensi.case')

  WRITE(12,'(A/A)')    "#", "# Post-processing file generated by subroutine &
                             &WRITE_ENSI in "
  WRITE(12,'(A,A,/A)') "#", " Smith, Griffiths and Margetts, 'Programming the &
                             &Finite Element Method',","# Wiley, 2013."        
  WRITE(12,'(A/A/A)')  "#","# Ensight Gold Format","#"
  WRITE(12,'(2A/A)')   "# Problem name: ",argv(1:nlen),"#"
  WRITE(12,'(A/A/A)')  "FORMAT","type:  ensight gold","GEOMETRY"
  WRITE(12,'(2A/A)')   "model: 1  ",argv(1:nlen)//'.ensi.geo',"VARIABLE"
  WRITE(12,'(2A)')     "scalar per element:  material      ",                 &
                        argv(1:nlen)//'.ensi.MATID'
  IF(solid) THEN
    WRITE(12,'(2A)')   "scalar per node:     restraint     ",                 &
                        argv(1:nlen)//'.ensi.NDBND'
    WRITE(12,'(2A)')   "vector per node:     displacement  ",                 &
                        argv(1:nlen)//'.ensi.DISPL-*****'
  ELSE
    WRITE(12,'(2A)')   "scalar per node:     pressure      ",                 &
                        argv(1:nlen)//'.ensi.PRESSURE-*****'
  END IF
  WRITE(12,'(2A)')     "vector per node:     load          ",                 &
                        argv(1:nlen)//'.ensi.NDLDS'
  WRITE(12,'(A/A)')     "TIME","time set:     1"
  WRITE(12,'(A,I5)')    "number of steps:",nstep/npri
  WRITE(12,'(A,I5)')    "filename start number:",npri
  WRITE(12,'(A,I5)')    "filename increment:",npri
  WRITE(12,'(A)')       "time values:"
  prnwidth  = 5
  remainder = mod(nstep/npri,prnwidth)
  n         = ((nstep/npri) - remainder)/prnwidth
  IF(nstep/npri<=prnwidth) THEN
    DO i=1,nstep,npri
      IF(i==nstep) THEN
        WRITE(12,'(E12.5)') i*dtim
      ELSE
        WRITE(12,'(E12.5)',ADVANCE='no') i*dtim
      END IF
    END DO
  ELSE
    IF(remainder==0) THEN
      DO j=1,n
        m = ((j-1)*prnwidth)+1
        l = ((j-1)*prnwidth)+prnwidth
        WRITE(12,'(5E12.5)') (k*dtim,k=m,l)
      END DO
    ELSE
!     DO j=1,n-1
      DO j=1,n
        m = ((j-1)*prnwidth)+1
        l = ((j-1)*prnwidth)+prnwidth
        WRITE(12,'(5E12.5)') (k*dtim,k=m,l)
      END DO
      m = (n*prnwidth)+1
      l = (n*prnwidth)+remainder
      DO i=m,l
        IF(i==l) THEN
          WRITE(12,'(E12.5)') dtim*i
        ELSE
          WRITE(12,'(E12.5)',ADVANCE='no') dtim*i
        END IF
      END DO
    END IF
  END IF
 
  CLOSE(12)

!------------------------------------------------------------------------------
! 3. Write geometry file
!------------------------------------------------------------------------------

  OPEN(13,FILE=argv(1:nlen)//'.ensi.geo')
  WRITE(13,'(/2A)')   "Problem name: ", argv(1:nlen)
  WRITE(13,'(A/A/A)') "Geometry files","node id given","element id given"
  WRITE(13,'(A/A)')   "part","      1"
  IF(ndim==2) WRITE(13,'(A)') "2d-mesh"
  IF(ndim==3) WRITE(13,'(A)') "Volume Mesh"
  WRITE(13,'(A)')     "coordinates"
  
  WRITE(13,'(I10)') nn
  DO j=1,ndim
    DO i=1,nn  
      WRITE(13,'(E12.5)') g_coord(j,i)
    END DO
  END DO

  IF(ndim==2) THEN ! ensight requires zeros for the z-ordinate
    DO i=1,nn
      WRITE(13,'(A)') " 0.00000E+00"
    END DO
  END IF

  SELECT CASE(element)
    CASE('triangle')
      SELECT CASE(nod)
        CASE(3)
          WRITE(13,'(A/I10)') "tria3", nels
          DO i = 1,nels
            WRITE(13,'(3I10)')g_num(3,i),g_num(2,i),g_num(1,i)
          END DO
        CASE DEFAULT
          WRITE(13,'(A)')   "# Element type not recognised"
      END SELECT
    CASE('quadrilateral')
      SELECT CASE(nod)
        CASE(4)
          WRITE(13,'(A/I10)') "quad4", nels
          DO i = 1,nels
            WRITE(13,'(4I10)')g_num(1,i),g_num(4,i),g_num(3,i),g_num(2,i)
          END DO
        CASE(8)
          WRITE(13,'(A/I10)') "quad8", nels
          DO i = 1,nels
            WRITE(13,'(8I10)')g_num(1,i),g_num(7,i),g_num(5,i),g_num(3,i),    &
                              g_num(8,i),g_num(6,i),g_num(4,i),g_num(2,i)
          END DO
        CASE DEFAULT
          WRITE(13,'(A)')   "# Element type not recognised"
      END SELECT
    CASE('hexahedron')
      SELECT CASE(nod)
        CASE(8)
          WRITE(13,'(A/I10)') "hexa8", nels
          DO i = 1,nels
            WRITE(13,'(8I10)') g_num(1,i),g_num(4,i),g_num(8,i),g_num(5,i),   &
                               g_num(2,i),g_num(3,i),g_num(7,i),g_num(6,i)
          END DO
        CASE(20)
          WRITE(13,'(A/I10)') "hexa20", nels
          DO i = 1,nels
            WRITE(13,'(20I10)')                                               &
              g_num(1,i), g_num(7,i), g_num(19,i),g_num(13,i),g_num(3,i),     &
              g_num(5,i), g_num(17,i),g_num(15,i),g_num(8,i), g_num(12,i),    &
              g_num(20,i),g_num(9,i), g_num(4,i), g_num(11,i),g_num(16,i),    &
              g_num(10,i),g_num(2,i), g_num(6,i), g_num(18,i),g_num(14,i) 
          END DO
        CASE DEFAULT
          WRITE(13,'(A)')   "# Element type not recognised"
      END SELECT
    CASE('tetrahedron')
      SELECT CASE(nod)
        CASE(4)
          WRITE(13,'(A/I10)') "tetra4", nels
          DO i = 1,nels
            WRITE(13,'(4I10)') g_num(1,i),g_num(3,i),g_num(2,i),g_num(4,i)
          END DO
        CASE DEFAULT
          WRITE(13,'(A)')   "# Element type not recognised"
      END SELECT
    CASE DEFAULT
      WRITE(13,'(A)')       "# Element type not recognised"
  END SELECT

  CLOSE(13)

!------------------------------------------------------------------------------
! 4. Write file containing material IDs
!------------------------------------------------------------------------------

  OPEN(14,FILE=argv(1:nlen)//'.ensi.MATID')
  WRITE(14,'(A)') "Alya Ensight Gold --- Scalar per-element variable file"
  WRITE(14,'(A/A)') "part", "      1"

  SELECT CASE(element)
    CASE('triangle')
      SELECT CASE(nod) 
        CASE(3)
          WRITE(14,'(A)') "tria3"
        CASE DEFAULT
          WRITE(14,'(A)') "# Element type not recognised"
      END SELECT
    CASE('quadrilateral')
      SELECT CASE(nod) 
        CASE(4)
          WRITE(14,'(A)') "quad4"
        CASE(8)
          WRITE(14,'(A)') "quad8"
        CASE DEFAULT
          WRITE(14,'(A)') "# Element type not recognised"
      END SELECT
    CASE('hexahedron')
      SELECT CASE(nod) 
        CASE(8)
          WRITE(14,'(A)') "hexa8"
        CASE(20)
          WRITE(14,'(A)') "hexa20"
        CASE DEFAULT
          WRITE(14,'(A)') "# Element type not recognised"
      END SELECT
    CASE('tetrahedron')
      SELECT CASE(nod)
        CASE(4)
          WRITE(14,'(A)') "tetra4"
        CASE DEFAULT
        WRITE(14,'(A)') "# Element type not recognised"
      END SELECT
    CASE DEFAULT
      WRITE(14,'(A)')   "# Element type not recognised"
  END SELECT
 
  DO i=1,nels; WRITE(14,'(I10)') etype(i); END DO

  WRITE(14,'(A)')

  CLOSE(14)

!------------------------------------------------------------------------------
! 5. Write boundary conditions. Encoded using formula: 4z + 2y + 1x
!
!    110 = 1   010 = 2   100 = 3   011 = 4   101 = 5   001 = 6   000 = 7
!------------------------------------------------------------------------------

  IF(solid) THEN
    OPEN(15,FILE=argv(1:nlen)//'.ensi.NDBND')
    WRITE(15,'(A)')     "Alya Ensight Gold --- Scalar per-node variable file"
    WRITE(15,'(A/A/A)') "part", "      1","coordinates"
    IF(ndim==3) THEN
      DO i=1,UBOUND(g_coord,2) 
        nfe=0
        IF(nf(1,i)==0) nfe=nfe+1
        IF(nf(2,i)==0) nfe=nfe+2
        IF(nf(3,i)==0) nfe=nfe+4
        WRITE(15,'(I2)') nfe
      END DO
    ELSE IF(ndim==2) THEN
      DO i=1,nn
        nfe=0
        IF(nf(1,i)==0) nfe=nfe+1
        IF(nf(2,i)==0) nfe=nfe+2
        WRITE(15,'(I2)') nfe
      END DO
    ELSE
      PRINT *, "Wrong number of dimensions in mesh_ensi"
    END IF   
  END IF

  CLOSE(15)

!------------------------------------------------------------------------------
! 6. Write loaded nodes
!------------------------------------------------------------------------------

  OPEN(16,FILE=argv(1:nlen)//'.ensi.NDLDS')
  WRITE(16,'(A)')     "Alya Ensight Gold --- Vector per-node variable file"
  WRITE(16,'(A/A/A)') "part", "      1","coordinates"
  DO j=1,UBOUND(nf,1)
    DO i=1, UBOUND(nf,2)
      WRITE(16,'(E12.5)') loads(nf(j,i))
    END DO
  END DO
  CLOSE(16)

  RETURN

END SUBROUTINE mesh_ensi

SUBROUTINE dismsh_ensi(argv,nlen,step,nf,loads)
!
! This subroutine outputs displacements in the Ensight gold format for 
! visualization in ParaView. ParaView also requires the output of subroutine
! mesh_ensi for the geometry.
!  
  IMPLICIT none

  INTEGER,PARAMETER             :: iwp=SELECTED_REAL_KIND(15)
  INTEGER,   INTENT(IN)         :: nlen,step,nf(:,:)
  INTEGER                       :: i,j
  REAL(iwp), INTENT(IN)         :: loads(:)
  CHARACTER(LEN=15), INTENT(IN) :: argv  
  CHARACTER(LEN=5)              :: ch

  WRITE(ch,'(I5.5)') step ! convert integer to string using internal file

  OPEN(17,FILE=argv(1:nlen)//'.ensi.DISPL-'//ch)

  WRITE(17,'(A)') "Alya Ensight Gold --- Vector per-node variable file"
  WRITE(17,'(A/A/A)') "part", "      1","coordinates"

  DO i=1,UBOUND(nf,1)
    DO j=1,UBOUND(nf,2)
      WRITE(17,'(E12.5)') loads(nf(i,j))
    END DO
  END DO

  IF(UBOUND(nf,1)==2) THEN ! ensight requires zeros for the z-ordinate
    DO i=1,UBOUND(nf,2)
      WRITE(17,'(A)') " 0.00000E+00"
    END DO
  END IF
  
  CLOSE(17)

  RETURN

END SUBROUTINE dismsh_ensi

SUBROUTINE pmsh_ensi(argv,nlen,step,loads)
!
! This subroutine outputs pressure in the Ensight gold format for 
! visualization in ParaView. ParaView also requires the output of subroutine
! mesh_ensi for the geometry.
!  
  IMPLICIT none

  INTEGER,PARAMETER             :: iwp=SELECTED_REAL_KIND(15)
  INTEGER,   INTENT(IN)         :: nlen,step
  INTEGER                       :: i
  REAL(iwp), INTENT(IN)         :: loads(:)
  CHARACTER(LEN=15), INTENT(IN) :: argv  
  CHARACTER(LEN=5)              :: ch

  WRITE(ch,'(I5.5)') step ! convert integer to string using internal file

  OPEN(17,FILE=argv(1:nlen)//'.ensi.PRESSURE-'//ch)

  WRITE(17,'(A)') "Alya Ensight Gold --- Scalar per-node variable file"
  WRITE(17,'(A/A/A)') "part", "      1","coordinates"

  DO i=1,UBOUND(loads,1)
    WRITE(17,'(E12.5)') loads(i)
  END DO

  CLOSE(17)

  RETURN

END SUBROUTINE pmsh_ensi

SUBROUTINE bee8(bee,coord,xi,eta,det)
!
! Analytical version of the bee matrix for an 8-node quad      
!
 IMPLICIT NONE
 INTEGER,PARAMETER::iwp=SELECTED_REAL_KIND(15)
 REAL(iwp),INTENT(IN)::coord(:,:),xi,eta
 REAL(iwp),INTENT(OUT)::bee(:,:),det
 REAL::x1,x2,x3,x4,x5,x6,x7,x8,y1,y2,y3,y4,y5,y6,y7,y8,xi2,xi3,et2,et3,   &
   xi2et2,xi1et1,xi2et1,xi1et2,xy12,xy13,xy14,xy15,xy16,xy17,xy18,xy23,   &
   xy24,xy25,xy26,xy27,xy28,xy34,xy35,xy36,xy37,xy38,xy45,xy46,xy47,xy48, &
   xy56,xy57,xy58,xy67,xy68,xy78,xcu,xsq,xon,ecu,esq,eon,x2e2,x1e1,x2e1,  &
   x1e2,cons,bot,top,dn1dx,dn2dx,dn3dx,dn4dx,dn5dx,dn6dx,dn7dx,dn8dx,     &
   dn1dy,dn2dy,dn3dy,dn4dy,dn5dy,dn6dy,dn7dy,dn8dy,zero=0.0_iwp,          &
   one=1.0_iwp,two=2.0_iwp,d3=3.0_iwp,d4=4.0_iwp,d5=5.0_iwp,d6=6.0_iwp,   &
   d8=8.0_iwp,pt125=0.125_iwp
 x1=coord(1,1)
 x2=coord(2,1)
 x3=coord(3,1)
 x4=coord(4,1)
 x5=coord(5,1)
 x6=coord(6,1)
 x7=coord(7,1)
 x8=coord(8,1)
 y1=coord(1,2)
 y2=coord(2,2)
 y3=coord(3,2)
 y4=coord(4,2)
 y5=coord(5,2)
 y6=coord(6,2)
 y7=coord(7,2)
 y8=coord(8,2)
 xi2=xi*xi
 xi3=xi2*xi
 et2=eta*eta
 et3=et2*eta
 xi2et2=xi2*et2
 xi1et1=xi*eta
 xi2et1=xi2*eta
 xi1et2=xi*et2
 xy12=x1*y2-y1*x2
 xy13=x1*y3-y1*x3
 xy14=x1*y4-y1*x4
 xy15=x1*y5-y1*x5
 xy16=x1*y6-y1*x6
 xy17=x1*y7-y1*x7
 xy18=x1*y8-y1*x8
 xy23=x2*y3-y2*x3
 xy24=x2*y4-y2*x4
 xy25=x2*y5-y2*x5
 xy26=x2*y6-y2*x6
 xy27=x2*y7-y2*x7
 xy28=x2*y8-y2*x8
 xy34=x3*y4-y3*x4
 xy35=x3*y5-y3*x5
 xy36=x3*y6-y3*x6
 xy37=x3*y7-y3*x7
 xy38=x3*y8-y3*x8
 xy45=x4*y5-y4*x5
 xy46=x4*y6-y4*x6
 xy47=x4*y7-y4*x7
 xy48=x4*y8-y4*x8
 xy56=x5*y6-y5*x6
 xy57=x5*y7-y5*x7
 xy58=x5*y8-y5*x8
 xy67=x6*y7-y6*x7
 xy68=x6*y8-y6*x8
 xy78=x7*y8-y7*x8
 xcu=-d8*xy48+d4*(-xy14+xy38+xy47+xy58)+two*(xy13+xy15-xy37-xy57)
 xsq=two*(-xy13+xy14-xy17+xy18+xy24-xy28-xy34+xy35-xy38-xy45+xy46+xy47-   &
   xy57+xy58+xy68-xy78)+one*(-xy12+xy16-xy23-xy25+xy27-xy36-xy56-xy67) 
 xon=d8*xy48+two*(xy14-xy18+xy34-xy38-xy45-xy47-xy58-xy78)+xy12-xy16+xy23-&
   xy25+xy27+xy36-xy56-xy67
 ecu=-d8*xy26+d4*(xy16+xy25+xy36+xy27)+two*(-xy15-xy17-xy35-xy37)
 esq=two*(-xy12+xy13-xy16+xy17-xy23+xy24+xy25-xy27-xy28-xy35+xy36+xy46-   &
   xy56+xy57-xy67+xy68)-xy14+xy18-xy34+xy38-xy45-xy47-xy58-xy78
 eon=d8*xy26+two*( xy12-xy16-xy23-xy25-xy27-xy36-xy56+xy67)+xy14-xy18-    &
   xy34+xy38-xy45+xy47-xy58+xy78
 x2e2=d6*(xy24-xy28+xy46+xy68)+d3*(-xy12+xy13-xy14+xy16-xy17+xy18-xy23-   &
   xy25+xy27-xy34+xy35-xy36+xy38-xy45-xy47-xy56+xy57-xy58-xy67-xy78)
 x1e1=d8*(-xy24-xy28+xy46-xy68)+d6*(-xy12+xy18+xy23+xy34-xy45-xy56+xy67+  &
   xy78)+two*(xy14-xy16+xy25+xy27-xy36+xy38-xy47+xy58)
 x2e1=d8*(xy24+xy28+xy46-xy68)+d5*( xy17-xy18-xy34+xy35-xy45+xy78)+d4*    &
   (xy12-xy16-xy23-xy25-xy27-xy36-xy56+xy67)+d3*(-xy14+xy15+xy37-xy38-    &
   xy47+xy58)
 x1e2=d8*(-xy24+xy28+xy46+xy68)+d5*(xy12-xy13+xy23+xy57-xy56-xy67)+d4*    &
   (xy14-xy18+xy34-xy38-xy45-xy47-xy58-xy78)+d3*(-xy15+xy16+xy25-xy27-    &
   xy36+xy37)
 cons= two*(-xy24+xy28-xy46-xy68)
!
 bot=xi3*xcu+xi2*xsq+xi*xon+et3*ecu+et2*esq+eta*eon
 bot=bot+xi2et2*x2e2+xi1et1*x1e1
 bot=bot+xi2et1*x2e1+xi1et2*x1e2+cons
!
 det=pt125*bot
!
 xcu=two*y3-d4*y4+two*y5
 xsq=-y2-two*y3+two*y4+y6-two*y7+two*y8
 xon=y2+two*y4 -y6-two*y8
 ecu=-two*y5+d4*y6-two*y7
 esq=-two*y2+two*y3-y4-two*y6+two*y7+y8
 eon=two*y2+y4-two*y6-y8
 x2e2=-d3*y2+d3*y3-d3*y4+d3*y6-d3*y7+d3*y8
 x1e1=-d6*y2+two*y4-two*y6+d6*y8
 x2e1=d4*y2-d3*y4+d3*y5-d4*y6+d5*y7-d5*y8
 x1e2=d5*y2-d5*y3+d4*y4-d3*y5+d3*y6-d4*y8
 cons=zero
!
 top=xi3*xcu+xi2*xsq+xi*xon+et3*ecu+et2*esq+eta*eon
 top=top+xi2et2*x2e2+xi1et1*x1e1
 top=top+xi2et1*x2e1+xi1et2*x1e2+cons
!
 dn1dx=top/bot
!
 xcu=zero
 xsq=y1-y3+two*y4-y5+y7-two*y8
 xon=-y1+y3-y5+y7      
 ecu=d4*y5-d8*y6+d4*y7
 esq=two*y1-two*y3+two*y4+two*y5-two*y7-two*y8
 eon=-two*y1-two*y3-two*y5+d8*y6-two*y7      
 x2e2= d3*y1-d3*y3+d6*y4-d3*y5+d3*y7-d6*y8
 x1e1= d6*y1+d6*y3-d8*y4+two*y5+two*y7-d8*y8
 x2e1=-d4*y1-d4*y3+d8*y4-d4*y5-d4*y7+d8*y8
 x1e2=-d5*y1+d5*y3-d8*y4+d3*y5-d3*y7+d8*y8
 cons=-two*y4+two*y8
!
 top=xi3*xcu+xi2*xsq+xi*xon+et3*ecu+et2*esq+eta*eon
 top=top+xi2et2*x2e2+xi1et1*x1e1
 top=top+xi2et1*x2e1+xi1et2*x1e2+cons
!
 dn2dx=top/bot
!
 xcu=-two*y1-two*y7+d4*y8
 xsq=two*y1+y2-two*y4+two*y5-y6-two*y8
 xon=-y2+two*y4+y6-two*y8
 ecu=-two*y5+d4*y6-two*y7
 esq=-two*y1+two*y2-y4-two*y5+two*y6+y8
 eon=two*y2-y4-two*y6+y8
 x2e2=-d3*y1+d3*y2-d3*y4+d3*y5-d3*y6+d3*y8
 x1e1=-d6*y2+d6*y4-two*y6+two*y8
 x2e1=+d4*y2-d5*y4+d5*y5-d4*y6+d3*y7-d3*y8
 x1e2=d5*y1-d5*y2+d4*y4-d3*y6+d3*y7-d4*y8
 cons=zero
!
 top=xi3*xcu+xi2*xsq+xi*xon+et3*ecu+et2*esq+eta*eon
 top=top+xi2et2*x2e2+xi1et1*x1e1
 top=top+xi2et1*x2e1+xi1et2*x1e2+cons
!
 dn3dx=top/bot
!
 xcu=d4*y1+d4*y7-d8*y8
 xsq=-two*y1-two*y2+two*y3-two*y5+two*y6+two*y7
 xon=-two*y1-two*y3-two*y5-two*y7+d8*y8
 ecu=zero
 esq=y1-two*y2+y3-y5+two*y6-y7
 eon=-y1+y3-y5+y7 
 x2e2=d3*y1-d6*y2+d3*y3-d3*y5+d6*y6-d3*y7  
 x1e1=-two*y1+d8*y2-d6*y3-d6*y5+d8*y6-two*y7 
 x2e1=d3*y1-d8*y2+d5*y3-d5*y5+d8*y6-d3*y7 
 x1e2=-d4*y1+d8*y2-d4*y3-d4*y5+d8*y6-d4*y7  
 cons=two*y2-two*y6
!
 top=xi3*xcu+xi2*xsq+xi*xon+et3*ecu+et2*esq+eta*eon
 top=top+xi2et2*x2e2+xi1et1*x1e1
 top=top+xi2et1*x2e1+xi1et2*x1e2+cons
!
 dn4dx=top/bot
!
 xcu=-two*y1-two*y7+d4*y8
 xsq=y2-two*y3+two*y4-y6-two*y7+two*y8
 xon=y2 +two*y4-y6-two*y8
 ecu=two*y1-d4*y2+two*y3
 esq=-two*y2+two*y3+y4-two*y6+two*y7-y8
 eon=two*y2+y4-two*y6-y8
 x2e2=d3*y2-d3*y3+d3*y4-d3*y6+d3*y7-d3*y8 
 x1e1=-two*y2+d6*y4-d6*y6+two*y8
 x2e1=-d3*y1+d4*y2-d5*y3+d5*y4-d4*y6+d3*y8
 x1e2=d3*y1-d3*y2+d4*y4-d5*y6+d5*y7-d4*y8 
 cons=zero
!
 top=xi3*xcu+xi2*xsq+xi*xon+et3*ecu+et2*esq+eta*eon
 top=top+xi2et2*x2e2+xi1et1*x1e1
 top=top+xi2et1*x2e1+xi1et2*x1e2+cons
!
 dn5dx=top/bot
!
 xcu=zero     
 xsq=-y1+y3-two*y4+y5-y7+two*y8
 xon=y1-y3+y5-y7 
 ecu=-d4*y1+d8*y2-d4*y3
 esq=two*y1-two*y3-two*y4+two*y5-two*y7+two*y8
 eon=two*y1-d8*y2+two*y3+two*y5+two*y7 
 x2e2=-d3*y1+d3*y3-d6*y4+d3*y5-d3*y7+d6*y8 
 x1e1=two*y1+two*y3-d8*y4+d6*y5+d6*y7-d8*y8
 x2e1=d4*y1+d4*y3-d8*y4+d4*y5+d4*y7-d8*y8
 x1e2=-d3*y1+d3*y3-d8*y4+d5*y5-d5*y7+d8*y8 
 cons=two*y4-two*y8
!
 top=xi3*xcu+xi2*xsq+xi*xon+et3*ecu+et2*esq+eta*eon
 top=top+xi2et2*x2e2+xi1et1*x1e1
 top=top+xi2et1*x2e1+xi1et2*x1e2+cons
!
 dn6dx=top/bot
!
 xcu=two*y3-d4*y4+two*y5         
 xsq=two*y1-y2-two*y4+two*y5+y6-two*y8
 xon=-y2+two*y4+y6-two*y8
 ecu=two*y1-d4*y2+two*y3
 esq=-two*y1+two*y2+y4-two*y5+two*y6-y8
 eon=two*y2-y4-two*y6+y8
 x2e2=d3*y1-d3*y2+d3*y4-d3*y5+d3*y6-d3*y8 
 x1e1=-two*y2+two*y4-d6*y6+d6*y8
 x2e1=-d5*y1+d4*y2-d3*y3+d3*y4-d4*y6+d5*y8
 x1e2=d3*y2-d3*y3+d4*y4-d5*y5+d5*y6-d4*y8 
 cons=zero
!
 top=xi3*xcu+xi2*xsq+xi*xon+et3*ecu+et2*esq+eta*eon
 top=top+xi2et2*x2e2+xi1et1*x1e1
 top=top+xi2et1*x2e1+xi1et2*x1e2+cons
!
 dn7dx=top/bot
!
 xcu=-d4*y3+d8*y4-d4*y5         
 xsq=-two*y1+two*y2+two*y3-two*y5-two*y6+two*y7 
 xon=two*y1+two*y3-d8*y4+two*y5+two*y7 
 ecu=zero      
 esq=-y1+two*y2-y3+y5-two*y6+y7 
 eon=y1-y3+y5-y7  
 x2e2=-d3*y1+d6*y2-d3*y3+d3*y5-d6*y6+d3*y7  
 x1e1=-d6*y1+d8*y2-two*y3-two*y5+d8*y6-d6*y7  
 x2e1=d5*y1-d8*y2+d3*y3-d3*y5+d8*y6-d5*y7  
 x1e2=d4*y1-d8*y2+d4*y3+d4*y5-d8*y6+d4*y7  
 cons=-two*y2+two*y6
!
 top=xi3*xcu+xi2*xsq+xi*xon+et3*ecu+et2*esq+eta*eon
 top=top+xi2et2*x2e2+xi1et1*x1e1
 top=top+xi2et1*x2e1+xi1et2*x1e2+cons
!
 dn8dx=top/bot
!
 y1=-x1
 y2=-x2
 y3=-x3
 y4=-x4
 y5=-x5
 y6=-x6
 y7=-x7
 y8=-x8
!
 xcu=two*y3-d4*y4+two*y5
 xsq=-y2-two*y3+two*y4+y6-two*y7+two*y8
 xon=y2+two*y4-y6-two*y8
 ecu=-two*y5+d4*y6-two*y7
 esq=-two*y2+two*y3-y4-two*y6+two*y7+y8
 eon=two*y2+y4-two*y6-y8
 x2e2=-d3*y2+d3*y3-d3*y4 +d3*y6-d3*y7+d3*y8
 x1e1=-d6*y2+two*y4-two*y6+d6*y8
 x2e1=d4*y2-d3*y4+d3*y5-d4*y6+d5*y7-d5*y8
 x1e2=d5*y2-d5*y3+d4*y4-d3*y5+d3*y6-d4*y8
 cons=zero
!
 top=xi3*xcu+xi2*xsq+xi*xon+et3*ecu+et2*esq+eta*eon
 top=top+xi2et2*x2e2+xi1et1*x1e1
 top=top+xi2et1*x2e1+xi1et2*x1e2+cons
!
 dn1dy=top/bot
!
 xcu=zero
 xsq=y1-y3+two*y4-y5+y7-two*y8
 xon=-y1+y3-y5+y7 
 ecu=d4*y5-d8*y6+d4*y7
 esq=two*y1-two*y3+two*y4+two*y5-two*y7-two*y8
 eon=-two*y1-two*y3-two*y5+d8*y6-two*y7 
 x2e2=d3*y1-d3*y3+d6*y4-d3*y5+d3*y7-d6*y8
 x1e1=d6*y1+d6*y3-d8*y4+two*y5+two*y7-d8*y8
 x2e1=-d4*y1-d4*y3+d8*y4-d4*y5-d4*y7+d8*y8
 x1e2=-d5*y1+d5*y3-d8*y4+d3*y5-d3*y7+d8*y8
 cons=-two*y4+two*y8
!
 top=xi3*xcu+xi2*xsq+xi*xon+et3*ecu+et2*esq+eta*eon
 top=top+xi2et2*x2e2+xi1et1*x1e1
 top=top+xi2et1*x2e1+xi1et2*x1e2+cons
!
 dn2dy=top/bot
!
 xcu=-two*y1-two*y7+d4*y8
 xsq=two*y1+y2-two*y4+two*y5-y6-two*y8
 xon=-y2+two*y4+y6-two*y8
 ecu=-two*y5+d4*y6-two*y7
 esq=-two*y1+two*y2-y4-two*y5+two*y6+y8
 eon=two*y2-y4-two*y6+y8
 x2e2=-d3*y1+d3*y2-d3*y4+d3*y5-d3*y6+d3*y8
 x1e1=-d6*y2+d6*y4-two*y6+two*y8
 x2e1=d4*y2-d5*y4+d5*y5-d4*y6+d3*y7-d3*y8
 x1e2=d5*y1-d5*y2+d4*y4-d3*y6+d3*y7-d4*y8
 cons=zero
!
 top=xi3*xcu+xi2*xsq+xi*xon+et3*ecu+et2*esq+eta*eon
 top=top+xi2et2*x2e2+xi1et1*x1e1
 top=top+xi2et1*x2e1+xi1et2*x1e2+cons
!
 dn3dy=top/bot
!
 xcu=d4*y1+d4*y7-d8*y8
 xsq=-two*y1-two*y2+two*y3-two*y5+two*y6+two*y7
 xon=-two*y1-two*y3-two*y5-two*y7+d8*y8
 ecu=zero
 esq=y1-two*y2+y3-y5+two*y6-y7
 eon=-y1+y3-y5+y7 
 x2e2=d3*y1-d6*y2+d3*y3-d3*y5+d6*y6-d3*y7  
 x1e1=-two*y1+d8*y2-d6*y3-d6*y5+d8*y6-two*y7 
 x2e1=d3*y1-d8*y2+d5*y3-d5*y5+d8*y6-d3*y7 
 x1e2=-d4*y1+d8*y2-d4*y3-d4*y5+d8*y6-d4*y7  
 cons=two*y2-two*y6
!
 top=xi3*xcu+xi2*xsq+xi*xon+et3*ecu+et2*esq+eta*eon
 top=top+xi2et2*x2e2+xi1et1*x1e1
 top=top+xi2et1*x2e1+xi1et2*x1e2+cons
!
 dn4dy=top/bot
!
 xcu=-two*y1-two*y7+d4*y8
 xsq=y2-two*y3+two*y4-y6-two*y7+two*y8
 xon=y2+two*y4-y6-two*y8
 ecu=two*y1-d4*y2+two*y3
 esq=-two*y2+two*y3+y4-two*y6+two*y7-y8
 eon=two*y2+y4-two*y6-y8
 x2e2=d3*y2-d3*y3+d3*y4-d3*y6+d3*y7-d3*y8 
 x1e1=-two*y2+d6*y4-d6*y6+two*y8
 x2e1=-d3*y1+d4*y2-d5*y3+d5*y4-d4*y6 +d3*y8
 x1e2=d3*y1-d3*y2+d4*y4-d5*y6+d5*y7-d4*y8 
 cons=zero
!
 top=xi3*xcu+xi2*xsq+xi*xon+et3*ecu+et2*esq+eta*eon
 top=top+xi2et2*x2e2+xi1et1*x1e1
 top=top+xi2et1*x2e1+xi1et2*x1e2+cons
!
 dn5dy=top/bot
!
 xcu=zero          
 xsq=-y1+y3-two*y4+y5-y7+two*y8
 xon=y1-y3+y5-y7 
 ecu=-d4*y1+d8*y2-d4*y3
 esq=two*y1-two*y3-two*y4+two*y5-two*y7+two*y8
 eon=two*y1-d8*y2+two*y3+two*y5+two*y7 
 x2e2=-d3*y1+d3*y3-d6*y4+d3*y5-d3*y7+d6*y8 
 x1e1=two*y1+two*y3-d8*y4+d6*y5+d6*y7-d8*y8
 x2e1=d4*y1+d4*y3-d8*y4+d4*y5+d4*y7-d8*y8
 x1e2=-d3*y1+d3*y3-d8*y4+d5*y5-d5*y7+d8*y8 
 cons=two*y4-two*y8
!
 top=xi3*xcu+xi2*xsq+xi*xon+et3*ecu+et2*esq+eta*eon
 top=top+xi2et2*x2e2+xi1et1*x1e1
 top=top+xi2et1*x2e1+xi1et2*x1e2+cons
!
 dn6dy=top/bot
!
 xcu=two*y3-d4*y4+two*y5         
 xsq=two*y1-y2-two*y4+two*y5+y6-two*y8
 xon=-y2+two*y4+y6-two*y8
 ecu=two*y1-d4*y2+two*y3
 esq=-two*y1+two*y2+y4-two*y5+two*y6-y8
 eon=two*y2-y4-two*y6+y8
 x2e2=d3*y1-d3*y2+d3*y4-d3*y5+d3*y6-d3*y8 
 x1e1=-two*y2+two*y4-d6*y6+d6*y8
 x2e1=-d5*y1+d4*y2-d3*y3+d3*y4-d4*y6+d5*y8
 x1e2=d3*y2-d3*y3+d4*y4-d5*y5+d5*y6-d4*y8 
 cons=zero
!
 top=xi3*xcu+xi2*xsq+xi*xon+et3*ecu+et2*esq+eta*eon
 top=top+xi2et2*x2e2+xi1et1*x1e1
 top=top+xi2et1*x2e1+xi1et2*x1e2+cons
!
 dn7dy=top/bot
!
 xcu=-d4*y3+d8*y4-d4*y5         
 xsq=-two*y1+two*y2+two*y3-two*y5-two*y6+two*y7 
 xon=two*y1+two*y3-d8*y4+two*y5+two*y7 
 ecu=zero      
 esq=-y1+two*y2-y3+y5-two*y6+y7 
 eon=y1-y3+y5-y7  
 x2e2=-d3*y1+d6*y2-d3*y3+d3*y5-d6*y6+d3*y7  
 x1e1=-d6*y1+d8*y2-two*y3-two*y5+d8*y6-d6*y7  
 x2e1=d5*y1-d8*y2+d3*y3-d3*y5+d8*y6-d5*y7  
 x1e2=d4*y1-d8*y2+d4*y3+d4*y5-d8*y6+d4*y7  
 cons=-two*y2+two*y6
!
 top=xi3*xcu+xi2*xsq+xi*xon+et3*ecu+et2*esq+eta*eon
 top=top+xi2et2*x2e2+xi1et1*x1e1
 top=top+xi2et1*x2e1+xi1et2*x1e2+cons
!
 dn8dy=top/bot
!
 bee=zero
 bee(1,1)=dn1dx
 bee(1,3)=dn2dx
 bee(1,5)=dn3dx
 bee(1,7)=dn4dx
 bee(1,9)=dn5dx   
 bee(1,11)=dn6dx
 bee(1,13)=dn7dx   
 bee(1,15)=dn8dx
 bee(2,2)=dn1dy
 bee(2,4)=dn2dy
 bee(2,6)=dn3dy
 bee(2,8)=dn4dy
 bee(2,10)=dn5dy
 bee(2,12)=dn6dy
 bee(2,14)=dn7dy
 bee(2,16)=dn8dy
 bee(3,1)=dn1dy
 bee(3,3)=dn2dy
 bee(3,5)=dn3dy
 bee(3,7)=dn4dy
 bee(3,9)=dn5dy
 bee(3,11)=dn6dy
 bee(3,13)=dn7dy
 bee(3,15)=dn8dy
 bee(3,2)=dn1dx
 bee(3,4)=dn2dx   
 bee(3,6)=dn3dx
 bee(3,8)=dn4dx
 bee(3,10)=dn5dx
 bee(3,12)=dn6dx   
 bee(3,14)=dn7dx
 bee(3,16)=dn8dx
 RETURN
END SUBROUTINE bee8

SUBROUTINE exc_nods(noexe,exele,g_num,totex,ntote,nf)
!
! This subroutine forms the nodes removed in an excavation lift.
!
 IMPLICIT NONE
 INTEGER,INTENT(IN)::noexe,exele(:),g_num(:,:)
 INTEGER,INTENT(IN OUT)::totex(:),ntote,nf(:,:)
 INTEGER::i,jj,k,iel,modex,nodex,ncheck,nels,nod
 nels=UBOUND(g_num,2)
 nod=UBOUND(g_num,1)
 ntote=ntote+noexe
 totex(ntote-noexe+1:ntote)=exele
 DO i=1,noexe
   DO k=1,8
     nodex=0
     ncheck=g_num(k,exele(i))
     DO iel=1,nels
       modex=0
       DO jj=1,ntote
         IF(iel==totex(jj))THEN
           modex=1
           EXIT
         END IF
       END DO
       IF(modex==1)CYCLE
       DO jj=1,nod
         IF(ncheck==g_num(jj,iel))THEN
           nodex=1
           EXIT
         END IF
       END DO
       IF(nodex==1)EXIT
     END DO
     IF(nodex==0)nf(:,ncheck)=0
   END DO
 END DO
RETURN
END SUBROUTINE exc_nods

SUBROUTINE emb_3d_bc(ifix,nx1,nx2,ny1,ny2,nze,nf)
!
! This subroutine generates the nf array for a 3-d slope geometry.
! Side boundary conditions:
! smooth-smooth  ifix=1  (for checking against plane-strain)
! rough -smooth  ifix=2  (for centerline symmetry)
! rough -rough   ifix=3
!
 IMPLICIT NONE
 INTEGER,INTENT(IN)::nx1,nx2,ny1,ny2,nze,ifix
 INTEGER,INTENT(OUT)::nf(:,:)
 INTEGER::nm,ic,i,j,k,nye
 nye=ny1+ny2
 nm=0
 ic=0
!
!                       boundary condition for the front face
!
 DO i=1,2*nye
   nm=nm+1
   nf(:,nm)=0
   IF(ifix==1)THEN
     ic=ic+1
     nf(2,nm)=ic
   END IF
 END DO
 nm=nm+1
 nf(:,nm)=0
 DO j=1,nx1
   DO i=1,nye
     nm=nm+1
     nf(:,nm)=0
     IF(ifix==1)THEN
       ic=ic+1
       nf(1,nm)=ic
       ic=ic+1
       nf(2,nm)=ic
     END IF
   END DO
   nm=nm+1
   nf(:,nm)=0
   DO i=1,2*nye
     nm=nm+1
     nf(:,nm)=0
     IF(ifix==1)THEN
       ic=ic+1
       nf(1,nm)=ic
       ic=ic+1
       nf(2,nm)=ic
     END IF
   END DO
   nm=nm+1
   nf(:,nm)=0
 END DO
 DO j=1,nx2
   DO i=1,ny2
     nm=nm+1
     nf(:,nm)=0
     IF(ifix==1)THEN
       ic=ic+1
       nf(1,nm)=ic
       ic=ic+1
       nf(2,nm)=ic
     END IF
   END DO
   nm=nm+1
   nf(:,nm)=0
   DO i=1,2*ny2
     nm=nm+1
     nf(:,nm)=0
     IF(ifix==1)THEN
       IF(j<nx2)THEN
         ic=ic+1
         nf(1,nm)=ic
       END IF 
       ic=ic+1
       nf(2,nm)=ic
     END IF
   END DO
   nm=nm+1
   nf(:,nm)=0
 END DO    
!
!                       boundary conditions for the middle layer
!
 DO i=1,nye
   nm=nm+1
   nf(1,nm)=0
   ic=ic+1
   nf(3,nm)=ic
   ic=ic+1
   nf(2,nm)=ic
 END DO
 nm=nm+1
 nf(:,nm)=0
 DO j=1,nx1
   DO i=1,nye
     nm=nm+1
     ic=ic+1
     nf(1,nm)=ic
     ic=ic+1
     nf(3,nm)=ic
     ic=ic+1
     nf(2,nm)=ic
   END DO
   nm=nm+1
   nf(:,nm)=0
 END DO
 DO j=1,nx2
   DO i=1,ny2
     nm=nm+1
     IF(j==nx2)THEN
       nf(1,nm)=0
     ELSE
       ic=ic+1
       nf(1,nm)=ic
     END IF
     ic=ic+1
     nf(3,nm)=ic
     ic=ic+1
     nf(2,nm)=ic
   END DO
   nm=nm+1
   nf(:,nm)=0
 END DO
!all other layers
DO k=1,nze-1
  DO i=1,2*nye
    nm=nm+1
    nf(1,nm)=0
    ic=ic+1
    nf(3,nm)=ic
    ic=ic+1
    nf(2,nm)=ic
  END DO
  nm=nm+1
  nf(:,nm)=0
  DO j=1,nx1
    DO i=1,nye
      nm=nm+1
      ic=ic+1
      nf(1,nm)=ic
      ic=ic+1
      nf(3,nm)=ic
      ic=ic+1
      nf(2,nm)=ic
    END DO
    nm=nm+1
    nf(:,nm)=0
    DO i=1,2*nye
      nm=nm+1
      ic=ic+1
      nf(1,nm)=ic
      ic=ic+1
      nf(3,nm)=ic
      ic=ic+1
      nf(2,nm)=ic
    END DO
    nm=nm+1
    nf(:,nm)=0
  END DO
  DO j=1,nx2
    DO i=1,ny2
      nm=nm+1
      ic=ic+1
      nf(1,nm)=ic
      ic=ic+1
      nf(3,nm)=ic
      ic=ic+1
      nf(2,nm)=ic
    END DO
    nm=nm+1
    nf(:,nm)=0
    DO i=1,2*ny2
      nm=nm+1
      IF(j==nx2)THEN
        nf(1,nm)=0
      ELSE
        ic=ic+1
        nf(1,nm)=ic
      END IF
      ic=ic+1
      nf(3,nm)=ic
      ic=ic+1
      nf(2,nm)=ic
    END DO
    nm=nm+1
    nf(:,nm)=0
  END DO
! middle layers
  DO i=1,nye
    nm=nm+1
    nf(1,nm)=0
    ic=ic+1
    nf(3,nm)=ic
    ic=ic+1
    nf(2,nm)=ic
  END DO
  nm=nm+1
  nf(:,nm)=0
  DO j=1,nx1
    DO i=1,nye
      nm=nm+1
      ic=ic+1
      nf(1,nm)=ic
      ic=ic+1
      nf(3,nm)=ic
      ic=ic+1
      nf(2,nm)=ic
    END DO
    nm=nm+1
    nf(:,nm)=0
  END DO
  DO j=1,nx2
    DO i=1,ny2
      nm=nm+1
      IF(j==nx2)THEN
        nf(1,nm)=0
      ELSE
        ic=ic+1
        nf(1,nm)=ic
      END IF
      ic=ic+1
      nf(3,nm)=ic
      ic=ic+1
      nf(2,nm)=ic
    END DO
    nm=nm+1
    nf(:,nm)=0
  END DO
END DO
!
!                       boundary conditions for back face
!
 DO i=1,2*nye
   nm=nm+1
   nf(:,nm)=0
   IF(ifix==1.OR.ifix==2)THEN
     ic=ic+1
     nf(2,nm)=ic
   END IF
 END DO
 nm=nm+1
 nf(:,nm)=0
 DO j=1,nx1
   DO i=1,nye
     nm=nm+1
     nf(:,nm)=0
     IF(ifix==1.OR.ifix==2)THEN
       ic=ic+1
       nf(1,nm)=ic
       ic=ic+1
       nf(2,nm)=ic
     END IF
   END DO
   nm=nm+1
   nf(:,nm)=0
   DO i=1,2*nye
     nm=nm+1
     nf(:,nm)=0
     IF(ifix==1.OR.ifix==2)THEN
       ic=ic+1
       nf(1,nm)=ic
       ic=ic+1
       nf(2,nm)=ic
     END IF
   END DO
   nm=nm+1
   nf(:,nm)=0
 END DO
 DO j=1,nx2
   DO i=1,ny2
     nm=nm+1
     nf(:,nm)=0
     IF(ifix==1.OR.ifix==2)THEN
       ic=ic+1
       nf(1,nm)=ic
       ic=ic+1
       nf(2,nm)=ic
     END IF
   END DO
   nm=nm+1
   nf(:,nm)=0
   DO i=1,2*ny2
     nm=nm+1
     nf(:,nm)=0
     IF(ifix==1.OR.ifix==2)THEN
       IF(j<nx2)THEN
         ic=ic+1
         nf(1,nm)=ic
       END IF 
       ic=ic+1
       nf(2,nm)=ic
     END IF
   END DO
   nm=nm+1
   nf(:,nm)=0
 END DO    
RETURN
END SUBROUTINE emb_3d_bc

SUBROUTINE emb_3d_geom(iel,nx1,nx2,ny1,ny2,nze,w1,s1,w2,h1,h2,d1,coord,num)
!
! This subroutine forms the nodal coordinates and numbering for a 3-d
! slope of 20-node hexahedra. Nodes and elements numbered in xz planes
! going in the y-direction.
!
 IMPLICIT NONE
 INTEGER,PARAMETER::iwp=SELECTED_REAL_KIND(15)
 REAL(iwp),INTENT(IN)::w1,s1,w2,h1,h2, d1
 INTEGER,INTENT(IN)::iel,nx1,nx2,ny1,ny2,nze
 REAL(iwp),INTENT(OUT)::coord(:,:)
 INTEGER,intent(OUT)::num(:)
 INTEGER::nxe,nye,nc,nt,nn_mid,nn_main,ip,iq,is,nd,ns
 REAL(iwp)::facx,facy,facz,facb,facs,frh,zero=0.0_iwp,pt5=0.5_iwp
!
!                       basic variable initialization
!
 nxe=nx1+nx2
 nye=ny1+ny2
!number of elements in a slice
 ns=nx1*nye+nx2*ny2
!number of elements in sloped region
 nt=nx1*ny1
!number of nodes in main sloped region
 nc=(3*nye+2)*nx1+2*ny1
!number of nodes in mid sloped region
 nd=(nye+1)*nx1+ny1
!number of nodes in a middle layer
 nn_mid=(nye+1)*(nx1+1)+(ny2+1)*nx2
!number of nodes in a main layer
 nn_main=(3*nye+2)*nx1+2*nye+1+(3*ny2+2)*nx2
!
 facx=s1/ny1
 facy=h1/ny1
 facz=d1/nze
 facb=(w1+s1)/nx1
 facs=zero
 IF(ny2/=0)facs=h2/ny2
 frh=zero
 IF(nx2/=0)frh=w2/nx2
!
!                       determine element index values (ip, iq, is)
!
 is=(iel-1)/ns+1
 IF(iel-(is-1)*ns<=nt)THEN !if it is in sloped region
   iq=(iel-(is-1)*ns-1)/nx1+1
   ip=iel-(is-1)*ns-(iq-1)*nx1
 ELSE                      !if it is in unsloped region
   iq=(iel-(is-1)*ns-nt-1)/nxe+ny1+1
   ip=iel-(is-1)*ns-nt-(iq-ny1-1)*nxe
 END IF
!
! Calculate node indices (num()) and coordinates (coord(,))
 IF(ip<=nx1)THEN !if iel is in sloped region
   num(1)=(ip-1)*(3*nye+2)+2*iq+1+(is-1)*(nn_mid+nn_main)
   num(2)=num(1)-1
   num(3)=num(1)-2
   num(4)=(ip-1)*(3*nye+2)+2*nye+iq+1+(is-1)*(nn_mid+nn_main)
   num(5)=ip*(3*nye+2)+2*iq-1+(is-1)*(nn_mid+nn_main)
   num(6)=num(5)+1
   num(7)=num(5)+2
   num(8)=num(4)+1
   num(9)=(nn_main+(ip-1)*(nye+1)+iq+1)*is+(is-1)*                        &
     (nn_mid-(ip-1)*(nye+1)-iq-1)
   num(10)=num(9)-1
   num(11)=1+(nn_main+ip*(nye+1)+iq-1)*is+(is-1)*(nn_mid-ip*(nye+1)-iq+1)
   num(12)=num(11)+1
   num(13)=(ip-1)*(3*nye+2)+2*iq+1+is*(nn_mid+nn_main)
   num(14)=num(13)-1
   num(15)=num(13)-2
   num(16)=(ip-1)*(3*nye+2)+2*nye+iq+1+is*(nn_mid+nn_main)
   num(17)=ip*(3*nye+2)+2*iq-1+is*(nn_mid+nn_main)
   num(18)=num(17)+1
   num(19)=num(17)+2
   num(20)=num(16)+1
   IF(iq<=ny1)THEN ! if iel is in trapezoidal region
     coord(1,1)=(ip-1)*(w1+iq*facx)/nx1
     coord(3,1)=(ip-1)*(w1+(iq-1)*facx)/nx1
     coord(5,1)=ip*(w1+(iq-1)*facx)/nx1
     coord(7,1)=ip*(w1+iq*facx)/nx1
     coord(1,2)=-iq*facy
     coord(3,2)=-(iq-1)*facy
     coord(5,2)=-(iq-1)*facy
     coord(7,2)=-iq*facy
   ELSE            ! if iel is in rectangular region
     coord(1,1)=(ip-1)*facb
     coord(3,1)=(ip-1)*facb
     coord(5,1)=ip*facb
     coord(7,1)=ip*facb
     coord(1,2)=-h1-(iq-ny1)*facs
     coord(3,2)=-h1-(iq-ny1-1)*facs
     coord(5,2)=-h1-(iq-ny1-1)*facs
     coord(7,2)=-h1-(iq-ny1)*facs
   END IF
 ELSE              ! if iel is in unsloped region
   num(1)=(nn_mid+nn_main)*(is-1)+nc+(3*ny2+2)*(ip-nx1-1)+(iq-ny1)*2+1
   num(2)=num(1)-1
   num(3)=num(1)-2
   num(4)=(nn_mid+nn_main)*(is-1)+nc+(3*ny2+2)*(ip-nx1-1)+2*ny2+1+iq-ny1
   num(5)=(nn_mid+nn_main)*(is-1)+nc+(3*ny2+2)*(ip-nx1)+(iq-ny1)*2-1
   num(6)=num(5)+1
   num(7)=num(5)+2
   num(8)=num(4)+1
   num(9)=(nn_mid+nn_main)*(is-1)+nn_main+nd+(ny2+1)*(ip-nx1-1)+iq-ny1+1
   num(10)=num(9)-1
   num(11)=(nn_mid+nn_main)*(is-1)+nn_main+nd+(ny2+1)*(ip-nx1)+iq-ny1
   num(12)=num(11)+1
   num(13)=(nn_mid+nn_main)*is+nc+(3*ny2+2)*(ip-nx1-1)+(iq-ny1)*2+1
   num(14)=num(13)-1
   num(15)=num(13)-2
   num(16)=(nn_mid+nn_main)*is+nc+(3*ny2+2)*(ip-nx1-1)+2*ny2+1+iq-ny1
   num(17)=(nn_mid+nn_main)*is+nc+(3*ny2+2)*(ip-nx1)+(iq-ny1)*2-1
   num(18)=num(17)+1
   num(19)=num(17)+2
   num(20)=num(16)+1
!
   coord(1,1)=w1+s1+(ip-nx1-1)*frh
   coord(3,1)=coord(1,1)
   coord(5,1)=w1+s1+(ip-nx1)*frh
   coord(7,1)=coord(5,1)
   coord(1,2)=-h1-(iq-ny1)*facs
   coord(3,2)=-h1-(iq-ny1-1)*facs
   coord(5,2)=-h1-(iq-ny1-1)*facs
   coord(7,2)=-h1-(iq-ny1)*facs
 END IF
 coord(9,1)=coord(1,1)
 coord(10,1)=coord(3,1)
 coord(11,1)=coord(5,1)
 coord(12,1)=coord(7,1)
 coord(13,1)=coord(1,1)
 coord(15,1)=coord(3,1)
 coord(17,1)=coord(5,1)
 coord(19,1)=coord(7,1)
 coord(9,2) =coord(1,2)
 coord(10,2)=coord(3,2)
 coord(11,2)=coord(5,2)
 coord(12,2)=coord(7,2)
 coord(13,2)=coord(1,2)
 coord(15,2)=coord(3,2)
 coord(17,2)=coord(5,2)
 coord(19,2)=coord(7,2)
 coord(1:8:1,3)=-(is-1)*facz
 coord(9:12:1,3)=-(is*facz-facz*pt5)
 coord(13:20:1,3)=-is*facz    
 coord(2:6:2,:)=pt5*(coord(1:5:2,:)+coord(3:7:2,:))
 coord(14:18:2,:)=pt5*(coord(13:17:2,:)+coord(15:19:2,:))
 coord(8,:)=pt5*(coord(7,:)+coord(1,:))
 coord(20,:)=pt5*(coord(19,:)+coord(13,:))
RETURN
END SUBROUTINE emb_3d_geom

SUBROUTINE invar(stress,sigm,dsbar,theta)
!
! This subroutine forms the stress invariants in 2- or 3-d.
!
 IMPLICIT NONE
 INTEGER,PARAMETER::iwp=SELECTED_REAL_KIND(15)
 REAL(iwp),INTENT(IN)::stress(:)
 REAL(iwp),INTENT(OUT),OPTIONAL::sigm,dsbar,theta
 REAL(iwp)::sx,sy,sz,txy,dx,dy,dz,xj3,sine,s1,s2,s3,s4,s5,s6,ds1,ds2,ds3, &
   d2,d3,sq3,zero=0.0_iwp,small=1.e-10_iwp,one=1.0_iwp,two=2.0_iwp,       &
   three=3.0_iwp,six=6.0_iwp,thpt5=13.5_iwp
 INTEGER::nst 
 nst=UBOUND(stress,1)
 SELECT CASE(nst)
 CASE(4)
   sx=stress(1)
   sy=stress(2)
   txy=stress(3)
   sz=stress(4)
   sigm=(sx+sy+sz)/three
   dsbar=SQRT((sx-sy)**2+(sy-sz)**2+(sz-sx)**2+six*txy**2)/SQRT(two)
   IF(dsbar<small)THEN
     theta=zero
   ELSE
     dx=(two*sx-sy-sz)/three
     dy=(two*sy-sz-sx)/three
     dz=(two*sz-sx-sy)/three
     xj3=dx*dy*dz-dz*txy**2
     sine=-thpt5*xj3/dsbar**3
     IF(sine>=one)sine=one
     IF(sine<-one)sine=-one
     theta=ASIN(sine)/three
   END IF
 CASE(6)
   sq3=SQRT(three)
   s1=stress(1)  
   s2=stress(2)
   s3=stress(3) 
   s4=stress(4)
   s5=stress(5)
   s6=stress(6)
   sigm=(s1+s2+s3)/three
   d2=((s1-s2)**2+(s2-s3)**2+(s3-s1)**2)/six+s4*s4+s5*s5+s6*s6
   ds1=s1-sigm 
   ds2=s2-sigm  
   ds3=s3-sigm
   d3=ds1*ds2*ds3-ds1*s5*s5-ds2*s6*s6-ds3*s4*s4+two*s4*s5*s6
   dsbar=sq3*SQRT(d2)
   IF(dsbar<small)THEN
     theta=zero
   ELSE
     sine=-three*sq3*d3/(two*SQRT(d2)**3)
     IF(sine>=one)sine=one 
     IF(sine<-one)sine=-one 
     theta=ASIN(sine)/three
   END IF
 CASE DEFAULT
   WRITE(*,*)"wrong size for nst in invar"
 END SELECT
RETURN
END SUBROUTINE invar

SUBROUTINE mocouf(phi,c,sigm,dsbar,theta,f)
!
! This subroutine calculates the value of the yield function
! for a Mohr-Coulomb material (phi in degrees).
!
 IMPLICIT NONE
 INTEGER,PARAMETER::iwp=SELECTED_REAL_KIND(15)
 REAL(iwp),INTENT(IN)::phi,c,sigm,dsbar,theta   
 REAL(iwp),INTENT(OUT)::f
 REAL(iwp)::phir,snph,csph,csth,snth,one=1.0_iwp,d3=3.0_iwp,d4=4.0_iwp,   &
   d180=180.0_iwp
 phir=phi*d4*ATAN(one)/d180
 snph=SIN(phir) 
 csph=COS(phir) 
 csth=COS(theta)
 snth=SIN(theta)
 f=snph*sigm+dsbar*(csth/SQRT(d3)-snth*snph/d3)-c*csph
RETURN
END SUBROUTINE mocouf

SUBROUTINE mocouq(psi,dsbar,theta,dq1,dq2,dq3)
!
! This subroutine forms the derivatives of a Mohr-Coulomb potential
! function with respect to the three invariants (psi in degrees).
!
 IMPLICIT NONE
 INTEGER,PARAMETER::iwp=SELECTED_REAL_KIND(15)
 REAL(iwp),INTENT(IN)::psi,dsbar,theta
 REAL(iwp),INTENT(OUT)::dq1,dq2,dq3
 REAL(iwp)::psir,snth,snps,sq3,c1,csth,cs3th,tn3th,tnth,zero=0.0_iwp,     &
   pt49=0.49_iwp,pt5=0.5_iwp,one=1.0_iwp,d3=3.0_iwp,d4=4.0_iwp,           &
   d180=180.0_iwp
 psir=psi*d4*ATAN(one)/d180 
 snth=SIN(theta) 
 snps=SIN(psir)
 sq3=SQRT(d3)  
 dq1=snps
 if(ABS(snth).GT.pt49)THEN
   c1=one
   IF(snth.LT.zero)c1=-one
   dq2=(sq3*pt5-c1*snps*pt5/sq3)*sq3*pt5/dsbar 
   dq3=zero
 ELSE
   csth=COS(theta)
   cs3th=COS(d3*theta)
   tn3th=TAN(d3*theta)
   tnth=snth/csth
   dq2=sq3*csth/dsbar*((one+tnth*tn3th)+snps*(tn3th-tnth)/sq3)*pt5
   dq3=pt5*d3*(sq3*snth+snps*csth)/(cs3th*dsbar*dsbar)
 END IF
RETURN
END SUBROUTINE mocouq

SUBROUTINE formm(stress,m1,m2,m3)
!
! This subroutine forms the derivatives of the invariants with respect to
! stress in 2- or 3-d.
!
 IMPLICIT NONE
 INTEGER,PARAMETER::iwp=SELECTED_REAL_KIND(15)
 REAL(iwp),INTENT(IN)::stress(:)
 REAL(iwp),INTENT(OUT)::m1(:,:),m2(:,:),m3(:,:)
 REAL(iwp)::sx,sy,txy,tyz,tzx,sz,dx,dy,dz,sigm,zero=0.0_iwp,one=1.0_iwp,  &
   two=2.0_iwp,three=3.0_iwp,six=6.0_iwp,nine=9.0_iwp
 INTEGER::nst,i,j
 nst=UBOUND(stress,1)
 SELECT CASE(nst)
 CASE(4)
   sx=stress(1)
   sy=stress(2)
   txy=stress(3)
   sz=stress(4)
   dx=(two*sx-sy-sz)/three
   dy=(two*sy-sz-sx)/three
   dz=(two*sz-sx-sy)/three
   sigm=(sx+sy+sz)/three
   m1=zero
   m2=zero
   m3=zero
   m1(1,1:2)=one
   m1(2,1:2)=one
   m1(4,1:2)=one
   m1(1,4)=one
   m1(4,4)=one
   m1(2,4)=one
   m1=m1/nine/sigm
   m2(1,1)=two/three
   m2(2,2)=two/three
   m2(4,4)= two/three
   m2(2,4)=-one/three
   m2(4,2)=-one/three
   m2(1,2)=-one/three
   m2(2,1)=-one/three
   m2(1,4)=-one/three
   m2(4,1)=-one/three
   m2(3,3)=two
   m3(3,3)=-dz
   m3(1:2,3)=txy/three
   m3(3,1:2)=txy/three
   m3(3,4)=-two*txy/three
   m3(4,3)=-two*txy/three
   m3(1,1)=dx/three
   m3(2,4)=dx/three
   m3(4,2)=dx/three
   m3(2,2)=dy/three
   m3(1,4)=dy/three
   m3(4,1)=dy/three
   m3(4,4)=dz/three
   m3(1,2)=dz/three
   m3(2,1)=dz/three
 CASE(6)
   sx=stress(1)
   sy=stress(2)    
   sz=stress(3)
   txy=stress(4)  
   tyz=stress(5) 
   tzx=stress(6)
   sigm=(sx+sy+sz)/three
   dx=sx-sigm  
   dy=sy-sigm 
   dz=sz-sigm
   m1=zero
   m2=zero
   m1(1:3,1:3)=one/(three*sigm)
   DO i=1,3 
     m2(i,i)=two 
     m2(i+3,i+3)=six 
   END DO
   m2(1,2)=-one
   m2(1,3)=-one 
   m2(2,3)=-one
   m3(1,1)=dx
   m3(1,2)=dz 
   m3(1,3)=dy 
   m3(1,4)=txy  
   m3(1,5)=-two*tyz
   m3(1,6)=tzx 
   m3(2,2)=dy 
   m3(2,3)=dx 
   m3(2,4)=txy
   m3(2,5)=tyz 
   m3(2,6)=-two*tzx 
   m3(3,3)=dz
   m3(3,4)=-two*txy
   m3(3,5)=tyz 
   m3(3,6)=tzx
   m3(4,4)=-three*dz 
   m3(4,5)=three*tzx
   m3(4,6)=three*tyz
   m3(5,5)=-three*dx
   m3(5,6)=three*txy 
   m3(6,6)=-three*dy
   DO i=1,6 
     DO j=i+1,6
       m1(j,i)=m1(i,j) 
       m2(j,i)=m2(i,j)   
       m3(j,i)=m3(i,j)
     END DO
   END DO
   m1=m1/three
   m2=m2/three
   m3=m3/three
 CASE DEFAULT
   WRITE(*,*)"nst size not recognised in formm"
 END SELECT
RETURN   
END SUBROUTINE formm

SUBROUTINE contour(loads,g_coord,g_num,ned,argv,nlen,ips)
!
! This subroutine produces a PostScript output file "*.con" displaying
! a contour map of loads over the finite element mesh.
!
 IMPLICIT NONE
 INTEGER,PARAMETER::iwp=SELECTED_REAL_KIND(15)
 REAL(iwp),INTENT(IN):: loads(0:),g_coord(:,:)
 INTEGER,INTENT(IN)::g_num(:,:),ned,ips,nlen
 CHARACTER(*),INTENT(IN)::argv
 REAL(iwp)::xmin,xmax,ymin,ymax,width,height,scale=72,sxy,xo,yo
 REAL(iwp)::pmin,pmax,ratio,x12,y12,x23,y23,x34,y34,x41,y41,x1,y1
 REAL(iwp)::pt5=0.5_iwp,elvn=11.0_iwp,ept5=8.5_iwp,eight=8.0_iwp,         &
   onept5=1.5_iwp,fpt5=5.5_iwp
 LOGICAL::s12,s23,s34,s41,draw
 INTEGER,ALLOCATABLE::corner(:,:)
 REAL(iwp),ALLOCATABLE::cont(:)
 INTEGER::i,j,k,l,nn,nels,nci,nod,ns,i1,i2,j1,j2
!
 OPEN(ips,FILE=argv(1:nlen)//'.con')
!
!                       compute size of mesh
!
 nn=UBOUND(g_coord,2)
 nod= UBOUND(g_num,1)
 nels=UBOUND(g_num,2)
 xmin=g_coord(1,1)
 xmax=g_coord(1,1)
 ymin=g_coord(2,1)
 ymax=g_coord(2,1)
 DO i=2,nn
   IF(g_coord(1,i)<xmin)xmin=g_coord(1,i)      
   IF(g_coord(1,i)>xmax)xmax=g_coord(1,i)      
   IF(g_coord(2,i)<ymin)ymin=g_coord(2,i)      
   IF(g_coord(2,i)>ymax)ymax=g_coord(2,i)      
 END DO
 width =xmax-xmin
 height=ymax-ymin
!
!                       allow 1.5" margin minimum on each side of figure
!
!                       portrait mode 
!
 IF(height.GE.elvn/ept5*width)THEN 
!
!                       height governs the scale
!
   sxy=scale*eight/height
   xo=scale*pt5*(ept5-eight*width/height)
   yo=scale*onept5
 ELSE
!
!                       width governs the scale
!
   sxy=scale*fpt5/width
   xo=scale*onept5
   yo=scale*pt5*(elvn-fpt5*height/width)
 END IF
!
!                       find range of potentials and contour values
!
 nci=ned+1
 pmin=MINVAL(loads(1:))
 pmax=MAXVAL(loads(1:))
 ALLOCATE(cont(nci))
 cont(1)=pmin
 DO i=2,nci
   cont(i)=cont(i-1)+(pmax-pmin)/ned
 END DO
!
!                       start PostScript output
!
 WRITE(ips,'(a)')'%!PS-Adobe-1.0'
 WRITE(ips,'(a)')'%%DocumentFonts: none'
 WRITE(ips,'(a)')'%%Pages: 1'
 WRITE(ips,'(a)')'%%EndComments'
 WRITE(ips,'(a)')'/m {moveto} def'
 WRITE(ips,'(a)')'/l {lineto} def'
 WRITE(ips,'(a)')'/s {stroke} def'
 WRITE(ips,'(a)')'%%EndProlog'
 WRITE(ips,'(a)')'%%Page: 0 1'
 WRITE(ips,'(a)')'gsave'
!
 WRITE(ips,'(2f9.2,a)') xo, yo, ' translate'
 WRITE(ips,'(f9.2,a)') 1.0, ' setlinewidth'
!
!                       draw the mesh outline
!
 IF(nod==3.OR.nod==6.OR.nod==10.OR.nod==15)ns=3
 IF(nod==4.OR.nod==8.OR.nod==9)ns=4
 ALLOCATE(corner(ns,2))
 IF(nod== 3)corner=RESHAPE((/1,2,3,2,3,1/),(/3,2/))
 IF(nod== 6)corner=RESHAPE((/1,3,5,3,5,1/),(/3,2/))
 IF(nod==10)corner=RESHAPE((/1,4,7,4,7,1/),(/3,2/))
 IF(nod==15)corner=RESHAPE((/1,5,9,5,9,1/),(/3,2/))
 IF(nod== 4)corner=RESHAPE((/1,2,3,4,2,3,4,1/),(/4,2/))
 IF(nod== 8)corner=RESHAPE((/1,3,5,7,3,5,7,1/),(/4,2/))
 IF(nod== 9)corner=RESHAPE((/1,3,5,7,3,5,7,1/),(/4,2/))
 DO i=1,nels
   DO j=1,ns
     draw=.TRUE.
     i1=g_num(corner(j,1),i)
     i2=g_num(corner(j,2),i)
     DO k=1,nels
       DO l=1,ns
         j1=g_num(corner(l,1),k)
         j2=g_num(corner(l,2),k)
         IF((i1==j2).AND.(i2==j1))THEN
           draw=.FALSE.
           EXIT
         END IF
       END DO
       IF(.NOT.draw)EXIT
     END DO
     IF(draw)THEN
       x1=sxy*(g_coord(1,i1)-xmin)
       y1=sxy*(g_coord(2,i1)-ymin)
       WRITE(ips,'(2f9.2,a)') x1, y1,' m'
       x1=sxy*(g_coord(1,i2)-xmin)
       y1=sxy*(g_coord(2,i2)-ymin)
       WRITE(ips,'(2f9.2,a)') x1, y1,' l'
       WRITE(ips,'(a)')' s'
     END IF
   END DO
 END DO
!
!                       check intersection of contours with each element
!
 WRITE(ips,'(f9.2,a)') 0.5, ' setlinewidth'
 DO i=2,nci-1
   DO j=1,nels
     s12=.FALSE.
     s23=.FALSE.
     s34=.FALSE.
     s41=.FALSE.
     IF((loads(g_num(1,j))<=cont(i).AND.loads(g_num(2,j))>cont(i)).OR.    &
       (loads(g_num(2,j))<=cont(i).AND.loads(g_num(1,j))>cont(i)))        &
       s12=.TRUE.
     IF((loads(g_num(2,j))<=cont(i).AND.loads(g_num(3,j))>cont(i)).OR.    &
       (loads(g_num(3,j))<=cont(i).AND.loads(g_num(2,j))>cont(i)))        &
       s23=.TRUE.
     IF((loads(g_num(3,j))<=cont(i).AND.loads(g_num(4,j))>cont(i)).OR.    &
       (loads(g_num(4,j))<=cont(i).AND.loads(g_num(3,j))>cont(i)))        &
       s34=.TRUE.
     IF((loads(g_num(4,j))<=cont(i).AND.loads(g_num(1,j))>cont(i)).OR.    &
       (loads(g_num(1,j))<=cont(i).AND.loads(g_num(4,j))>cont(i)))        &
       s41=.TRUE.
     IF(s12)THEN
       ratio=(cont(i)-loads(g_num(1,j)))/                                 &
       (loads(g_num(2,j))-loads(g_num(1,j)))
       x12=sxy*(g_coord(1,g_num(1,j))+                                    &
         ratio*(g_coord(1,g_num(2,j))-g_coord(1,g_num(1,j)))-xmin)
       y12=sxy*(g_coord(2,g_num(1,j))+                                    &
         ratio*(g_coord(2,g_num(2,j))-g_coord(2,g_num(1,j)))-ymin)
     END IF
     IF(s23)THEN
       ratio=(cont(i)-loads(g_num(2,j)))/                                 &
         (loads(g_num(3,j))-loads(g_num(2,j)))
       x23=sxy*(g_coord(1,g_num(2,j))+                                    &
         ratio*(g_coord(1,g_num(3,j))-g_coord(1,g_num(2,j)))-xmin)
       y23=sxy*(g_coord(2,g_num(2,j))+                                    &
         ratio*(g_coord(2,g_num(3,j))-g_coord(2,g_num(2,j)))-ymin)
     END IF
     IF(s34)THEN
       ratio=(cont(i)-loads(g_num(3,j)))/                                 &
         (loads(g_num(4,j))-loads(g_num(3,j)))
       x34=sxy*(g_coord(1,g_num(3,j))+                                    &
         ratio*(g_coord(1,g_num(4,j))-g_coord(1,g_num(3,j)))-xmin)
       y34=sxy*(g_coord(2,g_num(3,j))+                                    &
         ratio*(g_coord(2,g_num(4,j))-g_coord(2,g_num(3,j)))-ymin)
     END IF
     IF(s41)THEN
       ratio=(cont(i)-loads(g_num(4,j)))/                                 &
         (loads(g_num(1,j))-loads(g_num(4,j)))
       x41=sxy*(g_coord(1,g_num(4,j))+                                    &
         ratio*(g_coord(1,g_num(1,j))-g_coord(1,g_num(4,j)))-xmin)
       y41=sxy*(g_coord(2,g_num(4,j))+                                    &
         ratio*(g_coord(2,g_num(1,j))-g_coord(2,g_num(4,j)))-ymin)
     END IF
!
!                       draw contours 
!
     IF(s12)THEN
       WRITE(ips,'(2f9.2,a)')x12,y12,' m'
       IF(s23)WRITE(ips,'(2f9.2,a)')x23,y23,' l s'
       IF(s34)WRITE(ips,'(2f9.2,a)')x34,y34,' l s'
       IF(s41)WRITE(ips,'(2f9.2,a)')x41,y41,' l s'
     END IF
     IF(s23)THEN
       WRITE(ips,'(2f9.2,a)')x23,y23,' m'
       IF(s34)WRITE(ips,'(2f9.2,a)')x34,y34,' l s'
       IF(s41)WRITE(ips,'(2f9.2,a)')x41,y41,' l s'
     END IF
     IF(s34.AND.s41)THEN
       WRITE(ips,'(2f9.2,a)')x34,y34,' m'
       WRITE(ips,'(2f9.2,a)')x41,y41,' l s'
     END IF
   END DO
 END DO
!                       close output file
 WRITE(ips,'(a)')'grestore'
 WRITE(ips,'(a)')'showpage'
 CLOSE(ips)
!
RETURN
END SUBROUTINE contour

SUBROUTINE cross_product(b,c,a)
!
! This subroutine forms the cross product of two vectors, a = b x c
!
 IMPLICIT NONE
 INTEGER,PARAMETER::iwp=SELECTED_REAL_KIND(15)
 REAL(iwp),INTENT(IN)::b(:),c(:)
 REAL(iwp),INTENT(OUT)::a(:,:)
 INTEGER::ib,ic,i,j
 ib=SIZE(b)
 ic=SIZE(c)
 DO i=1,ib
   DO j=1,ic
     a(i,j)=b(i)*c(j)
   END DO
 END DO
RETURN
END SUBROUTINE cross_product  

SUBROUTINE stiff4(km,coord,e,v)
!
! This subroutine generates the "analytical" stiffness matrix
! for a four node quadrilateral in plane strain based on nip=4.
!
 IMPLICIT NONE
 INTEGER,PARAMETER::iwp=SELECTED_REAL_KIND(15)
 REAL(iwp),INTENT(IN)::coord(:,:),e,v
 REAL(iwp),INTENT(OUT)::km(:,:)
 REAL(iwp)::e1,e2,g,x1,x2,x3,x4,y1,y2,y3,y4,a2,a2st3,alph,beta,c11,c21,   &
   c31,c41,c51,c61,c71,c81,c22,c32,c42,c52,c62,c72,c82,c33,c43,c53,c63,   &
   c73,c83,c44,c54,c64,c74,c84,c55,c65,c75,c85,c66,c76,c86,c77,c87,c88,   &
   f1,f2,s1,s2,s3,s4,t1,t2,t3,t4,pt5=0.5_iwp,one=1.0_iwp,two=2.0_iwp,     &
   d3=3.0_iwp,zero=0.0_iwp
 INTEGER::i,j
 e1=e*(one-v)/(one+v)/(1-two*v)
 e2=v*e1/(one-v)
 g=e/two/(one+v)      
!
 x1=coord(1,1)
 x2=coord(2,1)
 x3=coord(3,1)
 x4=coord(4,1)
 y1=coord(1,2)
 y2=coord(2,2)
 y3=coord(3,2)
 y4=coord(4,2)
!
 a2=(x4-x2)*(y3-y1)-(x3-x1)*(y4-y2)
 a2st3=d3*a2*a2
!
 CALL groupa(x1,x2,x3,x4,y1,y2,y3,y4,s1,s2,s3,s4,t1,t2,t3,t4,f1,f2)
 alph=a2*(s1*e1+s2*g)+f1*(s3*e1+s4*g)
 beta=a2*(t1*e1+t2*g)+f2*(t3*e1+t4*g)
 c11=(alph/(a2st3-f1**2)+beta/(a2st3-f2**2))*pt5
 km(1,1)=c11
!
 CALL groupa(x2,x3,x4,x1,y2,y3,y4,y1,s1,s2,s3,s4,t1,t2,t3,t4,f1,f2)
 alph=a2*(s1*e1+s2*g)+f1*(s3*e1+s4*g)
 beta=a2*(t1*e1+t2*g)+f2*(t3*e1+t4*g)
 c33=(alph/(a2st3-f1**2)+beta/(a2st3-f2**2))*pt5
 km(3,3)=c33
!
 CALL groupa(x3,x4,x1,x2,y3,y4,y1,y2,s1,s2,s3,s4,t1,t2,t3,t4,f1,f2)
 alph=a2*(s1*e1+s2*g)+f1*(s3*e1+s4*g)
 beta=a2*(t1*e1+t2*g)+f2*(t3*e1+t4*g)
 c55=(alph/(a2st3-f1**2)+beta/(a2st3-f2**2))*pt5
 km(5,5)=c55
!
 CALL groupa(x4,x1,x2,x3,y4,y1,y2,y3,s1,s2,s3,s4,t1,t2,t3,t4,f1,f2)
 alph=a2*(s1*e1+s2*g)+f1*(s3*e1+s4*g)
 beta=a2*(t1*e1+t2*g)+f2*(t3*e1+t4*g)
 c77=(alph/(a2st3-f1**2)+beta/(a2st3-f2**2))*pt5
 km(7,7)=c77
!
 CALL groupa(y4,y3,y2,y1,x4,x3,x2,x1,s1,s2,s3,s4,t1,t2,t3,t4,f1,f2)
 alph=a2*(s1*e1+s2*g)+f1*(s3*e1+s4*g)
 beta=a2*(t1*e1+t2*g)+f2*(t3*e1+t4*g)
 c88=(alph/(a2st3-f1**2)+beta/(a2st3-f2**2))*pt5
 km(8,8)=c88
!
 CALL groupa(y1,y4,y3,y2,x1,x4,x3,x2,s1,s2,s3,s4,t1,t2,t3,t4,f1,f2)
 alph=a2*(s1*e1+s2*g)+f1*(s3*e1+s4*g)
 beta=a2*(t1*e1+t2*g)+f2*(t3*e1+t4*g)
 c22=(alph/(a2st3-f1**2)+beta/(a2st3-f2**2))*pt5
 km(2,2)=c22
!
 CALL groupa(y2,y1,y4,y3,x2,x1,x4,x3,s1,s2,s3,s4,t1,t2,t3,t4,f1,f2)
 alph=a2*(s1*e1+s2*g)+f1*(s3*e1+s4*g)
 beta=a2*(t1*e1+t2*g)+f2*(t3*e1+t4*g)
 c44=(alph/(a2st3-f1**2)+beta/(a2st3-f2**2))*pt5
 km(4,4)=c44
!
 CALL groupa(y3,y2,y1,y4,x3,x2,x1,x4,s1,s2,s3,s4,t1,t2,t3,t4,f1,f2)
 alph=a2*(s1*e1+s2*g)+f1*(s3*e1+s4*g)
 beta=a2*(t1*e1+t2*g)+f2*(t3*e1+t4*g)
 c66=(alph/(a2st3-f1**2)+beta/(a2st3-f2**2))*pt5
 km(6,6)=c66
! 
 CALL groupb(x1,x2,x3,x4,y1,y2,y3,y4,s1,s2,s3,s4,t1,t2,t3,t4,f1,f2)
 alph=a2*(s1*e2+s2*g)+f1*(s3*e2+s4*g)
 beta=a2*(t1*e2+t2*g)+f2*(t3*e2+t4*g)
 c21=(alph/(a2st3-f1**2)+beta/(a2st3-f2**2))*pt5
 km(2,1)=c21
!
 CALL groupb(x2,x3,x4,x1,y2,y3,y4,y1,s1,s2,s3,s4,t1,t2,t3,t4,f1,f2)
 alph=a2*(s1*e2+s2*g)+f1*(s3*e2+s4*g)
 beta=a2*(t1*e2+t2*g)+f2*(t3*e2+t4*g)
 c43=(alph/(a2st3-f1**2)+beta/(a2st3-f2**2))*pt5
 km(4,3)=c43
!
 CALL groupb(x3,x4,x1,x2,y3,y4,y1,y2,s1,s2,s3,s4,t1,t2,t3,t4,f1,f2)
 alph=a2*(s1*e2+s2*g)+f1*(s3*e2+s4*g)
 beta=a2*(t1*e2+t2*g)+f2*(t3*e2+t4*g)
 c65=(alph/(a2st3-f1**2)+beta/(a2st3-f2**2))*pt5
 km(6,5)=c65
!
 CALL groupb(x4,x1,x2,x3,y4,y1,y2,y3,s1,s2,s3,s4,t1,t2,t3,t4,f1,f2)
 alph=a2*(s1*e2+s2*g)+f1*(s3*e2+s4*g)
 beta=a2*(t1*e2+t2*g)+f2*(t3*e2+t4*g)
 c87=(alph/(a2st3-f1**2)+beta/(a2st3-f2**2))*pt5
 km(8,7)=c87
!
 CALL groupc(x1,x2,x3,x4,y1,y2,y3,y4,s1,s2,s3,s4,t1,t2,t3,t4,f1,f2)
 alph=a2*(s1*e1+s2*g)+f1*(s3*e1+s4*g)
 beta=a2*(t1*e1+t2*g)+f2*(t3*e1+t4*g)
 c31=(alph/(a2st3-f1**2)+beta/(a2st3-f2**2))*pt5
 km(3,1)=c31
!
 CALL groupc(x2,x3,x4,x1,y2,y3,y4,y1,s1,s2,s3,s4,t1,t2,t3,t4,f1,f2)
 alph=a2*(s1*e1+s2*g)+f1*(s3*e1+s4*g)
 beta=a2*(t1*e1+t2*g)+f2*(t3*e1+t4*g)
 c53=(alph/(a2st3-f1**2)+beta/(a2st3-f2**2))*pt5
 km(5,3)=c53
!
 CALL groupc(x3,x4,x1,x2,y3,y4,y1,y2,s1,s2,s3,s4,t1,t2,t3,t4,f1,f2)
 alph=a2*(s1*e1+s2*g)+f1*(s3*e1+s4*g)
 beta=a2*(t1*e1+t2*g)+f2*(t3*e1+t4*g)
 c75=(alph/(a2st3-f1**2)+beta/(a2st3-f2**2))*pt5
 km(7,5)=c75
!
 CALL groupc(x4,x1,x2,x3,y4,y1,y2,y3,s1,s2,s3,s4,t1,t2,t3,t4,f1,f2)
 alph=a2*(s1*e1+s2*g)+f1*(s3*e1+s4*g)
 beta=a2*(t1*e1+t2*g)+f2*(t3*e1+t4*g)
 c71=(alph/(a2st3-f1**2)+beta/(a2st3-f2**2))*pt5
 km(7,1)=c71
! 
 CALL groupc(y4,y3,y2,y1,x4,x3,x2,x1,s1,s2,s3,s4,t1,t2,t3,t4,f1,f2)
 alph=a2*(s1*e1+s2*g)+f1*(s3*e1+s4*g)
 beta=a2*(t1*e1+t2*g)+f2*(t3*e1+t4*g)
 c86=(alph/(a2st3-f1**2)+beta/(a2st3-f2**2))*pt5
 km(8,6)=c86
!
 CALL groupc(y1,y4,y3,y2,x1,x4,x3,x2,s1,s2,s3,s4,t1,t2,t3,t4,f1,f2)
 alph=a2*(s1*e1+s2*g)+f1*(s3*e1+s4*g)
 beta=a2*(t1*e1+t2*g)+f2*(t3*e1+t4*g)
 c82=(alph/(a2st3-f1**2)+beta/(a2st3-f2**2))*pt5
 km(8,2)=c82
!
 CALL groupc(y2,y1,y4,y3,x2,x1,x4,x3,s1,s2,s3,s4,t1,t2,t3,t4,f1,f2)
 alph=a2*(s1*e1+s2*g)+f1*(s3*e1+s4*g)
 beta=a2*(t1*e1+t2*g)+f2*(t3*e1+t4*g)
 c42=(alph/(a2st3-f1**2)+beta/(a2st3-f2**2))*pt5
 km(4,2)=c42
!
 CALL groupc(y3,y2,y1,y4,x3,x2,x1,x4,s1,s2,s3,s4,t1,t2,t3,t4,f1,f2)
 alph=a2*(s1*e1+s2*g)+f1*(s3*e1+s4*g)
 beta=a2*(t1*e1+t2*g)+f2*(t3*e1+t4*g)
 c64=(alph/(a2st3-f1**2)+beta/(a2st3-f2**2))*pt5
 km(6,4)=c64
! 
 CALL groupd(x1,x2,x3,x4,y1,y2,y3,y4,s1,s2,s3,s4,t1,t2,t3,t4)
 CALL f1f2(x1,x2,x3,x4,y1,y2,y3,y4,f1,f2)
 alph=a2*(s1*e2+s2*g)+f1*(s3*e2+s4*g)
 beta=a2*(t1*e2+t2*g)+f2*(t3*e2+t4*g)
 c41=(alph/(a2st3-f1**2)+beta/(a2st3-f2**2))*pt5
 km(4,1)=c41
!
 CALL groupd(x2,x3,x4,x1,y2,y3,y4,y1,s1,s2,s3,s4,t1,t2,t3,t4)
 CALL f1f2(x2,x3,x4,x1,y2,y3,y4,y1,f1,f2)
 alph=a2*(s1*e2+s2*g)+f1*(s3*e2+s4*g)
 beta=a2*(t1*e2+t2*g)+f2*(t3*e2+t4*g)
 c63=(alph/(a2st3-f1**2)+beta/(a2st3-f2**2))*pt5
 km(6,3)=c63
!
 CALL groupd(x3,x4,x1,x2,y3,y4,y1,y2,s1,s2,s3,s4,t1,t2,t3,t4)
 CALL f1f2(x3,x4,x1,x2,y3,y4,y1,y2,f1,f2)
 alph=a2*(s1*e2+s2*g)+f1*(s3*e2+s4*g)
 beta=a2*(t1*e2+t2*g)+f2*(t3*e2+t4*g)
 c85=(alph/(a2st3-f1**2)+beta/(a2st3-f2**2))*pt5
 km(8,5)=c85
!
 CALL groupd(x4,x1,x2,x3,y4,y1,y2,y3,s1,s2,s3,s4,t1,t2,t3,t4)
 CALL f1f2(x4,x1,x2,x3,y4,y1,y2,y3,f1,f2)
 alph=a2*(s1*e2+s2*g)+f1*(s3*e2+s4*g)
 beta=a2*(t1*e2+t2*g)+f2*(t3*e2+t4*g)
 c72=(alph/(a2st3-f1**2)+beta/(a2st3-f2**2))*pt5
 km(7,2)=c72
!
 CALL groupd(y1,y2,y3,y4,x1,x2,x3,x4,s1,s2,s3,s4,t1,t2,t3,t4)
 CALL f1f2(x1,x2,x3,x4,y1,y2,y3,y4,f1,f2)
 alph=a2*(s1*e2+s2*g)+f1*(s3*e2+s4*g)
 beta=a2*(t1*e2+t2*g)+f2*(t3*e2+t4*g)
 c32=(alph/(a2st3-f1**2)+beta/(a2st3-f2**2))*pt5
 km(3,2)=c32
!
 CALL groupd(y2,y3,y4,y1,x2,x3,x4,x1,s1,s2,s3,s4,t1,t2,t3,t4)
 CALL f1f2(x2,x3,x4,x1,y2,y3,y4,y1,f1,f2)
 alph=a2*(s1*e2+s2*g)+f1*(s3*e2+s4*g)
 beta=a2*(t1*e2+t2*g)+f2*(t3*e2+t4*g)
 c54=(alph/(a2st3-f1**2)+beta/(a2st3-f2**2))*pt5
 km(5,4)=c54
!
 CALL groupd(y3,y4,y1,y2,x3,x4,x1,x2,s1,s2,s3,s4,t1,t2,t3,t4)
 CALL f1f2(x3,x4,x1,x2,y3,y4,y1,y2,f1,f2)
 alph=a2*(s1*e2+s2*g)+f1*(s3*e2+s4*g)
 beta=a2*(t1*e2+t2*g)+f2*(t3*e2+t4*g)
 c76=(alph/(a2st3-f1**2)+beta/(a2st3-f2**2))*pt5
 km(7,6)=c76
!
 CALL groupd(y4,y1,y2,y3,x4,x1,x2,x3,s1,s2,s3,s4,t1,t2,t3,t4)
 CALL f1f2(x4,x1,x2,x3,y4,y1,y2,y3,f1,f2)
 alph=a2*(s1*e2+s2*g)+f1*(s3*e2+s4*g)
 beta=a2*(t1*e2+t2*g)+f2*(t3*e2+t4*g)
 c81=(alph/(a2st3-f1**2)+beta/(a2st3-f2**2))*pt5
 km(8,1)=c81
!
 CALL groupe(x1,x2,x3,x4,y1,y2,y3,y4,s1,s2,s3,s4,t1,t2,t3,t4,f1,f2)
 alph=a2*(s1*e1+s2*g)+f1*(s3*e1+s4*g)
 beta=a2*(t1*e1+t2*g)+f2*(t3*e1+t4*g)
 c51=(alph/(a2st3-f1**2)+beta/(a2st3-f2**2))*pt5
 km(5,1)=c51
!
 CALL groupe(x2,x3,x4,x1,y2,y3,y4,y1,s1,s2,s3,s4,t1,t2,t3,t4,f1,f2)
 alph=a2*(s1*e1+s2*g)+f1*(s3*e1+s4*g)
 beta=a2*(t1*e1+t2*g)+f2*(t3*e1+t4*g)
 c73=(alph/(a2st3-f1**2)+beta/(a2st3-f2**2))*pt5
 km(7,3)=c73
!
 CALL groupe(y2,y1,y4,y3,x2,x1,x4,x3,s1,s2,s3,s4,t1,t2,t3,t4,f1,f2)
 alph=a2*(s1*e1+s2*g)+f1*(s3*e1+s4*g)
 beta=a2*(t1*e1+t2*g)+f2*(t3*e1+t4*g)
 c84=(alph/(a2st3-f1**2)+beta/(a2st3-f2**2))*pt5
 km(8,4)=c84
!
 CALL groupe(y3,y2,y1,y4,x3,x2,x1,x4,s1,s2,s3,s4,t1,t2,t3,t4,f1,f2)
 alph=a2*(s1*e1+s2*g)+f1*(s3*e1+s4*g)
 beta=a2*(t1*e1+t2*g)+f2*(t3*e1+t4*g)
 c62=(alph/(a2st3-f1**2)+beta/(a2st3-f2**2))*pt5
 km(6,2)=c62
!
 CALL groupf(x1,x2,x3,x4,y1,y2,y3,y4,s1,s2,s3,s4,t1,t2,t3,t4)
 CALL f1f2(x1,x2,x3,x4,y1,y2,y3,y4,f1,f2)
 alph=a2*(s1*e2+s2*g)+f1*(s3*e2+s4*g)
 beta=a2*(t1*e2+t2*g)+f2*(t3*e2+t4*g)
 c61=(alph/(a2st3-f1**2)+beta/(a2st3-f2**2))*pt5
 km(6,1)=c61
!
 CALL groupf(x2,x3,x4,x1,y2,y3,y4,y1,s1,s2,s3,s4,t1,t2,t3,t4)
 CALL f1f2(x2,x3,x4,x1,y2,y3,y4,y1,f1,f2)
 alph=a2*(s1*e2+s2*g)+f1*(s3*e2+s4*g)
 beta=a2*(t1*e2+t2*g)+f2*(t3*e2+t4*g)
 c83=(alph/(a2st3-f1**2)+beta/(a2st3-f2**2))*pt5
 km(8,3)=c83
!
 CALL groupf(y1,y2,y3,y4,x1,x2,x3,x4,s1,s2,s3,s4,t1,t2,t3,t4)
 CALL f1f2(x1,x2,x3,x4,y1,y2,y3,y4,f1,f2)
 alph=a2*(s1*e2+s2*g)+f1*(s3*e2+s4*g)
 beta=a2*(t1*e2+t2*g)+f2*(t3*e2+t4*g)
 c52=(alph/(a2st3-f1**2)+beta/(a2st3-f2**2))*pt5
 km(5,2)=c52
!
 CALL groupf(y2,y3,y4,y1,x2,x3,x4,x1,s1,s2,s3,s4,t1,t2,t3,t4)
 CALL f1f2(x2,x3,x4,x1,y2,y3,y4,y1,f1,f2)
 alph=a2*(s1*e2+s2*g)+f1*(s3*e2+s4*g)
 beta=a2*(t1*e2+t2*g)+f2*(t3*e2+t4*g)
 c74=(alph/(a2st3-f1**2)+beta/(a2st3-f2**2))*pt5
 km(7,4)=c74
!
 DO i=1,8
   DO j=i+1,8 
     km(i,j)=km(j,i) 
   END DO 
 END DO
!
RETURN 
CONTAINS 
SUBROUTINE groupa(x1,x2,x3,x4,y1,y2,y3,y4,s1,s2,s3,s4,t1,t2,t3,t4,f1,f2)
 IMPLICIT NONE
 INTEGER,PARAMETER::iwp=SELECTED_REAL_KIND(15)
 REAL(iwp),INTENT(IN)::x1,x2,x3,x4,y1,y2,y3,y4
 REAL(iwp),INTENT(OUT)::s1,s2,s3,s4,t1,t2,t3,t4,f1,f2
 s1=two*(y4-y2)**2
 s2=two*(x4-x2)**2
 s3=-s1/two
 s4=-s2/two
 t1=(y2-y3)**2+(y3-y4)**2+(y4-y2)**2
 t2=(x2-x3)**2+(x3-x4)**2+(x4-x2)**2
 t3=(y4-y3)**2-(y3-y2)**2
 t4=(x4-x3)**2-(x3-x2)**2
 f1=(x1+x3)*(y4-y2)-(y1+y3)*(x4-x2)-two*(x2*y4-x4*y2)
 f2=(y2+y4)*(x3-x1)-(x2+x4)*(y3-y1)-two*(x3*y1-x1*y3)
RETURN
END SUBROUTINE groupa
SUBROUTINE groupb(x1,x2,x3,x4,y1,y2,y3,y4,s1,s2,s3,s4,t1,t2,t3,t4,f1,f2)
 IMPLICIT NONE
 INTEGER,PARAMETER::iwp=SELECTED_REAL_KIND(15)
 REAL(iwp),INTENT(IN)::x1,x2,x3,x4,y1,y2,y3,y4
 REAL(iwp),INTENT(OUT)::s1,s2,s3,s4,t1,t2,t3,t4,f1,f2
 s1=two*(x2-x4)*(y4-y2)
 s2=s1
 s3=-s1/2
 s4=s3
 t1=x2*(y4-two*y2+y3)+x3*(y2-two*y3+y4)+x4*(y2-two*y4+y3)
 t2=t1
 t3=x2*(y2-y3)+x3*(y4-y2)+x4*(y3-y4)
 t4=t3
 f1=(x1+x3)*(y4-y2)-(y1+y3)*(x4-x2)-two*(x2*y4-x4*y2)
 f2=(y2+y4)*(x3-x1)-(x2+x4)*(y3-y1)-two*(x3*y1-x1*y3)
RETURN
END SUBROUTINE groupb
SUBROUTINE groupc(x1,x2,x3,x4,y1,y2,y3,y4,s1,s2,s3,s4,t1,t2,t3,t4,f1,f2)
 IMPLICIT NONE
 INTEGER,PARAMETER::iwp=SELECTED_REAL_KIND(15)
 REAL(iwp),INTENT(IN)::x1,x2,x3,x4,y1,y2,y3,y4
 REAL(iwp),INTENT(OUT)::s1,s2,s3,s4,t1,t2,t3,t4,f1,f2
 s1=(y4-y2)*(two*y1-y3-y4)
 s2=(x4-x2)*(two*x1-x3-x4)
 s3=(y4-y2)*(y4-y1)
 s4=(x4-x2)*(x4-x1)
 t1=(y3-y1)*(two*y2-y3-y4)
 t2=(x3-x1)*(two*x2-x3-x4)
 t3=(y3-y1)*(y3-y2)
 t4=(x3-x1)*(x3-x2)
 f1=(x1+x3)*(y4-y2)-(y1+y3)*(x4-x2)-two*(x2*y4-x4*y2)
 f2=(y2+y4)*(x3-x1)-(x2+x4)*(y3-y1)-two*(x3*y1-x1*y3)
RETURN
END SUBROUTINE groupc
SUBROUTINE groupd(x1,x2,x3,x4,y1,y2,y3,y4,s1,s2,s3,s4,t1,t2,t3,t4)
 IMPLICIT NONE
 INTEGER,PARAMETER::iwp=SELECTED_REAL_KIND(15)
 REAL(iwp),INTENT(IN)::x1,x2,x3,x4,y1,y2,y3,y4
 REAL(iwp),INTENT(OUT)::s1,s2,s3,s4,t1,t2,t3,t4
 s1=(x3-x1)*(y4-y2)+(x4-x1)*(y4-y2)
 s2=(y3-y1)*(x4-x2)+(y4-y1)*(x4-x2)
 s3=(x4-x1)*(y2-y4)
 s4=(y4-y1)*(x2-x4)
 t1=(x3-x1)*(y4-y2)+(x3-x1)*(y3-y2)
 t2=(y3-y1)*(x4-x2)+(y3-y1)*(x3-x2)
 t3=(x3-x1)*(y2-y3)
 t4=(y3-y1)*(x2-x3)
RETURN
END SUBROUTINE groupd
SUBROUTINE groupe(x1,x2,x3,x4,y1,y2,y3,y4,s1,s2,s3,s4,t1,t2,t3,t4,f1,f2)
 IMPLICIT NONE
 INTEGER,PARAMETER::iwp=SELECTED_REAL_KIND(15)
 REAL(iwp),INTENT(IN)::x1,x2,x3,x4,y1,y2,y3,y4
 REAL(iwp),INTENT(OUT)::s1,s2,s3,s4,t1,t2,t3,t4,f1,f2
 s1=-(y4-y2)**2
 s2=-(x4-x2)**2
 s3=zero
 s4=zero
 t1=(y3+y1)*(y4+y2)-two*(y4-y2)**2-two*(y1*y3+y2*y4)
 t2=(x3+x1)*(x4+x2)-two*(x4-x2)**2-two*(x1*x3+x2*x4)
 t3=(y4-y2)*(y1-y2+y3-y4)
 t4=(x4-x2)*(x1-x2+x3-x4)
 f1=(x1+x3)*(y4-y2)-(y1+y3)*(x4-x2)-two*(x2*y4-x4*y2)
 f2=(y2+y4)*(x3-x1)-(x2+x4)*(y3-y1)-two*(x3*y1-x1*y3)
RETURN
END SUBROUTINE groupe
SUBROUTINE groupf(x1,x2,x3,x4,y1,y2,y3,y4,s1,s2,s3,s4,t1,t2,t3,t4)
 IMPLICIT NONE
 INTEGER,PARAMETER::iwp=SELECTED_REAL_KIND(15)
 REAL(iwp),INTENT(IN)::x1,x2,x3,x4,y1,y2,y3,y4
 REAL(iwp),INTENT(OUT)::s1,s2,s3,s4,t1,t2,t3,t4
 s1=(x4-x2)*(y4-y2)
 s2=s1
 s3=zero
 s4=zero
 t1=(x4-x2)*(y4-y2)+(x2-x1)*(y2-y3)+(x4-x1)*(y4-y3)
 t2=(y4-y2)*(x4-x2)+(y2-y1)*(x2-x3)+(y4-y1)*(x4-x3)
 t3=(x2-x1)*(y3-y2)+(x4-x1)*(y4-y3)
 t4=(y2-y1)*(x3-x2)+(y4-y1)*(x4-x3)
RETURN
END SUBROUTINE groupf
SUBROUTINE f1f2(x1,x2,x3,x4,y1,y2,y3,y4,f1,f2)
 IMPLICIT NONE
 INTEGER,PARAMETER::iwp=SELECTED_REAL_KIND(15)
 REAL(iwp),INTENT(IN)::x1,x2,x3,x4,y1,y2,y3,y4
 REAL(iwp),INTENT(OUT)::f1,f2
 f1=(x1+x3)*(y4-y2)-(y1+y3)*(x4-x2)-two*(x2*y4-x4*y2)
 f2=(y2+y4)*(x3-x1)-(x2+x4)*(y3-y1)-two*(x3*y1-x1*y3)
RETURN
END SUBROUTINE f1f2
END SUBROUTINE stiff4

SUBROUTINE linmul_sky(kv,disps,loads,kdiag)
!
! This subroutine forms the product of symmetric matrix stored as
! a skyline and a vector.
!
 IMPLICIT NONE
 INTEGER,PARAMETER::iwp=SELECTED_REAL_KIND(15)
 REAL(iwp),INTENT(IN)::kv(:),disps(0:)
 REAL(iwp),INTENT(OUT)::loads(0:)
 INTEGER,INTENT(IN)::kdiag(:)
 INTEGER::n,i,j,low,lup,k
 REAL(iwp)::x,zero=0.0_iwp
 n=UBOUND(disps,1)
 DO i=1,n
   x=zero 
   lup=kdiag(i)
   IF(i==1)low=lup
   IF(i/=1)low=kdiag(i-1)+1
   DO j=low,lup
     x=x+kv(j)*disps(i+j-lup) 
   END DO
   loads(i)=x
   IF(i==1)CYCLE   
   lup=lup-1
   DO j=low,lup
     k=i+j-lup-1
     loads(k)=loads(k)+kv(j)*disps(i)        
   END DO
 END DO
RETURN
END SUBROUTINE linmul_sky  

SUBROUTINE ecmat(ecm,fun,ndof,nodof)
!
! This subroutine forms the element consistent mass matrix.
!
 IMPLICIT NONE
 INTEGER,PARAMETER::iwp=SELECTED_REAL_KIND(15)
 REAL(iwp),INTENT(IN)::fun(:)
 REAL(iwp),INTENT(OUT)::ecm(:,:)
 INTEGER,INTENT(IN)::nodof,ndof
 INTEGER::nod,i,j
 REAL::nt(ndof,nodof),tn(nodof,ndof),zero=0.0_iwp
 nod=ndof/nodof
 nt=zero
 tn=zero
 DO i=1,nod 
   DO j=1,nodof
     nt((i-1)*nodof+j,j)=fun(i)
     tn(j,(i-1)*nodof+j)=fun(i)
   END DO
 END DO
 ecm=MATMUL(nt,tn)
RETURN
END SUBROUTINE ecmat

SUBROUTINE geom_rect(element,iel,x_coords,y_coords,coord,num,dir)
!
! This subroutine forms the coordinates and connectivity for a
! rectangular mesh of rt. angled triangular elements (3, 6, 10 or 15-node)
! or quadrilateral elements (4, 8 or 9-node) counting in the
! x- or y-dir. 
!
 IMPLICIT NONE
 INTEGER,PARAMETER::iwp=SELECTED_REAL_KIND(15)
 REAL(iwp),INTENT(IN)::x_coords(:),y_coords(:)
 REAL(iwp),INTENT(OUT)::coord(:,:)
 CHARACTER(LEN=15),INTENT(IN)::element
 CHARACTER(LEN=1),INTENT(IN)::dir
 INTEGER,INTENT(IN)::iel
 INTEGER,INTENT(OUT)::num(:)
 INTEGER::ip,iq,jel,fac1,nod,nxe,nye
 REAL(iwp)::pt5=0.5_iwp,two=2.0_iwp,d3=3.0_iwp 
 nxe=UBOUND(x_coords,1)-1
 nod=UBOUND(num,1)
 IF(element=='triangle')THEN
   nye=(UBOUND(y_coords,1)-1)*2
   IF(dir=='x'.OR.dir=='r')THEN
     jel=2*nxe*((iel-1)/(2*nxe))
     ip=(iel-jel+1)/2
     iq=2*((iel-1)/(2*nxe)+1)-1+((iel/2)*2)/iel
   ELSE  
     jel=(iel-1)/nye
     ip=jel+1
     iq=iel-nye*jel
   END IF
   SELECT CASE(nod)
   CASE(3)
     IF(MOD(iq,2)/=0)THEN
       IF(dir=='x'.OR.dir=='r')THEN
         num(1)=(nxe+1)*(iq-1)/2+ip
         num(2)=num(1)+1              
         num(3)=(nxe+1)*(iq+1)/2+ip
       ELSE
         num(1)=(ip-1)*(nye+2)/2+(iq+1)/2
         num(2)=num(1)+(nye+2)/2
         num(3)=num(1)+1
       END IF
!
       coord(1,1)=x_coords(ip)
       coord(1,2)=y_coords((iq+1)/2)
       coord(2,1)=x_coords(ip+1)   
       coord(2,2)=y_coords((iq+1)/2)
       coord(3,1)=x_coords(ip)   
       coord(3,2)=y_coords((iq+3)/2)
     ELSE
       IF(dir=='x'.OR.dir=='r')THEN
         num(1)=(nxe+1)*iq/2+ip+1     
         num(2)=num(1)-1               
         num(3)=(nxe+1)*(iq-2)/2+ip+1
       ELSE
         num(1)=ip*(nye+2)/2+(iq+2)/2
         num(2)=(ip-1)*(nye+2)/2+(iq+1)/2+1
         num(3)=num(1)-1
       END IF
!
       coord(1,1)=x_coords(ip+1)
       coord(1,2)=y_coords((iq+2)/2)
       coord(2,1)=x_coords(ip)   
       coord(2,2)=y_coords((iq+2)/2)
       coord(3,1)=x_coords(ip+1) 
       coord(3,2)=y_coords(iq/2)
     END IF
   CASE(6)
     IF(MOD(iq,2)/=0)THEN
       IF(dir=='x'.OR.dir=='r')THEN
         num(1)=(iq-1)*(2*nxe+1)+2*ip-1
         num(2)=num(1)+1 
         num(3)=num(1)+2 
         num(4)=(iq-1)*(2*nxe+1)+2*nxe+2*ip+1
         num(5)=(iq+1)*(2*nxe+1)+2*ip-1
         num(6)=num(4)-1 
       ELSE
         num(1)=2*(nye+1)*(ip-1)+iq
         num(2)=2*(nye+1)*(ip-1)+nye+1+iq
         num(3)=2*(nye+1)*ip+iq
         num(4)=num(2)+1
         num(5)=num(1)+2 
         num(6)=num(1)+1
       END IF
!
       coord(1,1)=x_coords(ip)
       coord(1,2)=y_coords((iq+1)/2)
       coord(3,1)=x_coords(ip+1)   
       coord(3,2)=y_coords((iq+1)/2)
       coord(5,1)=x_coords(ip)   
       coord(5,2)=y_coords((iq+3)/2)
     ELSE
       IF(dir=='x'.OR.dir=='r')THEN
         num(1)=iq*(2*nxe+1)+2*ip+1
         num(2)=num(1)-1 
         num(3)=num(1)-2 
         num(4)=(iq-2)*(2*nxe+1)+2*nxe+2*ip+1
         num(5)=(iq-2)*(2*nxe+1)+2*ip+1
         num(6)=num(4)+1 
       ELSE 
         num(1)=2*(nye+1)*ip+iq+1 
         num(2)=2*(nye+1)*(ip-1)+nye+iq+2
         num(3)=2*(nye+1)*(ip-1)+iq+1
         num(4)=num(2)-1 
         num(5)=num(1)-2
         num(6)=num(1)-1
       END IF
!
       coord(1,1)=x_coords(ip+1)
       coord(1,2)=y_coords((iq+2)/2)
       coord(3,1)=x_coords(ip)   
       coord(3,2)=y_coords((iq+2)/2)
       coord(5,1)=x_coords(ip+1) 
       coord(5,2)=y_coords(iq/2)
     END IF
     coord(2,:)=pt5*(coord(1,:)+coord(3,:))
     coord(4,:)=pt5*(coord(3,:)+coord(5,:))
     coord(6,:)=pt5*(coord(5,:)+coord(1,:))
   CASE(10)
     IF(MOD(iq,2)/=0)THEN
       IF(dir=='x'.OR.dir=='r')THEN
         num(1)=(iq-1)/2*(3*nxe+1)*3+3*ip-2
         num(2)=num(1)+1
         num(3)=num(1)+2
         num(4)=num(1)+3
         num(5)=(iq-1)/2*(3*nxe+1)*3+3*nxe+1+3*ip
         num(6)=(iq-1)/2*(3*nxe+1)*3+6*nxe+2+3*ip-1
         num(7)=(iq-1)/2*(3*nxe+1)*3+9*nxe+3+3*ip-2
         num(8)=num(6)-1
         num(9)=num(5)-2
         num(10)=num(9)+1
       ELSE
         num(1)=(9*(nye-2)/2+12)*(ip-1)+3*(iq-1)/2+1
         num(2)=(9*(nye-2)/2+12)*(ip-1)+3*(nye-2)/2+4+3*(iq-1)/2+1
         num(3)=(9*(nye-2)/2+12)*(ip-1)+3*(nye-2)+8+3*(iq-1)/2+1
         num(4)=(9*(nye-2)/2+12)*(ip-1)+9*(nye-2)/2+12+3*(iq-1)/2+1
         num(5)=num(3)+1 
         num(6)=num(2)+2
         num(7)=num(1)+3
         num(8)=num(1)+2
         num(9)=num(1)+1
         num(10)=num(2)+1
       END IF
!
       coord(1,1)=x_coords(ip)
       coord(2,1)=x_coords(ip)+(x_coords(ip+1)-x_coords(ip))/d3
       coord(3,1)=x_coords(ip)+two*(x_coords(ip+1)-x_coords(ip))/d3
       coord(4,1)=x_coords(ip+1)
       coord(4,2)=y_coords((iq+1)/2)
       coord(5,2)=y_coords((iq+1)/2)+                                     &
         (y_coords((iq+3)/2)-y_coords((iq+1)/2))/d3
       coord(6,2)=y_coords((iq+1)/2)+                                     &
         two*(y_coords((iq+3)/2)-y_coords((iq+1)/2))/d3
       coord(7,2)=y_coords((iq+3)/2)
     ELSE
       IF(dir=='x'.OR.dir=='r')THEN
         num(1)=(iq-2)/2*(3*nxe+1)*3+9*nxe+3+3*ip+1
         num(2)=num(1)-1
         num(3)=num(1)-2
         num(4)=num(1)-3
         num(5)=(iq-2)/2*(3*nxe+1)*3+6*nxe+2+3*ip-1
         num(6)=(iq-2)/2*(3*nxe+1)*3+3*nxe+1+3*ip
         num(7)=(iq-2)/2*(3*nxe+1)*3+3*ip+1
         num(8)=num(6)+1
         num(9)=num(5)+2
         num(10)=num(9)-1
       ELSE
         num(1)=(9*(nye-2)/2+12)*(ip-1)+9*(nye-2)/2+12+3*iq/2+1
         num(2)=(9*(nye-2)/2+12)*(ip-1)+3*(nye-2)+8+3*iq/2+1
         num(3)=(9*(nye-2)/2+12)*(ip-1)+3*(nye-2)/2+4+3*iq/2+1
         num(4)=(9*(nye-2)/2+12)*(ip-1)+3*iq/2+1
         num(5)=num(3)-1
         num(6)=num(2)-2
         num(7)=num(1)-3
         num(8)=num(1)-2
         num(9)=num(1)-1
         num(10)=num(2)-1
       END IF
!
       coord(1,1)=x_coords(ip+1)
       coord(2,1)=x_coords(ip+1)-(x_coords(ip+1)-x_coords(ip))/d3
       coord(3,1)=x_coords(ip+1)-two*(x_coords(ip+1)-x_coords(ip))/d3
       coord(4,1)=x_coords(ip)
       coord(4,2)=y_coords((iq+2)/2)
       coord(5,2)=y_coords((iq+2)/2)-(y_coords((iq+2)/2)-y_coords(iq/2))/d3
       coord(6,2)=y_coords((iq+2)/2)-                                     &
         two*(y_coords((iq+2)/2)-y_coords(iq/2))/d3
       coord(7,2) =y_coords(iq/2)
     END IF
     coord(5,1)=coord(3,1)
     coord(6,1)=coord(2,1)
     coord(7,1)=coord(1,1)
     coord(8,1)=coord(1,1)
     coord(9,1)=coord(1,1)
     coord(10,1)=coord(2,1)
     coord(1,2)=coord(4,2)
     coord(2,2)=coord(4,2)
     coord(3,2)=coord(4,2)
     coord(8,2)=coord(6,2)
     coord(9,2)=coord(5,2)
     coord(10,2)=coord(5,2)
   CASE(15)
     IF(MOD(iq,2)/=0)THEN
       IF(dir=='x'.OR.dir=='r')THEN
       fac1=4*(4*nxe+1)*(iq-1)/2
         num(1)=fac1+4*ip-3
         num(2)=num(1)+1
         num(3)=num(1)+2
         num(4)=num(1)+3
         num(5)=num(1)+4
         num(6)=fac1+ 4*nxe+1+4*ip
         num(7)=fac1+ 8*nxe+1+4*ip
         num(8)=fac1+12*nxe+1+4*ip
         num(9)=fac1+16*nxe+1+4*ip
         num(10)=num(8)-1
         num(11)=num(7)-2
         num(12)=num(6)-3
         num(13)=num(12)+1
         num(14)=num(12)+2
         num(15)=num(11)+1
       ELSE
         fac1=4*(2*nye+1)*(ip-1)+2*iq-1 
         num(1)=fac1
         num(2)=fac1+2*nye+1
         num(3)=fac1+4*nye+2 
         num(4)=fac1+6*nye+3 
         num(5)=fac1+8*nye+4
         num(6)=fac1+6*nye+4 
         num(7)=fac1+4*nye+4 
         num(8)=fac1+2*nye+4
         num(9)=fac1+4 
         num(10)=fac1+3 
         num(11)=fac1+2 
         num(12)=fac1+1
         num(13)=fac1+2*nye+2 
         num(14)=fac1+4*nye+3
         num(15)=fac1+2*nye+3  
       END IF
!
       coord(1,1)=x_coords(ip)
       coord(1,2)=y_coords((iq+1)/2)
       coord(5,1)=x_coords(ip+1)   
       coord(5,2)=y_coords((iq+1)/2)
       coord(9,1)=x_coords(ip)   
       coord(9,2)=y_coords((iq+3)/2)
     ELSE
       IF(dir=='x'.OR.dir=='r')THEN
         fac1=4*(4*nxe+1)*(iq-2)/2
         num(1)=fac1+16*nxe+5+4*ip
         num(2)=num(1)-1
         num(3)=num(1)-2
         num(4)=num(1)-3
         num(5)=num(1)-4
         num(6)=fac1+12*nxe+1+4*ip
         num(7)=fac1+8*nxe+1+4*ip
         num(8)=fac1+4*nxe+1+4*ip
         num(9)=fac1+4*ip+1
         num(10)=num(8)+1
         num(11)=num(7)+2
         num(12)=num(6)+3
         num(13)=num(12)-1
         num(14)=num(12)-2
         num(15)=num(11)-1
       ELSE
         fac1=4*(2*nye+1)*(ip-1)+2*iq+8*nye+5 
         num(1)=fac1 
         num(2)=fac1-2*nye-1
         num(3)=fac1-4*nye-2 
         num(4)=fac1-6*nye-3 
         num(5)=fac1-8*nye-4
         num(6)=fac1-6*nye-4  
         num(7)=fac1-4*nye-4 
         num(8)=fac1-2*nye-4
         num(9)=fac1-4
         num(10)=fac1-3 
         num(11)=fac1-2 
         num(12)=fac1-1
         num(13)=fac1-2*nye-2  
         num(14)=fac1-4*nye-3
         num(15)=fac1-2*nye-3 
       END IF
!
       coord(1,1)=x_coords(ip+1)
       coord(1,2)=y_coords((iq+2)/2)
       coord(5,1)=x_coords(ip)   
       coord(5,2)=y_coords((iq+2)/2)
       coord(9,1)=x_coords(ip+1) 
       coord(9,2)=y_coords(iq/2)
     END IF
     coord(3,:)=pt5*(coord(1,:)+coord(5,:))
     coord(7,:)=pt5*(coord(5,:)+coord(9,:))
     coord(11,:)=pt5*(coord(9,:)+coord(1,:))
     coord(2,:)=pt5*(coord(1,:)+coord(3,:))
     coord(4,:)=pt5*(coord(3,:)+coord(5,:))
     coord(6,:)=pt5*(coord(5,:)+coord(7,:))
     coord(8,:)=pt5*(coord(7,:)+coord(9,:))
     coord(10,:)=pt5*(coord(9,:)+coord(11,:))
     coord(12,:)=pt5*(coord(11,:)+coord(1,:))
     coord(15,:)=pt5*(coord(7,:)+coord(11,:))
     coord(14,:)=pt5*(coord(3,:)+coord(7,:))
     coord(13,:)=pt5*(coord(2,:)+coord(15,:))
   CASE DEFAULT
     WRITE(11,'(a)')"Wrong number of nodes for triangular element"
     STOP
   END SELECT
 ELSE
   nye=UBOUND(y_coords,1)-1
   IF(dir=='x'.OR.dir=='r')THEN
     iq=(iel-1)/nxe+1
     ip=iel-(iq-1)*nxe
   ELSE
     ip=(iel-1)/nye+1
     iq=iel-(ip-1)*nye
   END IF
   SELECT CASE(nod)
   CASE(4)
     IF(dir=='x'.OR.dir=='r')THEN
       num(1)=iq*(nxe+1)+ip		        		
       num(2)=(iq-1)*(nxe+1)+ip				
       num(3)=num(2)+1					
       num(4)=num(1)+1					
     ELSE
       num(1)=(ip-1)*(nye+1)+iq+1
       num(2)=num(1)-1
       num(3)=ip*(nye+1)+iq
       num(4)=num(3)+1
     END IF
!
     coord(1:2,1)=x_coords(ip)
     coord(3:4,1)=x_coords(ip+1)
     coord(1,2)=y_coords(iq+1)
     coord(2:3,2)=y_coords(iq)
     coord(4,2)=coord(1,2)
   CASE(5)
     IF(dir=='x'.OR.dir=='r')THEN
       num(1)=iq*(2*nxe+1)+ip
       num(2)=(iq-1)*(2*nxe+1)+ip
       num(3)=num(2)+1
       num(4)=num(1)+1
       num(5)=iq*(2*nxe+1)+ip-nxe
     ELSE
       num(1)=(ip-1)*(2*nye+1)+iq+1
       num(2)=num(1)-1
       num(3)=ip*(2*nye+1)+iq
       num(4)=num(3)+1
       num(5)=ip*(2*nye+1)+iq-nye
     END IF
!
     coord(1:2,1)=x_coords(ip)
     coord(3:4,1)=x_coords(ip+1)
     coord(1,2)=y_coords(iq+1)
     coord(2:3,2)=y_coords(iq)
     coord(4,2)=coord(1,2)
     coord(5,:)=0.25_iwp*(coord(1,:)+coord(2,:)+coord(3,:)+coord(4,:))
   CASE(8)
     IF(dir=='x'.OR.dir=='r')THEN
       num(1)=iq*(3*nxe+2)+2*ip-1                 
       num(2)=iq*(3*nxe+2)+ip-nxe-1		  
       num(3)=(iq-1)*(3*nxe+2)+2*ip-1		   
       num(4)=num(3)+1
       num(5)=num(4)+1
       num(6)=num(2)+1
       num(7)=num(1)+2
       num(8)=num(1)+1
     ELSE
       num(1)=(ip-1)*(3*nye+2)+2*iq+1
       num(2)=num(1)-1
       num(3)=num(1)-2
       num(4)=(ip-1)*(3*nye+2)+2*nye+iq+1
       num(5)=ip*(3*nye+2)+2*iq-1
       num(6)=num(5)+1
       num(7)=num(5)+2
       num(8)=num(4)+1
     END IF
!
     coord(1:3,1)=x_coords(ip)
     coord(5:7,1)=x_coords(ip+1)
     coord(4,1)=pt5*(coord(3,1)+coord(5,1))
     coord(8,1)=pt5*(coord(7,1)+coord(1,1))
     coord(1,2)=y_coords(iq+1)
     coord(7:8,2)=y_coords(iq+1)
     coord(3:5,2)=y_coords(iq)
     coord(2,2)=pt5*(coord(1,2)+coord(3,2))
     coord(6,2)=pt5*(coord(5,2)+coord(7,2))
   CASE(9)
     IF(dir=='x'.OR.dir=='r')THEN
       num(1)=iq*(4*nxe+2)+2*ip-1
       num(2)=iq*(4*nxe+2)+2*ip-nxe-4
       num(3)= (iq-1)*(4*nxe+2)+2*ip-1
       num(4)=num(3)+1
       num(5)=num(4)+1
       num(6)=num(2)+2
       num(7)=num(1)+2
       num(8)=num(1)+1
       num(9)=num(2)+1
     ELSE
       num(1)=(ip-1)*2*(2*nye+1)+2*iq+1
       num(2)=num(1)-1
       num(3)=num(1)-2
       num(4)=(ip-1)*2*(2*nye+1)+2*nye+2*iq
       num(5)=ip*2*(2*nye+1)+2*iq-1
       num(6)=num(5)+1
       num(7)=num(5)+2
       num(8)=num(4)+2
       num(9)=num(4)+1
     END IF
!
     coord(1:3,1)=x_coords(ip)
     coord(5:7,1)=x_coords(ip+1)
     coord(4,1)=pt5*(coord(3,1)+coord(5,1))
     coord(8,1)=pt5*(coord(7,1)+coord(1,1))
     coord(1,2)=y_coords(iq+1)
     coord(7:8,2)=y_coords(iq+1)
     coord(3:5,2)=y_coords(iq)
     coord(2,2)=pt5*(coord(1,2)+coord(3,2))
     coord(6,2)=pt5*(coord(5,2)+coord(7,2))
     coord(9,:)=pt5*(coord(4,:)+coord(8,:))
   CASE DEFAULT
     WRITE(11,'(a)')"Wrong number of nodes for quadrilateral element"
     STOP
   END SELECT
 END IF
RETURN
END SUBROUTINE geom_rect

SUBROUTINE elmat(area,rho,emm)
!
! This subroutine forms the "analytical" lumped mass matrix for
! quadrilateral 4- or 8-node plane strain elements.
!
 IMPLICIT NONE
 INTEGER,PARAMETER::iwp=SELECTED_REAL_KIND(15)
 REAL(iwp),INTENT(IN)::area,rho  
 REAL(iwp),INTENT(OUT)::emm(:,:)
 REAL(iwp)::zero=0.0_iwp,pt2=0.2_iwp,pt25=0.25_iwp
 INTEGER::i,ndof
 ndof=UBOUND(emm,1)
 emm=zero
 SELECT CASE(ndof)
 CASE(8) 
   DO i=1,8
     emm(i,i)=pt25*area*rho
   END DO
 CASE(16)
   DO i=1,16
     emm(i,i)=pt2*area*rho
   END DO
   DO i=1,13,4
     emm(i,i)=pt25*emm(3,3)
   END DO
   DO i=2,14,4
     emm(i,i)=pt25*emm(3,3)
   END DO                     
 CASE DEFAULT
   WRITE(*,*)"Wrong number of nodes for rectangular element"
 END SELECT
RETURN
END SUBROUTINE elmat   

SUBROUTINE mesh(g_coord,g_num,argv,nlen,ips)
!
! This subroutine produces a PostScript output file "*.msh" displaying
! the undeformed finite element mesh.
!
 IMPLICIT NONE
 INTEGER,PARAMETER::iwp=SELECTED_REAL_KIND(15)
 REAL(iwp),INTENT(IN)::g_coord(:,:)
 INTEGER,INTENT(IN)::g_num(:,:),ips,nlen
 CHARACTER(*),INTENT(IN)::argv
 REAL(iwp)::xmin,xmax,ymin,ymax,width,height,scale=72,sxy,xo,yo,x,y,      &
   pt5=0.5_iwp,opt5=1.5_iwp,fpt5=5.5_iwp,d8=8.0_iwp,ept5=8.5_iwp,         &
   d11=11.0_iwp
 INTEGER::i,ii,j,jj,nn,nod,nel
 OPEN(ips,FILE=argv(1:nlen)//'.msh')
!
!                       compute size of mesh
!
 nn=UBOUND(g_coord,2)
 xmin=g_coord(1,1)
 xmax=g_coord(1,1)
 ymin=g_coord(2,1)
 ymax=g_coord(2,1)
 DO i=2,nn
   IF(g_coord(1,i)<xmin)xmin=g_coord(1,i)      
   IF(g_coord(1,i)>xmax)xmax=g_coord(1,i)      
   IF(g_coord(2,i)<ymin)ymin=g_coord(2,i)      
   IF(g_coord(2,i)>ymax)ymax=g_coord(2,i)      
 END DO
 width =xmax-xmin
 height=ymax-ymin
!
!                       allow 1.5" margin minimum on each side of figure
!
 IF(height.GE.d11/ept5*width)THEN
!
!                       height governs the scale
!
   sxy=scale*d8/height
   xo=scale*pt5*(ept5-d8*width/height)
   yo=scale*opt5
 ELSE
!
!                       width governs the scale
!
   sxy=scale*fpt5/width
   xo=scale*opt5
   yo=scale*pt5*(d11-fpt5*height/width)
 END IF
!
!                       start PostScript output
!
 WRITE(ips,'(a)')'%!PS-Adobe-1.0'
 WRITE(ips,'(a)')'%%DocumentFonts: none'
 WRITE(ips,'(a)')'%%Pages: 1'
 WRITE(ips,'(a)')'%%EndComments'
 WRITE(ips,'(a)')'/m {moveto} def'
 WRITE(ips,'(a)')'/l {lineto} def'
 WRITE(ips,'(a)')'/s {stroke} def'
 WRITE(ips,'(a)')'/c {closepath} def'
 WRITE(ips,'(a)')'%%EndProlog'
 WRITE(ips,'(a)')'%%Page: 0 1'
 WRITE(ips,'(a)')'gsave'
 WRITE(ips,'(2f9.2,a)') xo, yo, ' translate'
 WRITE(ips,'(f9.2,a)') 0.5, ' setlinewidth'
!
!                       draw the mesh
!
 nod=UBOUND(g_num,1)
 nel=UBOUND(g_num,2)
 IF(nod==5)nod=4
 IF(nod==9)nod=8
 IF(nod==10)nod=9
 IF(nod==15)nod=12
 DO i=1,nel
   ii=g_num(1,i)
   IF(ii==0)CYCLE
   x=sxy*(g_coord(1,ii)-xmin)
   y=sxy*(g_coord(2,ii)-ymin)
   WRITE(ips,'(2f9.2,a)')x,y,' m'
   DO j=2,nod
     jj=g_num(j,i)
     x=sxy*(g_coord(1,jj)-xmin)
     y=sxy*(g_coord(2,jj)-ymin)
     WRITE(ips,'(2f9.2,a)') x, y,' l'
   END DO
   WRITE(ips,'(a)')'c s'
 END DO
!
!                       close output file
!
 WRITE(ips,'(a)')'grestore'
 WRITE(ips,'(a)')'showpage'
 CLOSE(ips)
!
RETURN
END SUBROUTINE mesh

SUBROUTINE fkdiag(kdiag,g)
!
! This subroutine computes the skyline profile.
!
 IMPLICIT NONE
 INTEGER,INTENT(IN)::g(:)
 INTEGER,INTENT(OUT)::kdiag(:)
 INTEGER::idof,i,iwp1,j,im,k
 idof=SIZE(g)
 DO i=1,idof
   iwp1=1
   IF(g(i)/=0)THEN
     DO j=1,idof
       IF(g(j)/=0)THEN
         im=g(i)-g(j)+1
         IF(im>iwp1)iwp1=im
       END IF
     END DO
     k=g(i)
     IF(iwp1>kdiag(k))kdiag(k)=iwp1
   END IF
 END DO
RETURN
END SUBROUTINE fkdiag

SUBROUTINE fsparv(kv,km,g,kdiag)
!
! This subroutine assembles element matrices into a symmetric skyline
! global matrix.
!
 IMPLICIT NONE
 INTEGER,PARAMETER::iwp=SELECTED_REAL_KIND(15)
 INTEGER,INTENT(IN)::g(:),kdiag(:)
 REAL(iwp),INTENT(IN)::km(:,:)
 REAL(iwp),INTENT(OUT)::kv(:) 
 INTEGER::i,idof,k,j,iw,ival
 idof=UBOUND(g,1)
 DO i=1,idof
   k=g(i)
   IF(k/=0)THEN
     DO j=1,idof
       IF(g(j)/=0)THEN
         iw=k-g(j)
         IF(iw>=0)THEN
           ival=kdiag(k)-iw
           kv(ival)=kv(ival)+km(i,j) 
         END IF
       END IF
     END DO
   END IF
 END DO
RETURN
END SUBROUTINE fsparv

SUBROUTINE sparin(kv,kdiag)
!
! This subroutine performs Cholesky factorisation on a symmetric
! skyline global matrix.
!
 IMPLICIT NONE
 INTEGER,PARAMETER::iwp=SELECTED_REAL_KIND(15)
 REAL(iwp),INTENT(IN OUT)::kv(:)
 INTEGER,INTENT(IN)::kdiag(:)
 INTEGER::n,i,ki,l,kj,j,ll,m,k
 REAL(iwp)::x
 n=UBOUND(kdiag,1)  
 kv(1)=SQRT(kv(1))
 DO i=2,n
   ki=kdiag(i)-i
   l=kdiag(i-1)-ki+1
   DO j=l,i
     x=kv(ki+j)
     kj=kdiag(j)-j
     IF(j/=1)THEN
       ll=kdiag(j-1)-kj+1
       ll=max(l,ll)
       IF(ll/=j)THEN
         m=j-1
         DO k=ll,m 
           x=x-kv(ki+k)*kv(kj+k) 
         END DO
       END IF
     END IF
     kv(ki+j)=x/kv(kj+j)
   END DO
   kv(ki+i)=SQRT(x)
 END DO
RETURN
END SUBROUTINE sparin

SUBROUTINE spabac(kv,loads,kdiag)
!
! This subroutine performs Cholesky forward and back-substitution
! on a symmetric skyline global matrix.
!
 IMPLICIT NONE
 INTEGER,PARAMETER::iwp=SELECTED_REAL_KIND(15)
 REAL(iwp),INTENT(IN)::kv(:)
 REAL(iwp),INTENT(IN OUT)::loads(0:)
 INTEGER,INTENT(IN)::kdiag(:)
 INTEGER::n,i,ki,l,m,j,it,k
 REAL(iwp)::x
 n=UBOUND(kdiag,1)
 loads(1)=loads(1)/kv(1)
 DO i=2,n
   ki=kdiag(i)-i
   l=kdiag(i-1)-ki+1 
   x=loads(i)
   IF(l/=i)THEN
     m=i-1
     DO j=l,m 
       x=x-kv(ki+j)*loads(j)
     END DO
   END IF
   loads(i)=x/kv(ki+i)
 END DO
 DO it=2,n
   i=n+2-it
   ki=kdiag(i)-i
   x=loads(i)/kv(ki+i)
   loads(i)=x
   l=kdiag(i-1)-ki+1
   IF(l/=i)THEN
     m=i-1
     DO k=l,m
       loads(k)=loads(k)-x*kv(ki+k)
     END DO
   END IF
 END DO
 loads(1)=loads(1)/kv(1)
RETURN
END SUBROUTINE spabac               

SUBROUTINE dismsh(loads,nf,ratmax,g_coord,g_num,argv,nlen,ips)
!
! This subroutine produces a PostScript output file "*.dis" displaying
! the deformed finite element mesh.
!
 IMPLICIT NONE
 INTEGER,PARAMETER::iwp=SELECTED_REAL_KIND(15)
 REAL(iwp),INTENT(IN)::g_coord(:,:),loads(0:),ratmax
 INTEGER,INTENT(IN)::g_num(:,:),ips,nf(:,:),nlen
 CHARACTER(*),INTENT(IN)::argv
 REAL(iwp)::width,height,scale=72,sxy,xo,yo,x,y,dismag,vmax
 REAL(iwp)::xmin,xmax,ymin,ymax,dmax,zero=0.0_iwp,pt5=0.5_iwp,            &
   opt5=1.5_iwp,fpt5=5.5_iwp,d8=8.0_iwp,ept5=8.5_iwp,d11=11.0_iwp
 INTEGER::i,ii,j,jj,nn,nel,nod
 OPEN(ips,FILE=argv(1:nlen)//'.dis')
!
 nn=UBOUND(nf,2)
 xmin=g_coord(1,1)
 xmax=g_coord(1,1)
 ymin=g_coord(2,1)
 ymax=g_coord(2,1)
 DO i=2,nn
   IF(g_coord(1,i)<xmin)xmin=g_coord(1,i)      
   IF(g_coord(1,i)>xmax)xmax=g_coord(1,i)      
   IF(g_coord(2,i)<ymin)ymin=g_coord(2,i)      
   IF(g_coord(2,i)>ymax)ymax=g_coord(2,i)      
 END DO
 width=xmax-xmin
 height=ymax-ymin
 dmax=ratmax*width
 IF(height>width)dmax=ratmax*height
!
 vmax=zero
 DO i=1,nn
   DO j=1,2
     IF(ABS(loads(nf(j,i)))>vmax)vmax=ABS(loads(nf(j,i)))
   END DO
 END DO
 dismag=dmax/vmax
!
 xmin=g_coord(1,1)
 xmax=g_coord(1,1)
 ymin=g_coord(2,1)
 ymax=g_coord(2,1)
 DO i=1,nn
   IF(g_coord(1,i)+dismag*loads(nf(1,i))<xmin)                            &
     xmin=g_coord(1,i)+dismag*loads(nf(1,i))
   IF(g_coord(1,i)+dismag*loads(nf(1,i))>xmax)                            &
     xmax=g_coord(1,i)+dismag*loads(nf(1,i))      
   IF(g_coord(2,i)+dismag*loads(nf(2,i))<ymin)                            &
     ymin=g_coord(2,i)+dismag*loads(nf(2,i))      
   IF(g_coord(2,i)+dismag*loads(nf(2,i))>ymax)                            &
     ymax=g_coord(2,i)+dismag*loads(nf(2,i))      
!
   IF(g_coord(1,i)<xmin)xmin=g_coord(1,i)
   IF(g_coord(1,i)>xmax)xmax=g_coord(1,i)
   IF(g_coord(2,i)<ymin)ymin=g_coord(2,i)
   IF(g_coord(2,i)>ymax)ymax=g_coord(2,i)
 END DO
!
 width =xmax-xmin
 height=ymax-ymin
!
!                       allow 1.5" margin minimum on each side of figure
!
!                       portrait mode 
!
 IF(height.GE.d11/ept5*width)THEN
!
!                       height governs the scale
!
   sxy=scale*d8/height
   xo=scale*pt5*(ept5-d8*width/height)
   yo=scale*opt5
 ELSE
!
!                       width governs the scale
!
   sxy=scale*fpt5/width
   xo=scale*opt5
   yo=scale*pt5*(d11-fpt5*height/width)
 END IF
!
 nel=UBOUND(g_num,2)
 nod=UBOUND(g_num,1)
!
!                       start PostScript output
!
 WRITE(ips,'(a)')'%!PS-Adobe-1.0'
 WRITE(ips,'(a)')'%%DocumentFonts: none'
 WRITE(ips,'(a)')'%%Pages: 1'
 WRITE(ips,'(a)')'%%EndComments'
 WRITE(ips,'(a)')'/m {moveto} def'
 WRITE(ips,'(a)')'/l {lineto} def'
 WRITE(ips,'(a)')'/s {stroke} def'
 WRITE(ips,'(a)')'/c {closepath} def'
 WRITE(ips,'(a)')'%%EndProlog'
 WRITE(ips,'(a)')'%%Page: 0 1'
 WRITE(ips,'(a)')'gsave'
!
!                       draw the deformed mesh
!
 WRITE(ips,'(2f9.2,a)') xo, yo, ' translate'
 WRITE(ips,'(f9.2,a)') 0.5, ' setlinewidth'
 IF(nod==5)nod=4
 IF(nod==9)nod=8
 IF(nod==10)nod=9
 IF(nod==15)nod=12
 DO i=1,nel
   ii=g_num(1,i)
   IF(ii==0)CYCLE
   x=sxy*(g_coord(1,ii)+dismag*loads(nf(1,ii))-xmin)
   y=sxy*(g_coord(2,ii)+dismag*loads(nf(2,ii))-ymin)
   WRITE(ips,'(2f9.2,a)') x, y,' m'
   DO j=2,nod
     jj=g_num(j,i)
     x=sxy*(g_coord(1,jj)+dismag*loads(nf(1,jj))-xmin)
     y=sxy*(g_coord(2,jj)+dismag*loads(nf(2,jj))-ymin)
     WRITE(ips,'(2f9.2,a)') x, y,' l'
   END DO
   WRITE(ips,'(a)')'c s'
 END DO
!
 WRITE(ips,'(a)')'grestore'
 WRITE(ips,'(a)')'showpage'
 CLOSE(ips)
!
RETURN
END SUBROUTINE dismsh

SUBROUTINE vecmsh(loads,nf,ratmax,cutoff,g_coord,g_num,argv,nlen,ips)
!
! This subroutine produces a PostScript output file "*.vec" displaying
! the nodal displacement vectors.
!
IMPLICIT NONE
 INTEGER,PARAMETER::iwp=SELECTED_REAL_KIND(15)
 REAL(iwp),INTENT(IN)::g_coord(:,:),loads(0:),ratmax,cutoff
 INTEGER,INTENT(IN)::g_num(:,:),ips,nf(:,:),nlen
 REAL(iwp)::width,height,scale=72,sxy,xo,yo,x1,y1,x2,y2,dismag,           &
   zero=0.0_iwp,pt5=0.5_iwp,opt5=1.5_iwp,fpt5=5.5_iwp,d8=8.0_iwp,         &
   ept5=8.5_iwp,d11=11.0_iwp,xmin,xmax,ymin,ymax,dmax,vlen,vmax
 INTEGER::i,j,k,l,nn,nels,nod,ns,i1,i2,j1,j2
 INTEGER,ALLOCATABLE::corner(:,:)
 CHARACTER(*),INTENT(IN)::argv
 LOGICAL::draw
!                       formats
 OPEN(ips,FILE=argv(1:nlen)//'.vec')
!                       open output file and compute scale factors
 nn=UBOUND(nf,2)
!
 xmin=g_coord(1,1)
 xmax=g_coord(1,1)
 ymin=g_coord(2,1)
 ymax=g_coord(2,1)
 DO i=2,nn
   IF(g_coord(1,i)<xmin)xmin=g_coord(1,i)      
   IF(g_coord(1,i)>xmax)xmax=g_coord(1,i)      
   IF(g_coord(2,i)<ymin)ymin=g_coord(2,i)      
   IF(g_coord(2,i)>ymax)ymax=g_coord(2,i)      
 END DO
 width =xmax-xmin
 height=ymax-ymin
 dmax=ratmax*width
 IF(height>width)dmax=ratmax*height
!
 vmax=zero
 DO i=1,nn
   DO j=1,2
     IF(ABS(loads(nf(j,i)))>vmax)vmax=ABS(loads(nf(j,i)))
   END DO
 END DO
 dismag=dmax/vmax
!
 xmin=g_coord(1,1)
 xmax=g_coord(1,1)
 ymin=g_coord(2,1)
 ymax=g_coord(2,1)
!
 DO i=1,nn
   IF(g_coord(1,i)+dismag*loads(nf(1,i)) < xmin)                          &
     xmin=g_coord(1,i)+dismag*loads(nf(1,i))      
   IF(g_coord(1,i)+dismag*loads(nf(1,i)) > xmax)                          &
     xmax=g_coord(1,i)+dismag*loads(nf(1,i))      
   IF(g_coord(2,i)+dismag*loads(nf(2,i)) < ymin)                          &
     ymin=g_coord(2,i)+dismag*loads(nf(2,i))      
   IF(g_coord(2,i)+dismag*loads(nf(2,i)) > ymax)                          &
     ymax=g_coord(2,i)+dismag*loads(nf(2,i))      
!
   IF(g_coord(1,i)<xmin)xmin=g_coord(1,i)
   IF(g_coord(1,i)>xmax)xmax=g_coord(1,i)
   IF(g_coord(2,i)<ymin)ymin=g_coord(2,i)
   IF(g_coord(2,i)>ymax)ymax=g_coord(2,i)
 END DO
!
 width=xmax-xmin
 height=ymax-ymin
!
!                       allow 1.5" margin minimum on each side of figure
!
!                       Portrait mode 
!
 IF(height.GE.d11/ept5*width)THEN
!
!                       height governs the scale
!
   sxy=scale*d8/height
   xo=scale*pt5*(ept5-d8*width/height)
   yo=scale*opt5
 ELSE
!
!                       width governs the scale
!
   sxy=scale*fpt5/width
   xo=scale*opt5
   yo=scale*pt5*(d11-fpt5*height/width)
 END IF
!
 WRITE(ips,'(a)')'%!PS-Adobe-1.0'
 WRITE(ips,'(a)')'%%DocumentFonts: none'
 WRITE(ips,'(a)')'%%Pages: 1'
 WRITE(ips,'(a)')'%%EndComments'
 WRITE(ips,'(a)')'/m {moveto} def'
 WRITE(ips,'(a)')'/l {lineto} def'
 WRITE(ips,'(a)')'/s {stroke} def'
 WRITE(ips,'(a)')'/c {closepath} def'
 WRITE(ips,'(a)')'/edef {exch def} bind def'
 WRITE(ips,'(a)')                                                         &
   '/arrow {/@to_y edef /@to_x edef /@from_y edef /@from_x edef'
 WRITE(ips,'(a)')'/@dx @to_x @from_x sub def /@dy @to_y @from_y sub def'
 WRITE(ips,'(a)')'/@length @dx @dx mul @dy @dy mul add sqrt def'
 WRITE(ips,'(a)')'/@angle @dy @dx atan def'
 WRITE(ips,'(a)')'gsave @from_x @from_y translate @angle rotate'
 WRITE(ips,'(a)')                                                         &
   '0 0 moveto @length 0 lineto currentpoint stroke newpath moveto'
 WRITE(ips,'(a)')'-4 -2 rlineto @length 0 moveto'
 WRITE(ips,'(a)')'-4  2 rlineto stroke grestore'
 WRITE(ips,'(a)')'} def'
 WRITE(ips,'(a)')'/*sf {'
 WRITE(ips,'(a)')'exch findfont exch'
 WRITE(ips,'(a)')                                                         &
   'dup type /arraytype eq {makefont}{scalefont} ifelse setfont'
 WRITE(ips,'(a)')'} bind def'
 WRITE(ips,'(a)')'/languagelevel where'
 WRITE(ips,'(a)')'{pop languagelevel} {1} ifelse'
 WRITE(ips,'(a)')'2 lt { % ifelse'
 WRITE(ips,'(a)')'/sf /*sf load def'
 WRITE(ips,'(a)')'} { % else'
 WRITE(ips,'(a)')'/sf /selectfont load def'
 WRITE(ips,'(a)')'} ifelse'
 WRITE(ips,'(a)')'%%EndProlog'
 WRITE(ips,'(a)')'%%Page: 0 1'
 WRITE(ips,'(a)')'gsave'
!
 WRITE(ips,'(2f9.2,a)')xo,yo, ' translate'
 WRITE(ips,'(f9.2,a)')0.5,' setlinewidth'
!
!                       draw the displacement vectors
!
 vmax=zero
 DO i=1,nn
   vlen=loads(nf(1,i))**2+loads(nf(2,i))**2
   IF(vlen>vmax)vmax=vlen
 END DO
 vmax=SQRT(vmax)*cutoff      
 DO i=1,nn
   vlen=SQRT(loads(nf(1,i))**2+loads(nf(2,i))**2)
   x1=sxy*(g_coord(1,i)-xmin)
   y1=sxy*(g_coord(2,i)-ymin)
   x2=sxy*(g_coord(1,i)+dismag*loads(nf(1,i))-xmin)
   y2=sxy*(g_coord(2,i)+dismag*loads(nf(2,i))-ymin)
   IF(vlen>vmax)THEN
     WRITE(ips,'(2f9.2,a,2f9.2,a)') x1, y1,' ', x2, y2, ' arrow'
     WRITE(ips,'(a)') 's'
   END IF
 END DO
!
!                       draw the mesh border                          
!
 nels=UBOUND(g_num,2)
 nod=UBOUND(g_num,1)
 IF(nod==3.OR.nod==6.OR.nod==10.OR.nod==15)ns=3
 IF(nod==4.OR.nod==5.OR.nod==8.OR.nod==9)ns=4
 ALLOCATE(corner(ns,2))
 IF(nod== 3)corner=RESHAPE((/1,2,3,2,3,1/),(/3,2/))
 IF(nod== 6)corner=RESHAPE((/1,3,5,3,5,1/),(/3,2/))
 IF(nod==10)corner=RESHAPE((/1,4,7,4,7,1/),(/3,2/))
 IF(nod==15)corner=RESHAPE((/1,5,9,5,9,1/),(/3,2/))
 IF(nod== 4)corner=RESHAPE((/1,2,3,4,2,3,4,1/),(/4,2/))
 IF(nod== 5)corner=RESHAPE((/1,2,3,4,2,3,4,1/),(/4,2/))
 IF(nod== 8)corner=RESHAPE((/1,3,5,7,3,5,7,1/),(/4,2/))
 IF(nod== 9)corner=RESHAPE((/1,3,5,7,3,5,7,1/),(/4,2/))
 DO i=1,nels
   DO j=1,ns
     draw=.TRUE.
     i1=g_num(corner(j,1),i)
     i2=g_num(corner(j,2),i)
     DO k=1,nels
       DO l=1,ns
         j1=g_num(corner(l,1),k)
         j2=g_num(corner(l,2),k)
         IF((i1==j2).AND.(i2==j1))THEN
           draw=.FALSE.
           EXIT
         END IF
       END DO
       IF(.NOT.draw)EXIT
     END DO
     IF(draw)THEN
       x1=sxy*(g_coord(1,i1)-xmin)
       y1=sxy*(g_coord(2,i1)-ymin)
       WRITE(ips,'(2f9.2,a)')x1, y1,' m'
       x1=sxy*(g_coord(1,i2)-xmin)
       y1=sxy*(g_coord(2,i2)-ymin)
       WRITE(ips,'(2f9.2,a)')x1, y1,' l'
       WRITE(ips,'(a)')' s'
     END IF
   END DO
 END DO
!                       close output file?
 WRITE(ips,'(a)')'grestore'
 WRITE(ips,'(a)')'showpage'
 CLOSE(ips)
RETURN
END SUBROUTINE vecmsh

SUBROUTINE checon(loads,oldlds,tol,converged)
!
! This subroutine sets converged to .FALSE. if relative change in loads
! and oldlds is greater than tol and updates oldlds.
!
 IMPLICIT NONE
 INTEGER,PARAMETER::iwp=SELECTED_REAL_KIND(15)
 REAL(iwp),INTENT(IN)::loads(0:),tol
 REAL(iwp),INTENT(IN OUT)::oldlds(0:)
 LOGICAL,INTENT(OUT)::converged
 CONVERGED=.TRUE.
 CONVERGED=(MAXVAL(ABS(loads-oldlds))/MAXVAL(ABS(loads))<=tol)
 oldlds=loads
RETURN
END SUBROUTINE checon

FUNCTION elap_time()

 INTEGER,PARAMETER::iwp=SELECTED_REAL_KIND(15)
 REAL(iwp)        :: time
 REAL(iwp)        :: elap_time

 CALL CPU_TIME(time)
 elap_time = REAL(time,iwp)

END FUNCTION elap_time

SUBROUTINE hexahedron_xz(iel,x_coords,y_coords,z_coords,coord,num)
!
! This subroutine generates nodal coordinates and numbering for
! 8, 14 or 20-node "bricks" counting x-z planes in the y-direction.
!
 IMPLICIT NONE
 INTEGER,PARAMETER::iwp=SELECTED_REAL_KIND(15)
 INTEGER,INTENT(IN)::iel
 REAL(iwp),INTENT(IN)::x_coords(:),y_coords(:),z_coords(:)
 REAL(iwp),INTENT(OUT)::coord(:,:)
 INTEGER,INTENT(OUT)::num(:)
 REAL(iwp)::pt5=0.5_iwp
 INTEGER::fac1,fac2,ip,iq,is,iplane,nod,nxe,nze
 nxe=UBOUND(x_coords,1)-1
 nze=UBOUND(z_coords,1)-1
 nod=UBOUND(num,1)
 iq=(iel-1)/(nxe*nze)+1
 iplane=iel-(iq-1)*nxe*nze
 is=(iplane-1)/nxe+1 
 ip=iplane-(is-1)*nxe
 SELECT CASE(nod)
 CASE(8)
   fac1=(nxe+1)*(nze+1)*(iq-1)
   num(1)=fac1+is*(nxe+1)+ip
   num(2)=fac1+(is-1)*(nxe+1)+ip
   num(3)=num(2)+1
   num(4)=num(1)+1
   num(5)=(nxe+1)*(nze+1)*iq+is*(nxe+1)+ip
   num(6)=(nxe+1)*(nze+1)*iq+(is-1)*(nxe+1)+ip
   num(7)=num(6)+1
   num(8)=num(5)+1
!
   coord(1:2,1)=x_coords(ip)
   coord(5:6,1)=x_coords(ip)
   coord(3:4,1)=x_coords(ip+1)
   coord(7:8,1)=x_coords(ip+1)
!
   coord(1:4,2)=y_coords(iq)
   coord(5:8,2)=y_coords(iq+1)
!
   coord(2:3,3)=z_coords(is)
   coord(6:7,3)=z_coords(is)
   coord(1:4:3,3)=z_coords(is+1)
   coord(5:8:3,3)=z_coords(is+1)
!
 CASE(14)
   fac1=(2*nxe+1)*(2*nze+1)*(iq-1)
   fac2=(2*nxe+1)*(2*nze+1)*iq
   num(1)=fac1+is*(2*nxe+1)+ip
   num(2)=num(1)-(2*nxe+1)
   num(3)=num(2)+1
   num(4)=num(1)+1
   num(5)=num(2)+nxe+1
   num(6)=fac1+(nxe+1)*(nze+1)+nxe*nze+(is-1)*(2*nxe+1)+nxe+ip
   num(7)=num(6)-nxe
   num(8)=num(6)+1
   num(9)=num(8)+nxe
   num(10)=fac2+is*(2*nxe+1)+ip
   num(11)=num(10)-(2*nxe+1)
   num(12)=num(11)+1
   num(13)=num(10)+1
   num(14)=num(11)+nxe+1
!
   coord(1:2,1)=x_coords(ip)
   coord(6,1)=x_coords(ip)
   coord(10:11,1)=x_coords(ip)
   coord(5:9:2,1)=pt5*(x_coords(ip)+x_coords(ip+1))
   coord(14,1)=pt5*(x_coords(ip)+x_coords(ip+1))
   coord(3:4,1)=x_coords(ip+1)
   coord(8,1)=x_coords(ip+1)
   coord(12:13,1)=x_coords(ip+1)
!
   coord(1:5,2)=y_coords(iq)
   coord(6:9,2)=pt5*(y_coords(iq)+y_coords(iq+1))
   coord(10:14,2)=y_coords(iq+1)
!
   coord(2:3,3)=z_coords(is)
   coord(7,3)=z_coords(is)
   coord(11:12,3)=z_coords(is)
   coord(5:6,3)=pt5*(z_coords(is)+z_coords(is+1))
   coord(8:14:6,3)=pt5*(z_coords(is)+z_coords(is+1))
   coord(1:4:3,3)=z_coords(is+1)
   coord(9,3)=z_coords(is+1)
   coord(10:13:3,3)=z_coords(is+1)
!
 CASE(20)
   fac1=((2*nxe+1)*(nze+1)+(2*nze+1)*(nxe+1))*(iq-1)
   fac2=((2*nxe+1)*(nze+1)+(2*nze+1)*(nxe+1))*iq
   num(1)=fac1+(3*nxe+2)*is+2*ip-1
   num(2)=fac1+(3*nxe+2)*is-nxe+ip-1
   num(3)=num(1)-3*nxe-2
   num(4)=num(3)+1
   num(5)=num(4)+1
   num(6)=num(2)+1
   num(7)=num(1)+2
   num(8)=num(1)+1
   num(9)=fac2-(nxe+1)*(nze+1)+(nxe+1)*is+ip
   num(10)=num(9)-nxe-1
   num(11)=num(10)+1
   num(12)=num(9)+1
   num(13)=fac2+(3*nxe+2)*is+2*ip-1
   num(14)=fac2+(3*nxe+2)*is-nxe+ip-1
   num(15)=num(13)-3*nxe-2
   num(16)=num(15)+1
   num(17)=num(16)+1
   num(18)=num(14)+1
   num(19)=num(13)+2
   num(20)=num(13)+1 
!
   coord(1:3,1)=x_coords(ip)
   coord(9:10,1)=x_coords(ip)
   coord(13:15,1)=x_coords(ip)
   coord(5:7,1)=x_coords(ip+1)
   coord(11:12,1)=x_coords(ip+1)
   coord(17:19,1)=x_coords(ip+1)
   coord(4:8:4,1)=pt5*(x_coords(ip)+x_coords(ip+1))
   coord(16:20:4,1)=pt5*(x_coords(ip)+x_coords(ip+1))
!
   coord(1:8,2)=y_coords(iq)
   coord(13:20,2)=y_coords(iq+1)
   coord(9:12,2)=pt5*(y_coords(iq)+y_coords(iq+1))
!
   coord(1,3)=z_coords(is+1)
   coord(7:9,3)=z_coords(is+1)
   coord(12:13,3)=z_coords(is+1)
   coord(19:20,3)=z_coords(is+1)
   coord(3:5,3)=z_coords(is)
   coord(10:11,3)=z_coords(is)
   coord(15:17,3)=z_coords(is)
   coord(2:6:4,3)=pt5*(z_coords(is)+z_coords(is+1))
   coord(14:18:4,3)=pt5*(z_coords(is)+z_coords(is+1))
!
 CASE DEFAULT
   WRITE(11,'(a)')"Wrong number of nodes for hexahedral element"
   STOP
 END SELECT
RETURN
END SUBROUTINE hexahedron_xz

SUBROUTINE mesh_size(element,nod,nels,nn,nxe,nye,nze)
!
!  This subroutine returns the number of elements (nels) and the number
!  of nodes (nn) in a 2-d geometry-created mesh.
!
 IMPLICIT NONE
 CHARACTER(LEN=15),INTENT(IN)::element
 INTEGER,INTENT(IN)::nod,nxe,nye
 INTEGER,INTENT(IN),OPTIONAL::nze
 INTEGER,INTENT(OUT)::nels,nn
 IF(element=="triangle")THEN
   nels=nxe*nye*2
   IF(nod==3)nn=(nxe+1)*(nye+1)
   IF(nod==6)nn=(2*nxe+1)*(2*nye+1)
   IF(nod==10)nn=(3*nxe+1)*(3*nye+1)
   IF(nod==15)nn=(4*nxe+1)*(4*nye+1)
 ELSE IF(element=="quadrilateral")THEN
   nels=nxe*nye
   IF(nod==4)nn=(nxe+1)*(nye+1)
   IF(nod==5)nn=(nxe+1)*(nye+1)+nxe*nye
   IF(nod==8)nn=(2*nxe+1)*(nye+1)+(nxe+1)*nye
   IF(nod==9)nn=(2*nxe+1)*(2*nye+1)
 ELSE IF(element=="hexahedron")THEN
   nels=nxe*nye*nze
   IF(nod==8)nn=(nxe+1)*(nye+1)*(nze+1)
   IF(nod==14)nn=4*nxe*nye*nze+2*(nxe*nye+nye*nze+nze*nxe)+nxe+nye+nze+1
   IF(nod==20)nn=((2*nxe+1)*(nze+1)+(nxe+1)*nze)*(nye+1)+                 &
     (nxe+1)*(nze+1)*nye
 END IF
RETURN
END SUBROUTINE mesh_size

SUBROUTINE beemat(bee,deriv)
!
! This subroutine forms the bee matrix in 2-d (ih=3 or 4) or 3-d (ih=6).
!
 IMPLICIT NONE
 INTEGER,PARAMETER::iwp=SELECTED_REAL_KIND(15)
 REAL(iwp),INTENT(IN)::deriv(:,:)
 REAL(iwp),INTENT(OUT)::bee(:,:)
 INTEGER::k,l,m,n,ih,nod
 REAL::x,y,z
 bee=0.0_iwp
 ih=UBOUND(bee,1)
 nod=UBOUND(deriv,2)
 SELECT CASE (ih)
 CASE(3,4)
   DO m=1,nod
     k=2*m
     l=k-1
     x=deriv(1,m)
     y=deriv(2,m)
     bee(1,l)=x
     bee(3,k)=x
     bee(2,k)=y
     bee(3,l)=y
   END DO
 CASE(6)
   DO m=1,nod
     n=3*m
     k=n-1
     l=k-1
     x=deriv(1,m)
     y=deriv(2,m)
     z=deriv(3,m)
     bee(1,l)=x
     bee(4,k)=x
     bee(6,n)=x
     bee(2,k)=y
     bee(4,l)=y
     bee(5,n)=y
     bee(3,n)=z
     bee(5,k)=z
     bee(6,l)=z
   END DO
 CASE DEFAULT
   WRITE(*,*)'wrong dimension for nst in bee matrix'        
 END SELECT   
RETURN
END SUBROUTINE beemat

SUBROUTINE deemat(dee,e,v)
!
! This subroutine returns the elastic dee matrix for ih=3 (plane strain),
! ih=4 (axisymmetry or plane strain elastoplasticity) or ih=6
! (three dimensions).
!
 IMPLICIT NONE
 INTEGER,PARAMETER::iwp=SELECTED_REAL_KIND(15)
 REAL(iwp),INTENT(IN)::e,v
 REAL(iwp),INTENT(OUT)::dee(:,:)
 REAL(iwp)::v1,v2,c,vv,zero=0.0_iwp,pt5=0.5_iwp,one=1.0_iwp,two=2.0_iwp
 INTEGER::i,ih
 dee=zero  
 ih=UBOUND(dee,1)
 v1=one-v
 c=e/((one+v)*(one-two*v))
 SELECT CASE(ih)
 CASE(3)
   dee(1,1)=v1*c
   dee(2,2)=v1*c
   dee(1,2)=v*c
   dee(2,1)=v*c
   dee(3,3)=pt5*c*(one-two*v)
 CASE(4)
   dee(1,1)=v1*c
   dee(2,2)=v1*c
   dee(4,4)=v1*c
   dee(3,3)=pt5*c*(one-two*v) 
   dee(1,2)=v*c
   dee(2,1)=v*c
   dee(1,4)=v*c
   dee(4,1)=v*c
   dee(2,4)=v*c
   dee(4,2)=v*c
 CASE(6)
   v2=v/(one-v)
   vv=(one-two*v)/(one-v)*pt5
   DO i=1,3
     dee(i,i)=one
   END DO
   DO i=4,6
     dee(i,i)=vv
   END DO
   dee(1,2)=v2
   dee(2,1)=v2
   dee(1,3)=v2
   dee(3,1)=v2
   dee(2,3)=v2
   dee(3,2)=v2
   dee=dee*e/(two*(one+v)*vv)
 CASE DEFAULT
   WRITE(*,*)'wrong size for dee matrix'
 END SELECT
RETURN
END SUBROUTINE deemat    

FUNCTION determinant(jac) RESULT(det)
!
! This function returns the determinant of a 1x1, 2x2 or 3x3
! Jacobian matrix.
!
 IMPLICIT NONE    
 INTEGER,PARAMETER::iwp=SELECTED_REAL_KIND(15)
 REAL(iwp),INTENT(IN)::jac(:,:)
 REAL(iwp)::det
 INTEGER::it 
 it=UBOUND(jac,1)  
 SELECT CASE(it)
 CASE(1)
   det=1.0_iwp
 CASE(2)
   det=jac(1,1)*jac(2,2)-jac(1,2)*jac(2,1)
 CASE(3)
   det=jac(1,1)*(jac(2,2)*jac(3,3)-jac(3,2)*jac(2,3))
   det=det-jac(1,2)*(jac(2,1)*jac(3,3)-jac(3,1)*jac(2,3))
   det=det+jac(1,3)*(jac(2,1)*jac(3,2)-jac(3,1)*jac(2,2))
 CASE DEFAULT
   WRITE(*,*)' wrong dimension for Jacobian matrix'
 END SELECT
RETURN
END FUNCTION determinant

SUBROUTINE formnf(nf)
!
! This subroutine forms the nf matrix.
!
 IMPLICIT NONE
 INTEGER,INTENT(INOUT)::nf(:,:)
 INTEGER::i,j,m
 m=0
 DO j=1,UBOUND(nf,2)
   DO i=1,UBOUND(nf,1)
     IF(nf(i,j)/=0)THEN
       m=m+1
       nf(i,j)=m
     END IF
   END DO
 END DO
RETURN
END SUBROUTINE formnf 

SUBROUTINE getname(argv,nlen)
!
! This subroutine the base name of data file.
!
 IMPLICIT NONE
 INTEGER::narg
 INTEGER,INTENT(OUT)::nlen
 INTEGER::lnblnk,iargc
 CHARACTER(*),INTENT(OUT)::argv
 LOGICAL found
 narg=iargc()
 IF(narg.lt.1)THEN
   WRITE(*,*)'Please enter the base name of data file: '
   READ(*,*) argv
  ELSE
   CALL getarg(1,argv)
 ENDIF
!nlen=lnblnk(argv)
 nlen=len_trim(argv)
 INQUIRE(file=argv(1:nlen)//'.dat',exist=found)
 IF(.not.found)THEN
  WRITE(*,*)'Data file not found: ',argv(1:nlen)//'.dat'
  WRITE(*,*)'Please create or check spelling.'
  STOP
 ENDIF
RETURN
END SUBROUTINE getname

SUBROUTINE invert(matrix)
!
! This subroutine inverts a small square matrix onto itself.
!
 IMPLICIT NONE
 INTEGER,PARAMETER::iwp=SELECTED_REAL_KIND(15)
 REAL(iwp),INTENT(IN OUT)::matrix(:,:)
 REAL(iwp)::det,j11,j12,j13,j21,j22,j23,j31,j32,j33,con
 INTEGER::ndim,i,k
 ndim=UBOUND(matrix,1)
 IF(ndim==2)THEN
   det=matrix(1,1)*matrix(2,2)-matrix(1,2)*matrix(2,1)
   j11=matrix(1,1)
   matrix(1,1)=matrix(2,2)
   matrix(2,2)=j11
   matrix(1,2)=-matrix(1,2)
   matrix(2,1)=-matrix(2,1)
   matrix=matrix/det
 ELSE IF(ndim==3)THEN
   det=matrix(1,1)*(matrix(2,2)*matrix(3,3)-matrix(3,2)*matrix(2,3))
   det=det-matrix(1,2)*(matrix(2,1)*matrix(3,3)-matrix(3,1)*matrix(2,3))
   det=det+matrix(1,3)*(matrix(2,1)*matrix(3,2)-matrix(3,1)*matrix(2,2))
   j11=matrix(2,2)*matrix(3,3)-matrix(3,2)*matrix(2,3)
   j21=-matrix(2,1)*matrix(3,3)+matrix(3,1)*matrix(2,3)
   j31=matrix(2,1)*matrix(3,2)-matrix(3,1)*matrix(2,2)
   j12=-matrix(1,2)*matrix(3,3)+matrix(3,2)*matrix(1,3)
   j22=matrix(1,1)*matrix(3,3)-matrix(3,1)*matrix(1,3)
   j32=-matrix(1,1)*matrix(3,2)+matrix(3,1)*matrix(1,2)
   j13=matrix(1,2)*matrix(2,3)-matrix(2,2)*matrix(1,3)
   j23=-matrix(1,1)*matrix(2,3)+matrix(2,1)*matrix(1,3)
   j33=matrix(1,1)*matrix(2,2)-matrix(2,1)*matrix(1,2)
   matrix(1,1)=j11
   matrix(1,2)=j12
   matrix(1,3)=j13
   matrix(2,1)=j21
   matrix(2,2)=j22
   matrix(2,3)=j23
   matrix(3,1)=j31
   matrix(3,2)=j32
   matrix(3,3)=j33
   matrix=matrix/det
 ELSE
   DO k=1,ndim
     con=matrix(k,k)
     matrix(k,k)=1.0_iwp
     matrix(k,:)=matrix(k,:)/con
     DO i=1,ndim
       IF(i/=k)THEN
         con=matrix(i,k)
         matrix(i,k)=0.0_iwp
         matrix(i,:)=matrix(i,:)-matrix(k,:)*con
       END IF
     END DO
   END DO
 END IF
RETURN
END SUBROUTINE invert

SUBROUTINE num_to_g(num,nf,g)
!
! This subroutine finds the g vector from num and nf.
!
 IMPLICIT NONE
 INTEGER,INTENT(IN)::num(:),nf(:,:)  
 INTEGER,INTENT(OUT)::g(:)
 INTEGER::i,k,nod,nodof 
 nod=UBOUND(num,1) 
 nodof=UBOUND(nf,1)
 DO i=1,nod
   k=i*nodof
   g(k-nodof+1:k)=nf(:,num(i))
 END DO
RETURN
END SUBROUTINE num_to_g   

SUBROUTINE sample(element,s,wt)
!
! This subroutine returns the local coordinates and weighting coefficients
! of the integrating points.
!
 IMPLICIT NONE
 INTEGER,PARAMETER::iwp=SELECTED_REAL_KIND(15)
 REAL(iwp),INTENT(OUT)::s(:,:)
 REAL(iwp),INTENT(OUT),OPTIONAL::wt(:)
 CHARACTER(*),INTENT(IN)::element
 INTEGER::nip
 REAL(iwp)::root3,r15,w(3),v(9),b,c
 root3=1.0_iwp/SQRT(3.0_iwp)
 r15=0.2_iwp*SQRT(15.0_iwp)
 nip=UBOUND(s,1)
 w=(/5.0_iwp/9.0_iwp,8.0_iwp/9.0_iwp,5.0_iwp/9.0_iwp/)
 v=(/5.0_iwp/9.0_iwp*w,8.0_iwp/9.0_iwp*w,5.0_iwp/9.0_iwp*w/)
 SELECT CASE(element)
 CASE('line')
   SELECT CASE(nip)
   CASE(1)
     s(1,1)=0.0_iwp
     wt(1) =2.0_iwp
   CASE(2)
     s(1,1)=-0.577350269189626_iwp
     s(2,1)= 0.577350269189626_iwp
     wt(1) = 1.000000000000000_iwp
     wt(2) = 1.000000000000000_iwp
   CASE(3)
     s(1,1)=-0.774596669241484_iwp
     s(2,1)= 0.000000000000000_iwp
     s(3,1)= 0.774596669241484_iwp
     wt(1) = 0.555555555555556_iwp
     wt(2) = 0.888888888888889_iwp
     wt(3) = 0.555555555555556_iwp
   CASE(4)
     s(1,1)=-0.861136311594053_iwp
     s(2,1)=-0.339981043584856_iwp
     s(3,1)= 0.339981043584856_iwp
     s(4,1)= 0.861136311594053_iwp
     wt(1) = 0.347854845137454_iwp
     wt(2) = 0.652145154862546_iwp
     wt(3) = 0.652145154862546_iwp
     wt(4) = 0.347854845137454_iwp
   CASE(5)
     s(1,1)=-0.906179845938664_iwp
     s(2,1)=-0.538469310105683_iwp
     s(3,1)= 0.000000000000000_iwp
     s(4,1)= 0.538469310105683_iwp
     s(5,1)= 0.906179845938664_iwp
     wt(1) = 0.236926885056189_iwp
     wt(2) = 0.478628670499366_iwp
     wt(3) = 0.568888888888889_iwp
     wt(4) = 0.478628670499366_iwp
     wt(5) = 0.236926885056189_iwp
   CASE(6)
     s(1,1)=-0.932469514203152_iwp
     s(2,1)=-0.661209386466265_iwp
     s(3,1)=-0.238619186083197_iwp
     s(4,1)= 0.238619186083197_iwp
     s(5,1)= 0.661209386466265_iwp
     s(6,1)= 0.932469514203152_iwp
     wt(1) = 0.171324492379170_iwp
     wt(2) = 0.360761573048139_iwp
     wt(3) = 0.467913934572691_iwp
     wt(4) = 0.467913934572691_iwp
     wt(5) = 0.360761573048139_iwp
     wt(6) = 0.171324492379170_iwp
   CASE(7)
     s(1,1)=-0.9491079123427585245261897_iwp
     s(2,1)=-0.7415311855993944398638648_iwp
     s(3,1)=-0.4058451513773971669066064_iwp
     s(4,1)= 0.000000000000000_iwp
     s(5,1)= 0.4058451513773971669066064_iwp
     s(6,1)= 0.7415311855993944398638648_iwp
     s(7,1)= 0.9491079123427585245261897_iwp
     wt(1) = 0.1294849661688696932706114_iwp
     wt(2) = 0.2797053914892766679014678_iwp
     wt(3) = 0.3818300505051189449503698_iwp
     wt(4) = 0.4179591836734693877551020_iwp
     wt(5) = 0.3818300505051189449503698_iwp
     wt(6) = 0.2797053914892766679014678_iwp
     wt(7) = 0.1294849661688696932706114_iwp
   CASE(8)
     s(1,1)=-0.9602898564975362316835609_iwp
     s(2,1)=-0.7966664774136267395915539_iwp
     s(3,1)=-0.5255324099163289858177390_iwp
     s(4,1)=-0.1834346424956498049394761_iwp
     s(5,1)= 0.1834346424956498049394761_iwp
     s(6,1)= 0.5255324099163289858177390_iwp
     s(7,1)= 0.7966664774136267395915539_iwp
     s(8,1)= 0.9602898564975362316835609_iwp
     wt(1) = 0.1012285362903762591525314_iwp
     wt(2) = 0.2223810344533744705443560_iwp
     wt(3) = 0.3137066458778872873379622_iwp
     wt(4) = 0.3626837833783619829651504_iwp
     wt(5) = 0.3626837833783619829651504_iwp
     wt(6) = 0.3137066458778872873379622_iwp
     wt(7) = 0.2223810344533744705443560_iwp
     wt(8) = 0.1012285362903762591525314_iwp
   CASE(9)
     s(1,1)=-0.9681602395076260898355762_iwp
     s(2,1)=-0.8360311073266357942994298_iwp    
     s(3,1)=-0.6133714327005903973087020_iwp
     s(4,1)=-0.3242534234038089290385380_iwp    
     s(5,1)= 0.000000000000000_iwp                            
     s(6,1)= 0.3242534234038089290385380_iwp                            
     s(7,1)= 0.6133714327005903973087020_iwp                            
     s(8,1)= 0.8360311073266357942994298_iwp                            
     s(9,1)= 0.9681602395076260898355762_iwp                            
     wt(1) = 0.0812743883615744119718922_iwp                            
     wt(2) = 0.1806481606948574040584720_iwp                            
     wt(3) = 0.2606106964029354623187429_iwp                            
     wt(4) = 0.3123470770400028400686304_iwp                            
     wt(5) = 0.3302393550012597631645251_iwp                            
     wt(6) = 0.3123470770400028400686304_iwp                            
     wt(7) = 0.2606106964029354623187429_iwp                            
     wt(8) = 0.1806481606948574040584720_iwp                            
     wt(9) = 0.0812743883615744119718922_iwp                            
   CASE(10)
     s(1,1)=-0.9739065285171717200779640_iwp            
     s(2,1)=-0.8650633666889845107320967_iwp 
     s(3,1)=-0.6794095682990244062343274_iwp 
     s(4,1)=-0.4333953941292471907992659_iwp 
     s(5,1)=-0.1488743389816312108848260_iwp 
     s(6,1)= 0.1488743389816312108848260_iwp 
     s(7,1)= 0.4333953941292471907992659_iwp 
     s(8,1)= 0.6794095682990244062343274_iwp 
     s(9,1)= 0.8650633666889845107320967_iwp 
    s(10,1)= 0.9739065285171717200779640_iwp 
     wt(1) = 0.0666713443086881375935688_iwp                     
     wt(2) = 0.1494513491505805931457763_iwp                     
     wt(3) = 0.2190863625159820439955349_iwp                     
     wt(4) = 0.2692667193099963550912269_iwp                     
     wt(5) = 0.2955242247147528701738930_iwp                     
     wt(6) = 0.2955242247147528701738930_iwp                      
     wt(7) = 0.2692667193099963550912269_iwp                     
     wt(8) = 0.2190863625159820439955349_iwp                     
     wt(9) = 0.1494513491505805931457763_iwp                     
    wt(10) = 0.0666713443086881375935688_iwp                     
   CASE DEFAULT                              
     WRITE(*,*)"Wrong number of integrating points for a line"
   END SELECT
 CASE('triangle')
 SELECT CASE(nip)
   CASE(1)
     s(1,1)= 0.333333333333333_iwp
     s(1,2)= 0.333333333333333_iwp
     wt(1) = 0.500000000000000_iwp
   CASE(3)
     s(1,1)= 0.500000000000000_iwp
     s(1,2)= 0.500000000000000_iwp
     s(2,1)= 0.500000000000000_iwp
     s(2,2)= 0.000000000000000_iwp
     s(3,1)= 0.000000000000000_iwp
     s(3,2)= 0.500000000000000_iwp
     wt(1:3)=0.333333333333333_iwp
     wt=0.5_iwp*wt
   CASE(4)
     s(1,1)= 0.6_iwp
     s(1,2)= 0.2_iwp
     s(2,1)= 0.2_iwp
     s(2,2)= 0.6_iwp
     s(3,1)= 0.2_iwp
     s(3,2)= 0.2_iwp
     s(4,1)= 0.333333333333333_iwp
     s(4,2)= 0.333333333333333_iwp
     wt(1:3)= 0.520833333333333_iwp
     wt(4)=  -0.5625_iwp
     wt=0.5_iwp*wt
   CASE(6)
     s(1,1)= 0.816847572980459_iwp
     s(1,2)= 0.091576213509771_iwp
     s(2,1)= 0.091576213509771_iwp
     s(2,2)= 0.816847572980459_iwp
     s(3,1)= 0.091576213509771_iwp
     s(3,2)= 0.091576213509771_iwp
     s(4,1)= 0.108103018168070_iwp
     s(4,2)= 0.445948490915965_iwp
     s(5,1)= 0.445948490915965_iwp
     s(5,2)= 0.108103018168070_iwp
     s(6,1)= 0.445948490915965_iwp
     s(6,2)= 0.445948490915965_iwp
     wt(1:3)=0.109951743655322_iwp
     wt(4:6)=0.223381589678011_iwp
     wt=0.5_iwp*wt
   CASE(7)
     s(1,1)= 0.333333333333333_iwp
     s(1,2)= 0.333333333333333_iwp
     s(2,1)= 0.797426985353087_iwp
     s(2,2)= 0.101286507323456_iwp
     s(3,1)= 0.101286507323456_iwp
     s(3,2)= 0.797426985353087_iwp
     s(4,1)= 0.101286507323456_iwp
     s(4,2)= 0.101286507323456_iwp
     s(5,1)= 0.470142064105115_iwp
     s(5,2)= 0.059715871789770_iwp
     s(6,1)= 0.059715871789770_iwp
     s(6,2)= 0.470142064105115_iwp
     s(7,1)= 0.470142064105115_iwp
     s(7,2)= 0.470142064105115_iwp
     wt(1) = 0.225000000000000_iwp
     wt(2:4)=0.125939180544827_iwp
     wt(5:7)=0.132394152788506_iwp
     wt=0.5_iwp*wt
   CASE(12)
     s(1,1)= 0.873821971016996_iwp
     s(1,2)= 0.063089014491502_iwp
     s(2,1)= 0.063089014491502_iwp
     s(2,2)= 0.873821971016996_iwp
     s(3,1)= 0.063089014491502_iwp
     s(3,2)= 0.063089014491502_iwp
     s(4,1)= 0.501426509658179_iwp
     s(4,2)= 0.249286745170910_iwp
     s(5,1)= 0.249286745170910_iwp
     s(5,2)= 0.501426509658179_iwp
     s(6,1)= 0.249286745170910_iwp
     s(6,2)= 0.249286745170910_iwp
     s(7,1) =0.053145049844817_iwp
     s(7,2) =0.310352451033784_iwp
     s(8,1) =0.310352451033784_iwp
     s(8,2) =0.053145049844817_iwp
     s(9,1) =0.053145049844817_iwp
     s(9,2) =0.636502499121398_iwp
     s(10,1)=0.310352451033784_iwp
     s(10,2)=0.636502499121398_iwp
     s(11,1)=0.636502499121398_iwp
     s(11,2)=0.053145049844817_iwp
     s(12,1)=0.636502499121398_iwp
     s(12,2)=0.310352451033784_iwp
     wt(1:3)=0.050844906370207_iwp
     wt(4:6)=0.116786275726379_iwp
     wt(7:12)=0.082851075618374_iwp
     wt=0.5_iwp*wt
   CASE(16)
     s(1,1)=0.333333333333333_iwp
     s(1,2)=0.333333333333333_iwp
     s(2,1)=0.658861384496478_iwp
     s(2,2)=0.170569307751761_iwp
     s(3,1)=0.170569307751761_iwp
     s(3,2)=0.658861384496478_iwp
     s(4,1)=0.170569307751761_iwp
     s(4,2)=0.170569307751761_iwp
     s(5,1)=0.898905543365938_iwp
     s(5,2)=0.050547228317031_iwp
     s(6,1)=0.050547228317031_iwp
     s(6,2)=0.898905543365938_iwp
     s(7,1)=0.050547228317031_iwp
     s(7,2)=0.050547228317031_iwp
     s(8,1)=0.081414823414554_iwp
     s(8,2)=0.459292588292723_iwp
     s(9,1)=0.459292588292723_iwp
     s(9,2)=0.081414823414554_iwp
     s(10,1)=0.459292588292723_iwp
     s(10,2)=0.459292588292723_iwp
     s(11,1)=0.008394777409958_iwp
     s(11,2)=0.263112829634638_iwp
     s(12,1)=0.008394777409958_iwp
     s(12,2)=0.728492392955404_iwp
     s(13,1)=0.263112829634638_iwp
     s(13,2)=0.008394777409958_iwp
     s(14,1)=0.263112829634638_iwp
     s(14,2)=0.728492392955404_iwp
     s(15,1)=0.728492392955404_iwp
     s(15,2)=0.008394777409958_iwp
     s(16,1)=0.728492392955404_iwp
     s(16,2)=0.263112829634638_iwp
     wt(1)=0.144315607677787_iwp
     wt(2:4)=0.103217370534718_iwp
     wt(5:7)=0.032458497623198_iwp
     wt(8:10)=0.095091634267284_iwp
     wt(11:16)=0.027230314174435_iwp
     wt=0.5_iwp*wt
   CASE DEFAULT
     WRITE(*,*)"wrong number of integrating points for a triangle"
   END SELECT
 CASE('quadrilateral')
   SELECT CASE(nip)
   CASE(1)
     s(1,1)=0.0_iwp
     s(1,2)=0.0_iwp
     wt(1)=4.0_iwp
   CASE(4)
     s(1,1)=-root3
     s(1,2)= root3
     s(2,1)= root3
     s(2,2)= root3
     s(3,1)=-root3
     s(3,2)=-root3
     s(4,1)= root3
     s(4,2)=-root3
     wt=1.0_iwp
   CASE(9)
     s(1:7:3,1)=-r15
     s(2:8:3,1)=0.0_iwp
     s(3:9:3,1)=r15
     s(1:3,2)  =r15
     s(4:6,2)  =0.0_iwp
     s(7:9,2)  =-r15
     wt= v
   CASE(16)
     s(1:13:4,1)=-0.861136311594053_iwp
     s(2:14:4,1)=-0.339981043584856_iwp
     s(3:15:4,1)= 0.339981043584856_iwp
     s(4:16:4,1)= 0.861136311594053_iwp
     s(1:4,2)   = 0.861136311594053_iwp
     s(5:8,2)   = 0.339981043584856_iwp
     s(9:12,2)  =-0.339981043584856_iwp
     s(13:16,2) =-0.861136311594053_iwp
     wt(1)      = 0.121002993285602_iwp
     wt(4)      = wt(1)
     wt(13)     = wt(1)
     wt(16)     = wt(1)
     wt(2)      = 0.226851851851852_iwp
     wt(3)      = wt(2)
     wt(5)      = wt(2)
     wt(8)      = wt(2)
     wt(9)      = wt(2)
     wt(12)     = wt(2)
     wt(14)     = wt(2)
     wt(15)     = wt(2)
     wt(6)      = 0.425293303010694_iwp
     wt(7)      = wt(6)
     wt(10)     = wt(6)
     wt(11)     = wt(6)
   CASE(25)
     s(1:21:5,1)= 0.906179845938664_iwp
     s(2:22:5,1)= 0.538469310105683_iwp
     s(3:23:5,1)= 0.0_iwp
     s(4:24:5,1)=-0.538469310105683_iwp
     s(5:25:5,1)=-0.906179845938664_iwp
     s( 1: 5,2) = 0.906179845938664_iwp
     s( 6:10,2) = 0.538469310105683_iwp
     s(11:15,2) = 0.0_iwp
     s(16:20,2) =-0.538469310105683_iwp
     s(21:25,2) =-0.906179845938664_iwp
     wt(1) =0.056134348862429_iwp
     wt(2) =0.113400000000000_iwp
     wt(3) =0.134785072387521_iwp
     wt(4) =0.113400000000000_iwp
     wt(5) =0.056134348862429_iwp
     wt(6) =0.113400000000000_iwp
     wt(7) =0.229085404223991_iwp
     wt(8) =0.272286532550750_iwp
     wt(9) =0.229085404223991_iwp
     wt(10)=0.113400000000000_iwp
     wt(11)=0.134785072387521_iwp
     wt(12)=0.272286532550750_iwp
     wt(13)=0.323634567901235_iwp
     wt(14)=0.272286532550750_iwp
     wt(15)=0.134785072387521_iwp
     wt(16)=0.113400000000000_iwp
     wt(17)=0.229085404223991_iwp
     wt(18)=0.272286532550750_iwp
     wt(19)=0.229085404223991_iwp
     wt(20)=0.113400000000000_iwp
     wt(21)=0.056134348862429_iwp
     wt(22)=0.113400000000000_iwp
     wt(23)=0.134785072387521_iwp
     wt(24)=0.113400000000000_iwp
     wt(25)=0.056134348862429_iwp
   CASE DEFAULT
     WRITE(*,*)"wrong number of integrating points for a quadrilateral"
   END SELECT
 CASE('tetrahedron')
!                       for tetrahedra weights multiplied by 1/6
   SELECT CASE(nip)
   CASE(1)
     s(1,1)=0.25_iwp
     s(1,2)=0.25_iwp
     s(1,3)=0.25_iwp
     wt(1)=1.0_iwp/6.0_iwp
   CASE(4)
     s(1,1)=0.58541020_iwp
     s(1,2)=0.13819660_iwp
     s(1,3)=s(1,2)
     s(2,2)=s(1,1)
     s(2,3)=s(1,2)
     s(2,1)=s(1,2)
     s(3,3)=s(1,1)
     s(3,1)=s(1,2)
     s(3,2)=s(1,2)
     s(4,1)=s(1,2)
     s(4,2)=s(1,2)
     s(4,3)=s(1,2)
     wt(1:4)=0.25_iwp/6.0_iwp
   CASE(5)
     s(1,1)=0.25_iwp
     s(1,2)=0.25_iwp
     s(1,3)=0.25_iwp
     s(2,1)=0.5_iwp
     s(2,2)=1.0_iwp/6.0_iwp
     s(2,3)=s(2,2)
     s(3,2)=0.5_iwp
     s(3,3)=1.0_iwp/6.0_iwp
     s(3,1)=s(3,3)
     s(4,3)=0.5_iwp
     s(4,1)=1.0_iwp/6.0_iwp
     s(4,2)=s(4,1)
     s(5,1)=1.0_iwp/6.0_iwp
     s(5,2)=s(5,1)
     s(5,3)=s(5,1)
     wt(1)=-0.8_iwp
     wt(2)=9.0_iwp/20.0_iwp
     wt(3:5)=wt(2)
     wt=wt/6.0_iwp
   CASE DEFAULT
     WRITE(*,*)"wrong number of integrating points for a tetrahedron"
   END SELECT
 CASE('hexahedron')
   SELECT CASE(nip)
   CASE(1)
     s(1,1:3)=0.0_iwp
     wt(1)=8.0_iwp
   CASE(8)
     s(1,1)= root3
     s(1,2)= root3
     s(1,3)= root3
     s(2,1)= root3
     s(2,2)= root3
     s(2,3)=-root3
     s(3,1)= root3
     s(3,2)=-root3
     s(3,3)= root3
     s(4,1)= root3
     s(4,2)=-root3
     s(4,3)=-root3
     s(5,1)=-root3
     s(5,2)= root3
     s(5,3)= root3
     s(6,1)=-root3
     s(6,2)=-root3
     s(6,3)= root3
     s(7,1)=-root3
     s(7,2)= root3
     s(7,3)=-root3
     s(8,1)=-root3
     s(8,2)=-root3
     s(8,3)=-root3
     wt=1.0_iwp
   CASE(14)
     b=0.795822426_iwp
     c=0.758786911_iwp
     wt(1:6)=0.886426593_iwp
     wt(7:14)=0.335180055_iwp
     s(1,1)=-b
     s(2,1)=b
     s(3,2)=-b
     s(4,2)=b
     s(5,3)=-b
     s(6,3)=b
     s(7:,:)=c
     s(7,1)=-c
     s(7,2)=-c
     s(7,3)=-c
     s(8,2)=-c
     s(8,3)=-c
     s(9,1)=-c
     s(9,3)=-c
     s(10,3)=-c
     s(11,1)=-c
     s(11,2)=-c
     s(12,2)=-c
     s(13,1)=-c
   CASE(15)
     b=1.0_iwp
     c       =0.674199862_iwp
     wt(1)   =1.564444444_iwp
     wt(2:7) =0.355555556_iwp
     wt(8:15)=0.537777778_iwp
     s(2,1)=-b
     s(3,1)=b
     s(4,2)=-b
     s(5,2)=b
     s(6,3)=-b
     s(7,3)=b
     s(8:,:)=c
     s(8,1)=-c
     s(8,2)=-c
     s(8,3)=-c
     s(9,2)=-c
     s(9,3)=-c
     s(10,1)=-c
     s(10,3)=-c
     s(11,3)=-c
     s(12,1)=-c
     s(12,2)=-c
     s(13,2)=-c
     s(14,1)=-c
   CASE(27)
     wt=(/5.0_iwp/9.0_iwp*v,8.0_iwp/9.0_iwp*v,5.0_iwp/9.0_iwp*v/)
     s(1:7:3,1)=-r15
     s(2:8:3,1)=0.0_iwp
     s(3:9:3,1)=r15
     s(1:3,3)=r15
     s(4:6,3)=0.0_iwp
     s(7:9,3)=-r15
     s(1:9,2)=-r15
     s(10:16:3,1)=-r15
     s(11:17:3,1)=0.0_iwp
     s(12:18:3,1)=r15
     s(10:12,3)=r15
     s(13:15,3)=0.0_iwp
     s(16:18,3)=-r15
     s(10:18,2)=0.0_iwp
     s(19:25:3,1)=-r15
     s(20:26:3,1)=0.0_iwp
     s(21:27:3,1)=r15
     s(19:21,3)= r15
     s(22:24,3)=0.0_iwp
     s(25:27,3)=-r15
     s(19:27,2)= r15
   CASE DEFAULT
     WRITE(*,*)"wrong number of integrating points for a hexahedron"
   END SELECT
 CASE DEFAULT
   WRITE(*,*)"not a valid element type"
 END SELECT
RETURN
END SUBROUTINE sample

SUBROUTINE shape_der(der,points,i)
!
!   This subroutine produces derivatives of shape functions withe respect
!   to local coordinates.
!
 IMPLICIT NONE
 INTEGER,PARAMETER::iwp=SELECTED_REAL_KIND(15)
 INTEGER,INTENT(IN)::i
 REAL(iwp),INTENT(IN)::points(:,:)
 REAL(iwp),INTENT(OUT)::der(:,:)
 REAL(iwp)::eta,xi,zeta,xi0,eta0,zeta0,etam,etap,xim,xip,c1,c2,c3 
 REAL(iwp)::t1,t2,t3,t4,t5,t6,t7,t8,t9,x2p1,x2m1,e2p1,e2m1,zetam,zetap
 REAL,PARAMETER::zero=0.0_iwp,pt125=0.125_iwp,pt25=0.25_iwp,pt5=0.5_iwp,  &
   pt75=0.75_iwp,one=1.0_iwp,two=2.0_iwp,d3=3.0_iwp,d4=4.0_iwp,d5=5.0_iwp,&
   d6=6.0_iwp,d8=8.0_iwp,d9=9.0_iwp,d10=10.0_iwp,d11=11.0_iwp,            &
   d12=12.0_iwp,d16=16.0_iwp,d18=18.0_iwp,d27=27.0_iwp,d32=32.0_iwp,      &
   d36=36.0_iwp,d54=54.0_iwp,d64=64.0_iwp,d128=128.0_iwp
 INTEGER::xii(20),etai(20),zetai(20),l,ndim,nod
 ndim=UBOUND(der,1)
 nod= UBOUND(der,2)
 SELECT CASE(ndim)
 CASE(1)   ! one dimensional elements
   xi=points(i,1)
   SELECT CASE(nod)
   CASE(2)
     der(1,1)=-pt5 
     der(1,2)= pt5
   CASE(3)
     t1=-one-xi 
     t2=-xi  
     t3=one-xi
     der(1,1)=-(t3+t2)/two  
     der(1,2)=(t3+t1)    
     der(1,3)=-(t2+t1)/two   
   CASE(4)
     t1=-one-xi 
     t2=-one/d3-xi 
     t3=one/d3-xi 
     t4=one-xi
     der(1,1)=-(t3*t4+t2*t4+t2*t3)*d9/d16     
     der(1,2)=(t3*t4+t1*t4+t1*t3)*d27/d16 
     der(1,3)=-(t2*t4+t1*t4+t1*t2)*d27/d16 
     der(1,4)=(t2*t3+t1*t3+t1*t2)*d9/d16   
   CASE(5)
     t1=-one-xi 
     t2=-pt5-xi 
     t3=-xi 
     t4=pt5-xi 
     t5=one-xi
     der(1,1)=-(t3*t4*t5+t2*t4*t5+t2*t3*t5+t2*t3*t4)*two/d3   
     der(1,2)=(t3*t4*t5+t1*t4*t5+t1*t3*t5+t1*t3*t4)*d8/d3
     der(1,3)=-(t2*t4*t5+t1*t4*t5+t1*t2*t5+t1*t2*t4)*d4 
     der(1,4)=(t2*t3*t5+t1*t3*t5+t1*t2*t5+t1*t2*t3)*d8/d3
     der(1,5)=-(t2*t3*t4+t1*t3*t4+t1*t2*t4+t1*t2*t3)*two/d3
   CASE DEFAULT
     WRITE(*,*)"wrong number of nodes in shape_der"        
   END SELECT
 CASE(2)      ! two dimensional elements
   xi=points(i,1)
   eta=points(i,2) 
   c1=xi 
   c2=eta 
   c3=one-c1-c2
   etam=pt25*(one-eta)
   etap=pt25*(one+eta)
   xim= pt25*(one-xi)
   xip= pt25*(one+xi)
   x2p1=two*xi+one 
   x2m1=two*xi-one 
   e2p1=two*eta+one 
   e2m1=two*eta-one
   SELECT CASE(nod)
   CASE(3)
     der(1,1)=one
     der(1,3)=zero
     der(1,2)=-one
     der(2,1)=zero
     der(2,3)=one
     der(2,2)=-one
   CASE(6) 
     der(1,1)=d4*c1-one 
     der(1,6)=d4*c2
     der(1,5)=zero  
     der(1,4)=-d4*c2
     der(1,3)=-(d4*c3-one)
     der(1,2)=d4*(c3-c1)
     der(2,1)=zero
     der(2,6)=d4*c1 
     der(2,5)=d4*c2-one
     der(2,4)=d4*(c3-c2)
     der(2,3)=-(d4*c3-one)  
     der(2,2)=-d4*c1
   CASE(10)                          
     der(1,1)=(d27*c1**2-d18*c1+two)/two
     der(1,9)=(d9*(d6*c1-one)*c2)/two
     der(1,8)=(d9*(d3*c2-one)*c2)/two
     der(1,7)=zero
     der(1,6)=-(d9*(d3*c2-one)*c2)/two
     der(1,5)= (d9*(d6*c1+d6*c2-d5)*c2)/two
     der(1,4)=-(d27*c1**2+d54*c1*c2-d36*c1+d27*c2**2-d36*c2+d11)/two
     der(1,3)= (d9*(d9*c1**2+d12*c1*c2-d10*c1+d3*c2**2-d5*c2+two))/two
     der(1,2)=-(d9*(d9*c1**2+d6*c1*c2-d8*c1-c2+one))/two
     der(1,10)=-d27*(((c2-one)+c1)+c1)*c2
     der(2,1)=zero
     der(2,9)= (d9*(d3*c1-one)*c1)/two
     der(2,8)= (d9*(d6*c2-one)*c1)/two
     der(2,7)=(d27*c2**2-d18*c2+two)/two
     der(2,6)=-(d9*((c1+c2-one)*(d6*c2-one)+(d3*c2-one)*c2))/two
     der(2,5)= (d9*(d3*c1**2+d12*c1*c2-d5*c1+d9*c2**2-d10*c2+two))/two
     der(2,4)=-(d27*c1**2+d54*c1*c2-d36*c1+d27*c2**2-d36*c2+d11)/two
     der(2,3)= (d9*(d6*c1+d6*c2-d5)*c1)/two
     der(2,2)=-(d9*(d3*c1-one)*c1)/two
     der(2,10)=-d27*(((c2-one)+c1)+c2)*c1
   CASE(15)                          
     t1=c1-pt25  
     t2=c1-pt5 
     t3=c1-pt75   
     t4=c2-pt25
     t5=c2-pt5   
     t6=c2-pt75 
     t7=c3-pt25  
     t8=c3-pt5 
     t9=c3-pt75
     der(1,1)=d32/d3*(t2*t3*(t1+c1)+c1*t1*(t3+t2))
     der(1,12)=d128/d3*c2*(t2*(t1+c1)+c1*t1) 
     der(1,11)=d64*c2*t4*(t1+c1)
     der(1,10)=d128/d3*c2*t4*t5  
     der(1,9)=zero 
     der(1,8)=-d128/d3*c2*t4*t5
     der(1,7)=-d64*c2*t4*(t7+c3) 
     der(1,6)=-d128/d3*c2*(t8*(t7+c3)+c3*t7)
     der(1,5)=-d32/d3*(t8*t9*(t7+c3)+c3*t7*(t8+t9))
     der(1,4)=d128/d3*(c3*t7*t8-c1*(t8*(t7+c3)+c3*t7))
     der(1,3)=d64*(c3*t7*(t1+c1)-c1*t1*(t7+c3))
     der(1,2)=d128/d3*(c3*(t2*(t1+c1)+c1*t1)-c1*t1*t2)
     der(1,13)=d128*c2*(c3*(t1+c1)-c1*t1) 
     der(1,15)=d128*c2*t4*(c3-c1)
     der(1,14)=d128*c2*(c3*t7-c1*(t7+c3))
     der(2,1)=zero 
     der(2,12)=d128/d3*c1*t1*t2
     der(2,11)=d64*c1*t1*(t4+c2)
     der(2,10)=d128/d3*c1*(t5*(t4+c2)+c2*t4)
     der(2,9)=d32/d3*(t5*t6*(t4+c2)+c2*t4*(t6+t5))
     der(2,8)=d128/d3*((c3*(t5*(t4+c2)+c2*t4))-c2*t4*t5)
     der(2,7)=d64*(c3*t7*(t4+c2)-c2*t4*(t7+c3))
     der(2,6)=d128/d3*(c3*t7*t8-c2*(t8*(t7+c3)+c3*t7))
     der(2,5)=-d32/d3*(t8*t9*(t7+c3)+c3*t7*(t8+t9))
     der(2,4)=-d128/d3*c1*(t8*(t7+c3)+c3*t7)
     der(2,3)=-d64*c1*t1*(t7+c3)  
     der(2,2)=-d128/d3*c1*t1*t2
     der(2,13)=d128*c1*t1*(c3-c2)
     der(2,15)=d128*c1*(c3*(t4+c2)-c2*t4)
     der(2,14)=d128*c1*(c3*t7-c2*(c3+t7))        
   CASE (4)                                                              
     der(1,1)=-etam
     der(1,2)=-etap
     der(1,3)=etap
     der(1,4)=etam
     der(2,1)=-xim
     der(2,2)=xim
     der(2,3)=xip
     der(2,4)=-xip
   CASE(5)
     der(1,1)=-etam+pt5*xi*(one-eta**2)
     der(1,2)=-etap+pt5*xi*(one-eta**2)
     der(1,3)=etap+pt5*xi*(one-eta**2)
     der(1,4)=etam+pt5*xi*(one-eta**2)
     der(1,5)=-two*xi*(one-eta**2)
     der(2,1)=-xim+pt5*eta*(one-xi**2)
     der(2,2)=xim+pt5*eta*(one-xi**2)
     der(2,3)=xip+pt5*eta*(one-xi**2)
     der(2,4)=-xip+pt5*eta*(one-xi**2)
     der(2,5)=-two*eta*(one-xi**2)
   CASE(8)
     der(1,1)=etam*(two*xi+eta)
     der(1,2)=-d8*etam*etap
     der(1,3)=etap*(two*xi-eta)
     der(1,4)=-d4*etap*xi
     der(1,5)=etap*(two*xi+eta)
     der(1,6)=d8*etap*etam
     der(1,7)=etam*(two*xi-eta)
     der(1,8)=-d4*etam*xi
     der(2,1)=xim*(xi+two*eta)
     der(2,2)=-d4*xim*eta
     der(2,3)=xim*(two*eta-xi)
     der(2,4)=d8*xim*xip
     der(2,5)=xip*(xi+two*eta)
     der(2,6)=-d4*xip*eta
     der(2,7)=xip*(two*eta-xi)
     der(2,8)=-d8*xim*xip   
   CASE(9)
     etam=eta-one
     etap=eta+one
     xim=xi-one
     xip=xi+one
     der(1,1)=pt25*x2m1*eta*etam  
     der(1,2)=-pt5*x2m1*etap*etam
     der(1,3)=pt25*x2m1*eta*etap  
     der(1,4)=-xi*eta*etap
     der(1,5)=pt25*x2p1*eta*etap  
     der(1,6)=-pt5*x2p1*etap*etam
     der(1,7)=pt25*x2p1*eta*etam  
     der(1,8)=-xi*eta*etam
     der(1,9)=two*xi*etap*etam    
     der(2,1)=pt25*xi*xim*e2m1
     der(2,2)=-xi*xim*eta        
     der(2,3)=pt25*xi*xim*e2p1
     der(2,4)=-pt5*xip*xim*e2p1   
     der(2,5)=pt25*xi*xip*e2p1
     der(2,6)=-xi*xip*eta        
     der(2,7)=pt25*xi*xip*e2m1
     der(2,8)=-pt5*xip*xim*e2m1   
     der(2,9)=two*xip*xim*eta
   CASE DEFAULT
     WRITE(*,*)"wrong number of nodes in shape_der"        
   END SELECT
 CASE(3)  ! d3 dimensional elements
   xi=points(i,1)
   eta=points(i,2)
   zeta=points(i,3)
   etam=one-eta 
   xim=one-xi
   zetam=one-zeta
   etap=eta+one 
   xip=xi+one 
   zetap=zeta+one
   SELECT CASE(nod)
   CASE(4)
     der(1:3,1:4)=zero
     der(1,1)=one
     der(2,2)=one  
     der(3,3)=one
     der(1,4)=-one 
     der(2,4)=-one 
     der(3,4)=-one  
   CASE(8)
     der(1,1)=-pt125*etam*zetam    
     der(1,2)=-pt125*etam*zetap
     der(1,3)= pt125*etam*zetap     
     der(1,4)= pt125*etam*zetam
     der(1,5)=-pt125*etap*zetam    
     der(1,6)=-pt125*etap*zetap
     der(1,7)= pt125*etap*zetap     
     der(1,8)= pt125*etap*zetam
     der(2,1)=-pt125*xim*zetam     
     der(2,2)=-pt125*xim*zetap
     der(2,3)=-pt125*xip*zetap     
     der(2,4)=-pt125*xip*zetam
     der(2,5)= pt125*xim*zetam      
     der(2,6)= pt125*xim*zetap
     der(2,7)= pt125*xip*zetap      
     der(2,8)= pt125*xip*zetam
     der(3,1)=-pt125*xim*etam      
     der(3,2)= pt125*xim*etam
     der(3,3)= pt125*xip*etam       
     der(3,4)=-pt125*xip*etam
     der(3,5)=-pt125*xim*etap      
     der(3,6)= pt125*xim*etap
     der(3,7)= pt125*xip*etap       
     der(3,8)=-pt125*xip*etap  
   CASE(14) ! type 6 element
     der(1,1)= (two*xi*eta+two*xi*zeta+d4*xi+eta*zeta+eta+zeta)*          &
       (eta-one)*(zeta-one)/d8
     der(1,2)=-(two*xi*eta-two*xi*zeta+d4*xi-eta*zeta+eta-zeta)*          &
       (eta-one)*(zeta+one)/d8
     der(1,3)=-(two*xi*eta-two*xi*zeta+d4*xi+eta*zeta-eta+zeta)*          &
       (eta-one)*(zeta+one)/d8
     der(1,4)= (two*xi*eta+two*xi*zeta+d4*xi-eta*zeta-eta-zeta)*          &
       (eta-one)*(zeta-one)/d8
     der(1,5)= -(eta-one)*(zeta+one)*(zeta-one)*xi 
     der(1,6)=-(eta+one)*(eta-one)*(zeta+one)*(zeta-one)/two
     der(1,7)=  (eta+one)*(eta-one)*(zeta+one)*xi
     der(1,8)= (eta+one)*(eta-one)*(zeta+one)*(zeta-one)/two
     der(1,9)= -(eta+one)*(eta-one)*(zeta-one)*xi  
     der(1,10)= (two*xi*eta-two*xi*zeta-d4*xi+eta*zeta+eta-zeta)*         &
       (eta+one)*(zeta-one)/d8
     der(1,11)=-(two*xi*eta+two*xi*zeta-d4*xi-eta*zeta+eta+zeta)*         &
       (eta+one)*(zeta+one)/d8
     der(1,12)=-(two*xi*eta+two*xi*zeta-d4*xi+eta*zeta-eta-zeta)*         &
       (eta+one)*(zeta+one)/d8
     der(1,13)= (two*xi*eta-two*xi*zeta-d4*xi-eta*zeta-eta+zeta)*         &
       (eta+one)*(zeta-one)/d8
     der(1,14)=  (eta+one)*(zeta+one)*(zeta-one)*xi
     der(2,1)= (two*xi*eta+xi*zeta+xi+two*eta*zeta+d4*eta+zeta)*          &
       (xi-one)*(zeta-one)/d8                                  
     der(2,2)=-(two*xi*eta-xi*zeta+xi-two*eta*zeta+d4*eta-zeta)*          &
       (xi-one)*(zeta+one)/d8
     der(2,3)=-(two*xi*eta-xi*zeta+xi+two*eta*zeta-d4*eta+zeta)*          &
       (xi+one)*(zeta+one)/d8
     der(2,4)= (two*xi*eta+xi*zeta+xi-two*eta*zeta-d4*eta-zeta)*          &
       (xi+one)*(zeta-one)/d8
     der(2,5)=-(xi+one)*(xi-one)*(zeta+one)*(zeta-one)/two
     der(2,6)= -(xi-one)*(zeta+one)*(zeta-one)*eta
     der(2,7)=  (xi+one)*(xi-one)*(zeta+one)*eta
     der(2,8)=  (xi+one)*(zeta+one)*(zeta-one)*eta
     der(2,9)= -(xi+one)*(xi-one)*(zeta-one)*eta
     der(2,10)= (two*xi*eta-xi*zeta-xi+two*eta*zeta+d4*eta-zeta)*         &
       (xi-one)*(zeta-one)/d8
     der(2,11)=-(two*xi*eta+xi*zeta-xi-two*eta*zeta+d4*eta+zeta)*         &
       (xi-one)*(zeta+one)/d8
     der(2,12)=-(two*xi*eta+xi*zeta-xi+two*eta*zeta-d4*eta-zeta)*         &
       (xi+one)*(zeta+one)/d8
     der(2,13)= (two*xi*eta-xi*zeta-xi-two*eta*zeta-d4*eta+zeta)*         &
       (xi+one)*(zeta-one)/d8
     der(2,14)= (xi+one)*(xi-one)*(zeta+one)*(zeta-one)/two
     der(3,1)= (xi*eta+two*xi*zeta+xi+two*eta*zeta+eta+d4*zeta)*          &
       (xi-one)*(eta-one)/d8
     der(3,2)=-(xi*eta-two*xi*zeta+xi-two*eta*zeta+eta-d4*zeta)*          &
       (xi-one)*(eta-one)/d8
     der(3,3)=-(xi*eta-two*xi*zeta+xi+two*eta*zeta-eta+d4*zeta)*          &
       (xi+one)*(eta-one)/d8
     der(3,4)= (xi*eta+two*xi*zeta+xi-two*eta*zeta-eta-d4*zeta)*          &
       (xi+one)*(eta-one)/d8
     der(3,5)= -(xi+one)*(xi-one)*(eta-one)*zeta  
     der(3,6)= -(xi-one)*(eta+one)*(eta-one)*zeta  
     der(3,7)= (xi+one)*(xi-one)*(eta+one)*(eta-one)/two
     der(3,8)=  (xi+one)*(eta+one)*(eta-one)*zeta
     der(3,9)=-(xi+one)*(xi-one)*(eta+one)*(eta-one)/two
     der(3,10)= (xi*eta-two*xi*zeta-xi+two*eta*zeta+eta-d4*zeta)*         &
       (xi-one)*(eta+one)/d8
     der(3,11)=-(xi*eta+two*xi*zeta-xi-two*eta*zeta+eta+d4*zeta)*         &
       (xi-one)*(eta+one)/d8
     der(3,12)=-(xi*eta+two*xi*zeta-xi+two*eta*zeta-eta-d4*zeta)*         &
       (xi+one)*(eta+one)/d8
     der(3,13)= (xi*eta-two*xi*zeta-xi-two*eta*zeta-eta+d4*zeta)*         &
       (xi+one)*(eta+one)/d8
     der(3,14)=  (xi+one)*(xi-one)*(eta+one)*zeta
   CASE(20)
     xii=(/-1,-1,-1,0,1,1,1,0,-1,-1,1,1,-1,-1,-1,0,1,1,1,0/)
     etai=(/-1,-1,-1,-1,-1,-1,-1,-1,0,0,0,0,1,1,1,1,1,1,1,1/)
     zetai=(/-1,0,1,1,1,0,-1,-1,-1,1,1,-1,-1,0,1,1,1,0,-1,-1/)
     DO l=1,20
       xi0=xi*xii(l)
       eta0=eta*etai(l)
       zeta0=zeta*zetai(l)
       IF(l==4.OR.l==8.OR.l==16.OR.l==20)THEN
         der(1,l)=-pt5*xi*(one+eta0)*(one+zeta0)
         der(2,l)=pt25*etai(l)*(one-xi*xi)*(one+zeta0)
         der(3,l)=pt25*zetai(l)*(one-xi*xi)*(one+eta0)
       ELSE IF(l>=9.AND.l<=12)THEN
         der(1,l)=pt25*xii(l)*(one-eta*eta)*(one+zeta0)
         der(2,l)=-pt5*eta*(one+xi0)*(one+zeta0)
         der(3,l)=pt25*zetai(l)*(one+xi0)*(one-eta*eta)
       ELSE IF(l==2.OR.l==6.OR.l==14.OR.l==18) THEN
         der(1,l)=pt25*xii(l)*(one+eta0)*(one-zeta*zeta)
         der(2,l)=pt25*etai(l)*(one+xi0)*(one-zeta*zeta)
         der(3,l)=-pt5*zeta*(one+xi0)*(one+eta0)
       ELSE
         der(1,l)=pt125*xii(l)*(one+eta0)*(one+zeta0)*                    &
           (two*xi0+eta0+zeta0-one)
         der(2,l)=pt125*etai(l)*(one+xi0)*(one+zeta0)*                    &
           (xi0+two*eta0+zeta0-one)
         der(3,l)=pt125*zetai(l)*(one+xi0)*(one+eta0)*                    &
           (xi0+eta0+two*zeta0-one)
       END IF
     END DO 
   CASE DEFAULT
     WRITE(*,*)"wrong number of nodes in shape_der"        
   END SELECT
 CASE DEFAULT
   WRITE(*,*)"wrong number of dimensions in shape_der"
 END SELECT
RETURN
END SUBROUTINE shape_der

SUBROUTINE shape_fun(fun,points,i)
!
!   This subroutine computes the values of the shape functions.
!   to local coordinates
!
 IMPLICIT NONE
 INTEGER,PARAMETER::iwp=SELECTED_REAL_KIND(15)
 INTEGER,INTENT(in)::i
 REAL(iwp),INTENT(IN)::points(:,:)
 REAL(iwp),INTENT(OUT)::fun(:)
 REAL(iwp)::eta,xi,etam,etap,xim,xip,zetam,zetap,c1,c2,c3     
 REAL(iwp)::t1,t2,t3,t4,t5,t6,t7,t8,t9
 REAL(iwp)::zeta,xi0,eta0,zeta0
 INTEGER::xii(20),etai(20),zetai(20),l,ndim,nod
 REAL,PARAMETER::pt125=0.125_iwp,pt25=0.25_iwp,pt5=0.5_iwp,pt75=0.75_iwp, &
   one=1.0_iwp,two=2.0_iwp,d3=3.0_iwp,d4=4.0_iwp,d8=8.0_iwp,d9=9.0_iwp,   &
   d16=16.0_iwp,d27=27.0_iwp,d32=32.0_iwp,d64=64.0_iwp,d128=128.0_iwp
 ndim=UBOUND(points,2)
 nod=UBOUND(fun,1)  
 SELECT CASE(ndim)
 CASE(1) ! one dimensional case
   xi=points(i,1)
   SELECT CASE(nod)
   CASE(2)
     t1=-one-xi 
     t2= one-xi
     fun(1)=t2/two 
     fun(2)=-t1/two
   CASE(3)
     t1=-one-xi 
     t2=-xi 
     t3=one-xi
     fun(1)=t2*t3/two 
     fun(2)=-t1*t3 
     fun(3)=t1*t2/two
   CASE(4)
     t1=-one-xi 
     t2=-one/d3-xi 
     t3=one/d3-xi 
     t4=one-xi
     fun(1)=t2*t3*t4*d9/d16  
     fun(2)=-t1*t3*t4*d27/d16
     fun(3)=t1*t2*t4*d27/d16 
     fun(4)=-t1*t2*t3*d9/d16
   CASE(5)
     t1=-one -xi 
     t2=-pt5-xi 
     t3=-xi 
     t4=pt5-xi 
     t5=one-xi
     fun(1)=t2*t3*t4*t5*two/d3 
     fun(2)=-t1*t3*t4*t5*d8/d3
     fun(3)=t1*t2*t4*t5*d4 
     fun(4)=-t1*t2*t3*t5*d8/d3
     fun(5)=t1*t2*t3*t4*two/d3
   CASE DEFAULT
     WRITE(*,*)"wrong number of nodes in shape_fun"
   END SELECT
 CASE(2) ! two dimensional case
   c1=points(i,1)
   c2=points(i,2)
   c3=one-c1-c2 
   xi=points(i,1)
   eta=points(i,2)
   etam=pt25*(one-eta)
   etap=pt25*(one+eta)
   xim=pt25*(one-xi)
   xip=pt25*(one+xi)
   SELECT CASE(nod)
   CASE(3)
     fun = (/c1,c3,c2/)  
   CASE(6)
     fun(1)=(two*c1-one)*c1 
     fun(2)=d4*c3*c1
     fun(3)=(two*c3-one)*c3 
     fun(4)=d4*c2*c3      
     fun(5)=(two*c2-one)*c2
     fun(6)=d4*c1*c2 
   CASE(10)
     fun(1)= ((d3*c1-one)*(d3*c1-two)*c1)/two
     fun(2)= -(d9*(d3*c1-one)*(c1+c2-one)*c1)/two
     fun(3)=  (d9*(d3*c1+d3*c2-two)*(c1+c2-one)*c1)/two
     fun(4)=-((d3*c1+d3*c2-one)*(d3*c1+d3*c2-two)*(c1+c2-one))/two    
     fun(5)=  (d9*(d3*c1+d3*c2-two)*(c1+c2-one)*c2)/two
     fun(6)= -(d9*(c1+c2-one)*(d3*c2-one)*c2)/two
     fun(7)= ((d3*c2-one)*(d3*c2-two)*c2)/two
     fun(8)=  (d9*(d3*c2-one)*c1*c2)/two
     fun(9)=  (d9*(d3*c1-one)*c1*c2)/two
     fun(10)=-d27*((c2-one)+c1)*c1*c2
   CASE(15)
     t1=c1-pt25  
     t2=c1-pt5 
     t3=c1-pt75   
     t4=c2-pt25
     t5=c2-pt5   
     t6=c2-pt75 
     t7=c3-pt25  
     t8=c3-pt5 
     t9=c3-pt75
     fun(1)=d32/d3*c1*t1*t2*t3   
     fun(2)=d128/d3*c3*c1*t1*t2
     fun(3)=d64*c3*c1*t1*t7      
     fun(4)=d128/d3*c3*c1*t7*t8
     fun(5)=d32/d3*c3*t7*t8*t9   
     fun(6)=d128/d3*c2*c3*t7*t8
     fun(7)=d64*c2*c3*t4*t7      
     fun(8)=d128/d3*c2*c3*t4*t5
     fun(9)=d32/d3*c2*t4*t5*t6   
     fun(10)=d128/d3*c1*c2*t4*t5
     fun(11)=d64*c1*c2*t1*t4     
     fun(12)=d128/d3*c1*c2*t1*t2
     fun(13)=d128*c1*c2*t1*c3    
     fun(15)=d128*c1*c2*c3*t4
     fun(14)=d128*c1*c2*c3*t7      
   CASE(4)
     fun=(/d4*xim*etam,d4*xim*etap,d4*xip*etap,d4*xip*etam/)
   CASE(5)
     fun=(/d4*xim*etam-pt25*(one-xi**2)*(one-eta**2),			  &
           d4*xim*etap-pt25*(one-xi**2)*(one-eta**2),			  &
           d4*xip*etap-pt25*(one-xi**2)*(one-eta**2),			  &
           d4*xip*etam-pt25*(one-xi**2)*(one-eta**2),			  &
           (one-xi**2)*(one-eta**2)/)
   CASE(8)
     fun=(/d4*etam*xim*(-xi-eta-one),d32*etam*xim*etap,                   &
           d4*etap*xim*(-xi+eta-one),d32*xim*xip*etap,                    &
           d4*etap*xip*(xi+eta-one), d32*etap*xip*etam,                   &
           d4*xip*etam*(xi-eta-one), d32*xim*xip*etam/)
   CASE(9)
     etam=eta-one
     etap=eta+one
     xim=xi-one
     xip=xi+one
     fun=(/pt25*xi*xim*eta*etam,-pt5*xi*xim*etap*etam,                    &
           pt25*xi*xim*eta*etap,-pt5*xip*xim*eta*etap,                    &
           pt25*xi*xip*eta*etap,-pt5*xi*xip*etap*etam,                    &
           pt25*xi*xip*eta*etam,-pt5*xip*xim*eta*etam,                    &
           xip*xim*etap*etam/)
   CASE DEFAULT
     WRITE(*,*)"wrong number of nodes in shape_fun"
   END SELECT
 CASE(3) ! d3 dimensional case
   xi=points(i,1)
   eta=points(i,2)
   zeta=points(i,3)
   etam=one-eta 
   xim=one-xi  
   zetam=one-zeta
   etap=eta+one 
   xip=xi+one   
   zetap=zeta+one
   SELECT CASE(nod)
   CASE(4)
     fun(1)=xi   
     fun(2)=eta 
     fun(3)=zeta 
     fun(4)=one-fun(1)-fun(2)-fun(3)
   CASE(8)
     fun=(/pt125*xim*etam*zetam,pt125*xim*etam*zetap,                     &
           pt125*xip*etam*zetap,pt125*xip*etam*zetam,                     &
           pt125*xim*etap*zetam,pt125*xim*etap*zetap,                     &
           pt125*xip*etap*zetap,pt125*xip*etap*zetam/)
   CASE(14) ! type 6 element
     fun(1) = (xi*eta+xi*zeta+two*xi+eta*zeta+two*eta+two*zeta+two)*      &
       (xi-one)*(eta-one)*(zeta-one)/d8
     fun(2) =-(xi*eta-xi*zeta+two*xi-eta*zeta+two*eta-two*zeta+two)*      &
       (xi-one)*(eta-one)*(zeta+one)/d8
     fun(3) =-(xi*eta-xi*zeta+two*xi+eta*zeta-two*eta+two*zeta-two)*      &
       (xi+one)*(eta-one)*(zeta+one)/d8
     fun(4) = (xi*eta+xi*zeta+two*xi-eta*zeta-two*eta-two*zeta-two)*      &
       (xi+one)*(eta-one)*(zeta-one)/d8
     fun(5) =-(xi+one)*(xi-one)*(eta-one)*(zeta+one)*(zeta-one)/two
     fun(6) =-(xi-one)*(eta+one)*(eta-one)*(zeta+one)*(zeta-one)/two
     fun(7) = (xi+one)*(xi-one)*(eta+one)*(eta-one)*(zeta+one)/two
     fun(8) = (xi+one)*(eta+one)*(eta-one)*(zeta+one)*(zeta-one)/two
     fun(9) =-(xi+one)*(xi-one)*(eta+one)*(eta-one)*(zeta-one)/two
     fun(10)= (xi*eta-xi*zeta-two*xi+eta*zeta+two*eta-two*zeta-two)*      &
       (xi-one)*(eta+one)*(zeta-one)/d8
     fun(11)=-(xi*eta+xi*zeta-two*xi-eta*zeta+two*eta+two*zeta-two)*      &
       (xi-one)*(eta+one)*(zeta+one)/d8
     fun(12)=-(xi*eta+xi*zeta-two*xi+eta*zeta-two*eta-two*zeta+two)*      &
       (xi+one)*(eta+one)*(zeta+one)/d8
     fun(13)= (xi*eta-xi*zeta-two*xi-eta*zeta-two*eta+two*zeta+two)*      &
       (xi+one)*(eta+one)*(zeta-one)/d8
     fun(14)= (xi+one)*(xi-one)*(eta+one)*(zeta+one)*(zeta-one)/two
   CASE(20)
     xii=(/-1,-1,-1,0,1,1,1,0,-1,-1,1,1,-1,-1,-1,0,1,1,1,0/)
     etai=(/-1,-1,-1,-1,-1,-1,-1,-1,0,0,0,0,1,1,1,1,1,1,1,1/)
     zetai=(/-1,0,1,1,1,0,-1,-1,-1,1,1,-1,-1,0,1,1,1,0,-1,-1/)
     DO l=1,20
       xi0=xi*xii(l)
       eta0=eta*etai(l)
       zeta0=zeta*zetai(l)
       IF(l==4.OR.l==8.OR.l==16.OR.l==20)THEN
         fun(l)=pt25*(one-xi*xi)*(one+eta0)*(one+zeta0)
       ELSE IF(l>=9.AND.l<=12)THEN
         fun(l)=pt25*(one+xi0)*(one-eta*eta)*(one+zeta0)
       ELSE IF(l==2.OR.l==6.OR.l==14.OR.l==18)THEN
         fun(l)=pt25*(one+xi0)*(one+eta0)*(one-zeta*zeta)
       ELSE
         fun(l)=pt125*(one+xi0)*(one+eta0)*(one+zeta0)*(xi0+eta0+zeta0-2)
       END IF
     END DO
   CASE DEFAULT
     WRITE(*,*)"wrong number of nodes in shape_fun"
   END SELECT
 CASE DEFAULT
   WRITE(*,*)"wrong number of dimensions in shape_fun"
 END SELECT
RETURN
END SUBROUTINE shape_fun 

END MODULE library
